// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: EHZ Setup Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias object.value1  : object.bgWaterPaletteTimer
private alias object.value4  : object.deformTimer
private alias object.value5  : object.bgLightDuration
private alias object.value6  : object.bgLightIndex
private alias object.value7  : object.pinkFlowerDuration
private alias object.value8  : object.pinkFlowerIndex
private alias object.value9  : object.redFlowerDuration
private alias object.value10 : object.redFlowerIndex
private alias object.value11 : object.rectFlowerDuration
private alias object.value12 : object.rectFlowerIndex
private alias object.value13 : object.pointyFlowerDuration
private alias object.value14 : object.pointyFlowerIndex

// Game Modes
private alias 0 : MODE_NOSAVE
private alias 1 : MODE_NORMAL
private alias 2 : MODE_TIMEATTACK

// Tracks
private alias 0 : TRACK_STAGE
private alias 1 : TRACK_ACTFINISH
private alias 2 : TRACK_INVINCIBLE
private alias 3 : TRACK_CONTINUE
private alias 4 : TRACK_BOSS
private alias 5 : TRACK_GAMEOVER
private alias 6 : TRACK_DROWNING
private alias 7 : TRACK_SUPER

// Music Events
private alias 25 : SLOT_MUSICEVENT_CHANGE
private alias 26 : SLOT_MUSICEVENT_BOSS

private alias  0 : MUSICEVENT_FADETOBOSS
private alias  1 : MUSICEVENT_FADETOSTAGE
private alias  2 : MUSICEVENT_TRANSITION

private alias 0 : MUSICEVENT_FLAG_NOCHANGE
private alias 1 : MUSICEVENT_FLAG_SPEEDUP
private alias 2 : MUSICEVENT_FLAG_SLOWDOWN

// Function declarations
reserve function EHZSetup_SpeedUpMusic1P
reserve function EHZSetup_SlowDownMusic1P
reserve function EHZSetup_SpeedUpMusic2P
reserve function EHZSetup_SlowDownMusic2P

// Static Values
private value EHZSetup_value33 = false
private alias EHZSetup_value33 : EHZSetup_hasAchievement

// Tables
private table EHZSetup_bgLightFrameTable
	508, 24, 509, 10, 510, 12, 511, 24, 510, 12, 509, 10
end table

private table EHZSetup_pinkFlowerFrameTable
	512, 128, 513, 20, 512, 8, 513, 8, 512, 8, 513, 8
end table

private table EHZSetup_redFlowerFrameTable
	514, 128, 515, 12, 514, 12, 515, 12, 514, 6, 515, 6, 514, 6, 515, 6
end table

private table EHZSetup_rectFlowerFrameTable
	516, 8, 517, 8
end table

private table EHZSetup_pointyFlowerFrameTable
	518, 128, 519, 8, 518, 8, 519, 8, 518, 8, 519, 12, 518, 12, 519, 12
end table

private table EHZSetup_deformationTable
	1, 2, 1, 3, 1, 2, 2, 1, 2, 3, 1, 2, 1, 2, 0, 0
	2, 0, 3, 2, 2, 3, 2, 2, 1, 3, 0, 0, 1, 0, 1, 3
	1, 2, 1, 3, 1, 2, 2, 1, 2, 3, 1, 2, 1, 2, 0, 0
	2, 0, 3, 2, 2, 3, 2, 2, 1, 3, 0, 0, 1, 0, 1, 3
end table

private table EHZSetup_replay_attract_S
	0x440000, 0x2910000
	0b00100000, 1
	0b00101000, 27
	0b00001000, 39
	0b00111000, 1
	0b00101000, 9
	0b00001000, 40
	0b00111000, 1
	0b00101000, 6
	0b00001000, 8
	0b00111000, 1
	0b00101000, 6
	0b00001000, 175
	0b00111000, 1
	0b00101000, 7
	0b00001000, 111
	0b00000000, 2
	0b00000100, 35
	0b00110100, 1
	0b00100100, 7
	0b00000100, 73
	0b00000000, 4
	0b00001000, 4
	0b00111000, 1
	0b00101000, 7
	0b00001000, 68
	0b00000000, 5
	0b00000100, 30
	0b00000000, 8
	0b00000010, 4
	0b00110010, 1
	0b00100010, 1
	0b00110010, 1
	0b00100000, 4
	0b00000000, 15
	0b00001000, 56
	0b00000000, 1
	0b00000010, 32
	0b00110010, 1
	0b00100010, 1
	0b00101010, 1
	0b00101000, 3
	0b00001000, 255
	0b00001000, 97
	0b00000000, 12
	0b00000100, 5
	0b00000000, 26
	0b00001000, 21
	0b00111000, 1
	0b00101000, 9
	0b00001000, 255
	0b00001000, 4
	0b00001010, 3
	0b00000010, 7
	0b00001010, 2
end table

private table EHZSetup_replay_attract_T
	0x440000, 0x2950000
	0b00100000, 13
	0b00101000, 38
	0b00001000, 47
	0b00111000, 1
	0b00101000, 12
	0b00001000, 224
	0b00111000, 1
	0b00101000, 5
	0b00001000, 69
	0b00111000, 1
	0b00101000, 8
	0b00100000, 2
	0b00000000, 2
	0b00001000, 12
	0b00000000, 28
	0b00001000, 16
	0b00000000, 1
	0b00001000, 255
	0b00001000, 5
	0b00111000, 1
	0b00101000, 24
	0b00001000, 218
	0b00111000, 1
	0b00101000, 9
	0b00001000, 50
	0b00000000, 1
	0b00000100, 7
	0b00000000, 16
	0b00001000, 174
	0b00111000, 1
	0b00101000, 12
	0b00001000, 7
	0b00111000, 1
	0b00101000, 8
	0b00001000, 4
	0b00111000, 1
	0b00101000, 5
	0b00001000, 3
	0b00000100, 2
	0b00110100, 1
	0b00100100, 5
	0b00000100, 4
	0b00110100, 1
	0b00100000, 5
	0b00000000, 4
	0b00000100, 1
	0b00110100, 1
	0b00100100, 3
	0b00000100, 4
	0b00110100, 1
	0b00100100, 4
	0b00000100, 5
	0b00110100, 1
	0b00100100, 3
	0b00000100, 1
	0b00000000, 2
	0b00001000, 2
	0b00111000, 1
	0b00101000, 3
	0b00100000, 2
	0b00000000, 1
	0b00001000, 5
	0b00000000, 5
	0b00001000, 3
	0b00111000, 1
	0b00101000, 2
	0b00001000, 1
	0b00000000, 9
	0b00001000, 5
	0b00000000, 15
	0b00001000, 22
	0b00000000, 3
	0b00000100, 2
	0b00110100, 1
	0b00100100, 9
	0b00100000, 2
	0b00101000, 8
	0b00001000, 4
	0b00111000, 1
	0b00101000, 4
	0b00100000, 1
	0b00000000, 3
	0b00110000, 1
	0b00100000, 6
	0b00000000, 4
	0b00110000, 1
	0b00100000, 5
	0b00000000, 1
	0b00001000, 3
	0b00000000, 1
	0b00110000, 1
	0b00100000, 5
	0b00000000, 3
	0b00110000, 1
	0b00100000, 3
	0b00101000, 3
	0b00001000, 2
	0b00000000, 1
	0b00110000, 1
	0b00100000, 5
	0b00000000, 5
	0b00110000, 1
	0b00100000, 4
	0b00000000, 4
	0b00110000, 1
	0b00100000, 4
	0b00000000, 5
	0b00110000, 1
	0b00100000, 4
	0b00000000, 4
	0b00110000, 1
	0b00100000, 5
	0b00000000, 4
	0b00110000, 1
	0b00100000, 4
	0b00000000, 5
	0b00110000, 1
	0b00100000, 4
	0b00000000, 5
	0b00110000, 1
	0b00100000, 4
	0b00000000, 5
	0b00110000, 1
	0b00100000, 5
	0b00000000, 3
	0b00110000, 1
	0b00100000, 4
	0b00000000, 4
	0b00110000, 1
	0b00100000, 5
	0b00000000, 3
	0b00110000, 1
	0b00100000, 5
	0b00000000, 3
	0b00110000, 1
	0b00100000, 6
	0b00000000, 3
	0b00110000, 1
	0b00100000, 4
	0b00000000, 4
	0b00110000, 1
	0b00100000, 4
	0b00000000, 4
	0b00110000, 1
	0b00100000, 4
	0b00000000, 4
	0b00110000, 1
	0b00100000, 4
	0b00101000, 2
	0b00001000, 39
	0b00111000, 1
	0b00101000, 12
	0b00001000, 13
	0b00000000, 17
	0b00001000, 4
	0b00000000, 12
	0b00000100, 11
	0b00001000, 8
end table

private table EHZSetup_replay_attract_K
	0x440000, 0x2910000
	0b00000000, 18
	0b00001000, 77
	0b00111000, 1
	0b00101000, 13
	0b00001000, 70
	0b00111000, 1
	0b00101000, 14
	0b00001000, 3
	0b00000000, 22
	0b00000100, 4
	0b00000000, 3
	0b00110000, 1
	0b00100000, 16
	0b00101000, 3
	0b00001000, 1
	0b00000000, 30
	0b00110000, 1
	0b00100000, 38
	0b00101000, 149
	0b00001000, 11
	0b00000000, 3
	0b00000100, 6
	0b00000000, 4
	0b00000100, 5
	0b00000000, 5
	0b00001000, 13
	0b00000000, 14
	0b00000100, 89
	0b00000000, 6
	0b00001000, 44
	0b00001010, 1
	0b00000010, 13
	0b00000000, 52
	0b00001000, 59
	0b00111000, 1
	0b00101000, 17
	0b00001000, 18
	0b00000000, 4
	0b00000100, 33
	0b00000000, 1
	0b00001000, 105
	0b00111000, 1
	0b00101000, 30
	0b00001000, 45
	0b00000100, 6
	0b00000000, 52
	0b00001000, 6
	0b00111000, 1
	0b00101000, 19
	0b00001000, 54
	0b00001010, 6
	0b00000010, 17
	0b00001010, 2
	0b00001000, 58
	0b00111000, 1
	0b00101000, 4
	0b00001000, 197
	0b00111000, 1
	0b00101001, 10
	0b00001001, 14
	0b00111001, 1
	0b00101001, 40
	0b00100001, 4
	0b00000001, 126
	0b00001001, 12
	0b00001000, 6
	0b00111000, 1
	0b00101000, 12
end table

function EHZSetup_SpeedUpMusic1P
	CheckEqual(object[SLOT_MUSICEVENT_CHANGE].type, TypeName[Music Event])
	temp0 = checkResult
	CheckEqual(object[SLOT_MUSICEVENT_CHANGE].propertyValue, 2)
	temp0 &= checkResult
	CheckEqual(stage.musicFlag, MUSICEVENT_FLAG_NOCHANGE)
	temp0 &= checkResult
	if temp0 == false
		switch music.currentTrack
		case TRACK_STAGE
			SetMusicTrack("Invincibility_F.ogg", TRACK_INVINCIBLE, 30897)
			SwapMusicTrack("EmeraldHill_F.ogg", TRACK_STAGE, 122240, 8000)
			break

		case TRACK_INVINCIBLE
			SetMusicTrack("EmeraldHill_F.ogg", TRACK_STAGE, 122240)
			SwapMusicTrack("Invincibility_F.ogg", TRACK_INVINCIBLE, 30897, 8000)
			break

		case TRACK_BOSS
		case TRACK_DROWNING
		case TRACK_SUPER
			SetMusicTrack("EmeraldHill_F.ogg", TRACK_STAGE, 122240)
			SetMusicTrack("Invincibility_F.ogg", TRACK_INVINCIBLE, 30897)
			break
		end switch
	else
		stage.musicFlag = MUSICEVENT_FLAG_SPEEDUP
	end if
end function


function EHZSetup_SlowDownMusic1P
	CheckEqual(object[SLOT_MUSICEVENT_CHANGE].type, TypeName[Music Event])
	temp0 = checkResult
	CheckEqual(object[SLOT_MUSICEVENT_CHANGE].propertyValue, 2)
	temp0 &= checkResult
	CheckEqual(stage.musicFlag, MUSICEVENT_FLAG_NOCHANGE)
	temp0 &= checkResult

	if temp0 == false
		switch music.currentTrack
		case TRACK_STAGE
			SetMusicTrack("Invincibility.ogg", TRACK_INVINCIBLE, 38679)
			SwapMusicTrack("EmeraldHill.ogg", TRACK_STAGE, 152750, 12500)
			break

		case TRACK_INVINCIBLE
			SetMusicTrack("EmeraldHill.ogg", TRACK_STAGE, 152750)
			SwapMusicTrack("Invincibility.ogg", TRACK_INVINCIBLE, 38679, 12500)
			break

		case TRACK_BOSS
		case TRACK_DROWNING
		case TRACK_SUPER
			SetMusicTrack("EmeraldHill.ogg", TRACK_STAGE, 152750)
			SetMusicTrack("Invincibility.ogg", TRACK_INVINCIBLE, 38679)
			break
		end switch
	else
		stage.musicFlag = MUSICEVENT_FLAG_SLOWDOWN
	end if
end function


function EHZSetup_SpeedUpMusic2P
	CheckEqual(object[SLOT_MUSICEVENT_CHANGE].type, TypeName[Music Event])
	temp0 = checkResult
	CheckEqual(object[SLOT_MUSICEVENT_CHANGE].propertyValue, 2)
	temp0 &= checkResult
	CheckEqual(stage.musicFlag, MUSICEVENT_FLAG_NOCHANGE)
	temp0 &= checkResult

	if temp0 == false
		switch music.currentTrack
		case TRACK_STAGE
			SetMusicTrack("Invincibility_F.ogg", TRACK_INVINCIBLE, 30897)
			SwapMusicTrack("EmeraldHill2_F.ogg", TRACK_STAGE, 79604, 8000)
			break

		case TRACK_INVINCIBLE
			SetMusicTrack("EmeraldHill2_F.ogg", TRACK_STAGE, 79604)
			SwapMusicTrack("Invincibility_F.ogg", TRACK_INVINCIBLE, 30897, 8000)
			break

		case TRACK_BOSS
		case TRACK_DROWNING
		case TRACK_SUPER
			SetMusicTrack("EmeraldHill2_F.ogg", TRACK_STAGE, 79604)
			SetMusicTrack("Invincibility_F.ogg", TRACK_INVINCIBLE, 30897)
			break
		end switch
	else
		stage.musicFlag = MUSICEVENT_FLAG_SPEEDUP
	end if
end function


function EHZSetup_SlowDownMusic2P
	CheckEqual(object[SLOT_MUSICEVENT_CHANGE].type, TypeName[Music Event])
	temp0 = checkResult
	CheckEqual(object[SLOT_MUSICEVENT_CHANGE].propertyValue, 2)
	temp0 &= checkResult
	CheckEqual(stage.musicFlag, MUSICEVENT_FLAG_NOCHANGE)
	temp0 &= checkResult
	if temp0 == false
		switch music.currentTrack
		case TRACK_STAGE
			SetMusicTrack("Invincibility.ogg", TRACK_INVINCIBLE, 38679)
			SwapMusicTrack("EmeraldHill2.ogg", TRACK_STAGE, 99572, 12500)
			break

		case TRACK_INVINCIBLE
			SetMusicTrack("EmeraldHill2.ogg", TRACK_STAGE, 99572)
			SwapMusicTrack("Invincibility.ogg", TRACK_INVINCIBLE, 38679, 12500)
			break

		case TRACK_BOSS
		case TRACK_DROWNING
		case TRACK_SUPER
			SetMusicTrack("EmeraldHill2.ogg", TRACK_STAGE, 99572)
			SetMusicTrack("Invincibility.ogg", TRACK_INVINCIBLE, 38679)
			break
		end switch
	else
		stage.musicFlag = MUSICEVENT_FLAG_SLOWDOWN
	end if
end function


event ObjectMain
	if object.state < 5
		object.bgWaterPaletteTimer++
		if object.bgWaterPaletteTimer == 8
			object.bgWaterPaletteTimer = 0
			GetPaletteEntry(0, 159, temp1)

			GetPaletteEntry(0, 158, temp0)
			SetPaletteEntry(0, 159, temp0)

			GetPaletteEntry(0, 148, temp0)
			SetPaletteEntry(0, 158, temp0)

			GetPaletteEntry(0, 147, temp0)
			SetPaletteEntry(0, 148, temp0)

			SetPaletteEntry(0, 147, temp1)
		end if
	end if

	object.deformTimer++
	if object.deformTimer > 7
		tileLayer[1].deformationOffset++
		object.deformTimer = 0
	end if

	if object.bgLightDuration < 2
		GetTableValue(temp0, object.bgLightIndex, EHZSetup_bgLightFrameTable)

		Copy16x16Tile(502, temp0)
		temp0 += 12

		Copy16x16Tile(503, temp0)
		temp0 += 4

		Copy16x16Tile(504, temp0)
		object.bgLightIndex++
		
		GetTableValue(object.bgLightDuration, object.bgLightIndex, EHZSetup_bgLightFrameTable)
		object.bgLightIndex++

		object.bgLightIndex %= 12
	else
		object.bgLightDuration--
	end if

	if object.pinkFlowerDuration < 2
		GetTableValue(temp0, object.pinkFlowerIndex, EHZSetup_pinkFlowerFrameTable)

		Copy16x16Tile(764, temp0)
		object.pinkFlowerIndex++

		GetTableValue(object.pinkFlowerDuration, object.pinkFlowerIndex, EHZSetup_pinkFlowerFrameTable)
		object.pinkFlowerIndex++

		object.pinkFlowerIndex %= 12
	else
		object.pinkFlowerDuration--
	end if

	if object.redFlowerDuration < 2
		GetTableValue(temp0, object.redFlowerIndex, EHZSetup_redFlowerFrameTable)

		Copy16x16Tile(765, temp0)
		object.redFlowerIndex++

		GetTableValue(object.redFlowerDuration, object.redFlowerIndex, EHZSetup_redFlowerFrameTable)
		object.redFlowerIndex++

		object.redFlowerIndex &= 15
	else
		object.redFlowerDuration--
	end if

	if object.rectFlowerDuration < 2
		GetTableValue(temp0, object.rectFlowerIndex, EHZSetup_rectFlowerFrameTable)

		Copy16x16Tile(766, temp0)
		object.rectFlowerIndex++

		GetTableValue(object.rectFlowerDuration, object.rectFlowerIndex, EHZSetup_rectFlowerFrameTable)
		object.rectFlowerIndex++

		object.rectFlowerIndex &= 3
	else
		object.rectFlowerDuration--
	end if

	if object.pointyFlowerDuration < 2
		GetTableValue(temp0, object.pointyFlowerIndex, EHZSetup_pointyFlowerFrameTable)

		Copy16x16Tile(767, temp0)
		object.pointyFlowerIndex++

		GetTableValue(object.pointyFlowerDuration, object.pointyFlowerIndex, EHZSetup_pointyFlowerFrameTable)
		object.pointyFlowerIndex++

		object.pointyFlowerIndex &= 15
	else
		object.pointyFlowerDuration--
	end if

	if EHZSetup_hasAchievement == false
		if stage.debugMode == false
			if specialStage.emeralds >= 0b01111111
				if options.gameMode == MODE_NOSAVE
					EHZSetup_hasAchievement = true
					CallNativeFunction2(SetAchievement, AchievementName[Early Bird Special], 100) // Get all emeralds in Emerald Hill achievment
				end if

				if options.gameMode == MODE_NORMAL
					arrayPos1 = options.saveSlot
					arrayPos1 <<= 3
					arrayPos1 += 4
					if saveRAM[arrayPos1] < 20 // make sure our save file hasn't been completed, you have to play by the rules and do it on the first go!
						EHZSetup_hasAchievement = true
						CallNativeFunction2(SetAchievement, AchievementName[Early Bird Special], 100)
					end if
				end if
			end if
		end if
	end if
end event


event ObjectStartup
	if stage.activeList != BONUS_STAGE
		if options.vsMode == false
			SetMusicTrack("EmeraldHill.ogg", TRACK_STAGE, 152750)
			SpeedUpMusic = EHZSetup_SpeedUpMusic1P
			SlowDownMusic = EHZSetup_SlowDownMusic1P
		else
			SetMusicTrack("EmeraldHill2.ogg", TRACK_STAGE, 99572)
			SpeedUpMusic = EHZSetup_SpeedUpMusic2P
			SlowDownMusic = EHZSetup_SlowDownMusic2P
		end if
	else
		// Boss Attack Stage, play DEZ track
		SetMusicTrack("DeathEgg.ogg", TRACK_STAGE, true)
	end if

	animalType1 = TypeName[Flicky]
	animalType2 = TypeName[Ricky]
	tileLayer[1].scrollPos = -0x80000

	arrayPos0 = 0
	while arrayPos0 < 576
		temp0 = arrayPos0
		temp0 &= 63
		GetTableValue(stage.deformationData2[arrayPos0], temp0, EHZSetup_deformationTable)
		arrayPos0++
	loop

	object[10].type = TypeName[EHZ Setup]
	object[10].priority = PRIORITY_ACTIVE
	SetPaletteEntry(0, 192, 0x000000) // boss flash colour

	if options.attractMode == true
		switch stage.playerListPos
		case PlayerName[SONIC]
			PlayerObject_ReplayTable = EHZSetup_replay_attract_S
			PlayerObject_ReplaySize = 110
			PlayerObject_ReplayLength = 1620
			break

		case PlayerName[TAILS]
			PlayerObject_ReplayTable = EHZSetup_replay_attract_T
			PlayerObject_ReplaySize = 318
			PlayerObject_ReplayLength = 1800
			break

		case PlayerName[KNUCKLES]
			PlayerObject_ReplayTable = EHZSetup_replay_attract_K
			PlayerObject_ReplaySize = 138
			PlayerObject_ReplayLength = 1800
			break
		end switch

		CallFunction(PlayerObject_InitReplay)
	end if
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
