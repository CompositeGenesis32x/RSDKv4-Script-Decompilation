// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Tube Path Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================
private alias object.value1 : object.targetPlayer
private alias object.value2 : object.nextNode

private alias 0 : TUBEPATH_ENTRY
private alias 1 : TUBEPATH_EXIT
private alias 2 : TUBEPATH_NODE

// Player Aliases
private alias object.state : player.state
private alias object.xpos : player.xpos
private alias object.ypos : player.ypos
private alias object.xvel : player.xvel
private alias object.yvel : player.yvel
private alias object.speed : player.speed
private alias object.gravity : player.gravity
private alias object.animation : player.animation
private alias object.tileCollisions : player.tileCollisions


// ========================
// Events
// ========================

event ObjectMain
	switch object.propertyValue
	case TUBEPATH_ENTRY
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			if player[currentPlayer].state != PlayerObject_Blank
				if player[currentPlayer].gravity == GRAVITY_GROUND
					BoxCollisionTest(C_TOUCH, object.entityPos, -16, -16, 16, 16, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
					if checkResult == true
						player[currentPlayer].state = PlayerObject_Blank
						player[currentPlayer].tileCollisions = false
						if player[currentPlayer].animation != ANI_JUMPING
							PlaySfx(SfxName[Rolling], false)
						end if
						CreateTempObject(TypeName[Tube Path], TUBEPATH_NODE, object.xpos, object.ypos)
						object[tempObjectPos].targetPlayer = currentPlayer
						object[tempObjectPos].nextNode = object.entityPos
						object[tempObjectPos].nextNode++
					end if
				end if
			end if
		next
		break

	case TUBEPATH_EXIT
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			if player[currentPlayer].state == PlayerObject_Blank
				BoxCollisionTest(C_TOUCH, object.entityPos, -16, -16, 16, 16, currentPlayer, 0, 0, 0, 0)
				if checkResult == true
					player[currentPlayer].state = PlayerObject_TubeSwitch
					player[currentPlayer].xpos = object.xpos
					player[currentPlayer].ypos = object.ypos
					player[currentPlayer].yvel = 0
					player[currentPlayer].speed = 0xC0000
					player[currentPlayer].tileCollisions = true
				end if
			end if
		next
		break

	case TUBEPATH_NODE
		currentPlayer = object.targetPlayer
		if player[currentPlayer].state == PlayerObject_Blank
			arrayPos0 = object.nextNode
			temp0 = player[currentPlayer].xpos
			temp0 -= object[arrayPos0].xpos
			temp0 >>= 16
			temp1 = player[currentPlayer].ypos
			temp1 -= object[arrayPos0].ypos
			temp1 >>= 16
			ATan2(temp2, temp0, temp1)
			Cos256(player[currentPlayer].xvel, temp2)
			Sin256(player[currentPlayer].yvel, temp2)
			player[currentPlayer].xvel *= -0xC00
			player[currentPlayer].yvel *= -0xC00
			if object[arrayPos0].type == TypeName[Blank Object]
				temp2 = temp0
				temp2 *= temp0
				temp0 = temp1
				temp0 *= temp1
				temp0 += temp2
				if temp0 < 256
					object.nextNode++
				end if
			end if
		else
			object.type = TypeName[Blank Object]
		end if
		break
	end switch
end event


event RSDKEdit
	if editor.returnVariable == true
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			checkResult = object.propertyValue
			checkResult &= 1
			break
		case 0 // type
			checkResult = object.propertyValue
			checkResult &= 1
			break
		end switch
	else
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			object.propertyValue = editor.variableValue
			object.propertyValue &= 1
			break
		case 0 // type
			object.propertyValue = editor.variableValue
			object.propertyValue &= 1
			break
		end switch
	end if
end event


event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)

	AddEditorVariable("type")
	SetActiveVariable("type")
	AddEnumVariable("Entry", 0)
	AddEnumVariable("Exit", 1)
end event
