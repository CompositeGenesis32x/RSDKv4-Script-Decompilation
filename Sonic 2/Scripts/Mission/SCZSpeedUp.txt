// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: SCZSpeedUp Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// This Object is used in Mission "Tornado Flyby" - M040 - Mission_Zone10

// ========================
// Aliases
// ========================
private alias 61 : TYPE_SCZSPEEDUP

// Object Type Aliases
private alias 49 : TYPE_TURTLOIDSHOT
private alias 51 : TYPE_NEBULABOMB
private alias 59 : TYPE_ASTERONSPIKE
private alias 60 : TYPE_OCTUSSHOT


// ========================
// Events
// ========================

event ObjectUpdate
	arrayPos0 = 0x420
	while arrayPos0 < 0x4A0
		switch object[arrayPos0].type
		case TYPE_TURTLOIDSHOT
			object[arrayPos0].xvel = -0x10000
			break
			
		case TYPE_NEBULABOMB
			object[arrayPos0].yvel += 0x3800
			break
			
		case TYPE_OCTUSSHOT
			if object[arrayPos0].xvel < 0
				object[arrayPos0].xvel = -0x40000
			else
				object[arrayPos0].xvel = 0x40000
			end if
			break
			
		case TYPE_ASTERONSPIKE
			switch object[arrayPos0].propertyValue
			case 0
				object[arrayPos0].yvel = -0x80000
				break
			case 1
				if object[arrayPos0].xvel < 0
					object[arrayPos0].xvel = -0x60000
				else
					object[arrayPos0].xvel = 0x60000
				end if
				object[arrayPos0].yvel = -0x20000
				break
			case 2
				if object[arrayPos0].xvel < 0
					object[arrayPos0].xvel = -0x60000
				else
					object[arrayPos0].xvel = 0x60000
				end if
				object[arrayPos0].yvel = 0x60000
				break
			end switch
			break
		end switch
		arrayPos0++
	loop
end event


event ObjectStartup
	foreach (TypeName[SCZSpeedUp], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].priority = 1
	next
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
