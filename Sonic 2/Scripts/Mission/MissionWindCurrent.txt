// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Wind Current Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// This Object is used in Mission "Quick Step" - M050 - Mission_Zone11

// ========================
// Aliases
// ========================

// Player Aliases
private alias object.gravity		: player.gravity
private alias object.animation		: player.animation
private alias object.speed			: player.speed
private alias object.xpos			: player.xpos
private alias object.ypos			: player.ypos
private alias object.xvel			: player.xvel
private alias object.yvel			: player.yvel
private alias object.state			: player.state
private alias object.tileCollisions	: player.tileCollisions
private alias object.up				: player.up
private alias object.down			: player.down
private alias object.left			: player.left
private alias object.right			: player.right

// ========================
// Function declarations
// ========================

reserve function WFZSetup_StateClinging

// ========================
// Function Definitions
// ========================

// Custom state given to the player in this stage only
// Identical to the (unused) Player_State_Clinging function in the Player Object script
public function WFZSetup_StateClinging
	player.gravity = 1
	if player.animation != ANI_CLINGING
		player.xvel = -0x80000
		player.speed = -0x80000
	else
		player.xvel = 0
		player.speed = 0
	end if

	if player.up == true
		player.ypos -= 0x10000
	else
		if player.down == true
			player.ypos += 0x10000
		end if
	end if

	player.yvel = 0
end function


// ========================
// Events
// ========================


event ObjectUpdate
	temp6 = 3
	
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		CheckLower(player[currentPlayer].xpos, object.value0)
		temp0 = checkResult
		CheckGreater(player[currentPlayer].xpos, object.value1)
		temp0 |= checkResult
		CheckLower(player[currentPlayer].ypos, object.value2)
		temp0 |= checkResult
		CheckGreater(player[currentPlayer].ypos, object.value3)
		temp0 |= checkResult
		
		if temp0 == false
			temp6 = 1
			
			CheckEqual(player[currentPlayer].state, Player_State_GotHit)
			temp0 = checkResult
			CheckEqual(player[currentPlayer].state, Player_State_Hurt)
			temp0 |= checkResult
			CheckEqual(player[currentPlayer].state, Player_State_Death)
			temp0 |= checkResult
			CheckEqual(player[currentPlayer].state, Player_State_Drown)
			temp0 |= checkResult
			CheckEqual(player[currentPlayer].state, WFZSetup_StateClinging)
			temp0 |= checkResult
			
			if temp0 == false
				player[currentPlayer].state = WFZSetup_StateClinging
				player[currentPlayer].animation = ANI_FANROTATE
				player[currentPlayer].tileCollisions = true
			end if
			
			if player[currentPlayer].animation == ANI_FANROTATE
				if player[currentPlayer].xvel == 0
					temp1 = object.value3
					temp1 -= object.value2
					temp1 >>= 1
					temp1 += object.value2
					if player[currentPlayer].ypos < temp1
						player[currentPlayer].ypos += 0x40000
					else
						player[currentPlayer].ypos -= 0x40000
					end if
				end if
			end if
		else
			if player[currentPlayer].state == WFZSetup_StateClinging
				player[currentPlayer].state = Player_State_Air_NoDropDash
				
				player[currentPlayer].xvel = 0x40000
				player[currentPlayer].speed = 0x40000
			end if
		end if
	next
	
	object.priority = temp6
	if object.propertyValue == 0
		object[+1].priority = temp6
	else
		object[-1].priority = temp6
	end if
end event


event ObjectStartup
	foreach (TypeName[Wind Current], arrayPos0, ALL_ENTITIES)
		if object[arrayPos0].propertyValue == 0
			arrayPos1 = arrayPos0
			arrayPos1++
			object[arrayPos0].value0 = object[arrayPos0].xpos
			object[arrayPos0].value1 = object[arrayPos1].xpos
			temp1 = object[arrayPos0].ypos
			temp2 = object[arrayPos1].ypos
		else
			arrayPos1 = arrayPos0
			arrayPos1--
			object[arrayPos0].value0 = object[arrayPos1].xpos
			object[arrayPos0].value1 = object[arrayPos0].xpos
			temp1 = object[arrayPos1].ypos
			temp2 = object[arrayPos0].ypos
		end if
		if temp1 < temp2
			object[arrayPos0].value2 = temp1
			object[arrayPos0].value3 = temp2
		else
			object[arrayPos0].value2 = temp2
			object[arrayPos0].value3 = temp1
		end if
		object[arrayPos0].priority = 3
	next
end event


// ========================
// Editor Events
// ========================

event RSDKEdit
	if editor.returnVariable == true
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			checkResult = object.propertyValue
			break
		case 0 // type
			checkResult = object.propertyValue
			break
		end switch
	else
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			object.propertyValue = editor.variableValue
			break
		case 0 // type
			object.propertyValue = editor.variableValue
			break
		end switch
	end if
end event


event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
	
	AddEditorVariable("type")
	SetActiveVariable("type")
	AddEnumVariable("Use Next Object Slot", 0)
	AddEnumVariable("Use Previous Object Slot", 1)
end event
