// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: C Ledge Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias object.value0 : object.timer
private alias object.value1 : object.delay

private alias 0 : STATE_ACTIVE
private alias 1 : STATE_COLLAPSE
private alias 2 : STATE_PLATFORMDELAY
private alias 3 : STATE_BLANK
private alias 4 : STATE_TILEDELAY
private alias 5 : STATE_TILEFALL

// Function declarations
reserve function CLedge_DebugDraw
reserve function CLedge_DebugSpawn

// Tables
public table CLedge_fragmentSpriteTable
	1, 2, 3, 4, 5, 6
end table

public table CLedge_fragmentDelayTable
	2, 6, 10, 14, 18, 22
end table


function CLedge_DebugDraw
	DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
end function


function CLedge_DebugSpawn
	CreateTempObject(TypeName[C Ledge], 0, object.xpos, object.ypos)
	object[tempObjectPos].direction = object.direction
end function


event ObjectMain
	switch object.state
	case STATE_COLLAPSE
		if object.timer < 10
			object.timer++
		else
			temp0 = 0
			while temp0 < 6
				GetTableValue(temp1, temp0, CLedge_fragmentSpriteTable)
				CreateTempObject(TypeName[C Ledge], temp1, object.xpos, object.ypos)
				object[tempObjectPos].state = 4
				object[tempObjectPos].direction = object.direction
				GetTableValue(object[tempObjectPos].delay, temp0, CLedge_fragmentDelayTable)
				temp0++
			loop
			PlaySfx(SfxName[Ledge Break], false)
			object.timer = 0
			object.state++
		end if
		break

	case STATE_PLATFORMDELAY
		// During this state, the object is invisible, but it's still providing collision for a brief moment before it truly disappears
		if object.timer < 20
			object.timer++
		else
			object.timer = 0
			object.state++
		end if
		break

	// Nothing for state 3 ("STATE_BLANK")

	case STATE_TILEDELAY
		if object.timer < object.delay
			object.timer++
		else
			object.timer = 0
			object.state++
		end if
		break

	case STATE_TILEFALL
		object.ypos += object.yvel
		object.yvel += 0x4000
		if object.outOfBounds == true
			object.type = TypeName[Blank Object]
		end if
		break

	end switch

	if object.state < STATE_TILEDELAY
		if object.outOfBounds == true
			object.state = 0
			object.timer = 0
			object.priority = PRIORITY_ACTIVE_BOUNDS
		end if
	end if

	if object.state < STATE_BLANK
		// In one of the platform states, so act like one and provide collision to the player
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			BoxCollisionTest(C_PLATFORM, object.entityPos, -32, -24, 32, -8, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)

			if checkResult == true
				if object.state == STATE_ACTIVE
					object.state = STATE_COLLAPSE
					object.priority = PRIORITY_ACTIVE
				end if
			end if
		next
	end if
end event


event ObjectDraw
	switch object.state
	case STATE_ACTIVE
	case STATE_COLLAPSE
		// Full platform - simply draw the entire sprite
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		break

	case STATE_TILEDELAY
	case STATE_TILEFALL
		// Tile fragment, draw whatever sprite's assigned to this object
		DrawSpriteFX(object.propertyValue, FX_FLIP, object.xpos, object.ypos)
		break

	end switch
end event


event ObjectStartup
	LoadSpriteSheet("MCZ/Objects.gif")

	SpriteFrame(-32, -24, 64, 48, 136, 66) // 0 - Full platform

	SpriteFrame(  8,  -8, 24, 32, 176, 82) // 1-6 - Individual platform strips
	SpriteFrame(-16,  -8, 24, 32, 152, 82) // (for when it falls apart)
	SpriteFrame( 16, -24, 16, 16, 184, 66)
	SpriteFrame(  0, -24, 16, 16, 168, 66)
	SpriteFrame(-16, -24, 16, 16, 152, 66)
	SpriteFrame(-32, -24, 16, 16, 136, 66)

	foreach (TypeName[Blank Object], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].direction = object[arrayPos0].propertyValue
		object[arrayPos0].direction &= FLIP_X
	next

	SetTableValue(TypeName[C Ledge], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(CLedge_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(CLedge_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("MCZ/Objects.gif")
	SpriteFrame(-32, -24, 64, 48, 136, 66)
end event
