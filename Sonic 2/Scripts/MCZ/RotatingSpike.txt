// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Rotating Spike Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias 48 : TYPE_ROTATINGSPIKE

private alias object.value0 : object.swingAngle
private alias object.value1 : object.spinRate
private alias object.value2 : object.length
private alias object.value3 : object.spikePos // To be thought of as like `object.entityPos`, not as in position
private alias object.value4 : object.spike.x
private alias object.value5 : object.spike.y

private alias object.value17 : debugMode.currentSelection

// Function declarations
reserve function RotatingSpike_DebugDraw
reserve function RotatingSpike_DebugSpawn

// Static Values
private value RotatingSpike_value33 = 0;
private alias RotatingSpike_value33 : RotatingSpike_startDebugID

function RotatingSpike_DebugDraw
	temp7 = debugMode[0].currentSelection
	temp7 -= RotatingSpike_startDebugID
	temp6 = object.direction
	temp6 <<= 8
	DrawSprite(0)
	temp0 = 0
	temp1 = 16
	while temp0 < temp7
		Cos(temp2, temp6)
		temp2 *= temp1
		temp2 <<= 7
		temp2 += object.xpos
		Sin(temp3, temp6)
		temp3 *= temp1
		temp3 <<= 7
		temp3 += object.ypos
		DrawSpriteXY(1, temp2, temp3)
		temp0++
		temp1 += 16
	loop
	Cos(temp2, temp6)
	temp2 *= temp1
	temp2 <<= 7
	temp2 += object.xpos
	Sin(temp3, temp6)
	temp3 *= temp1
	temp3 <<= 7
	temp3 += object.ypos
	DrawSpriteXY(2, temp2, temp3)
end function


function RotatingSpike_DebugSpawn
	CreateTempObject(TypeName[Rotating Spike], 0, object.xpos, object.ypos)

	object[tempObjectPos].swingAngle = object.direction
	object[tempObjectPos].swingAngle <<= 16

	if object.direction == FLIP_NONE
		object[tempObjectPos].spinRate = 256
	else
		object[tempObjectPos].spinRate = -256
	end if

	object[tempObjectPos].length = debugMode[0].currentSelection
	object[tempObjectPos].length -= RotatingSpike_startDebugID
	object[tempObjectPos].length++

	object[tempObjectPos].spikePos = object[tempObjectPos].length
	object[tempObjectPos].spikePos <<= 4
end function


event ObjectMain
	object.swingAngle += object.spinRate
	object.swingAngle &= 0x1FFFF
	temp0 = object.swingAngle
	temp0 >>= 8

	// Calculate the spike ball's position
	Cos(object.spike.x, temp0)
	object.spike.x *= object.spikePos
	object.spike.x <<= 7
	object.spike.x += object.xpos

	Sin(object.spike.y, temp0)
	object.spike.y *= object.spikePos
	object.spike.y <<= 7
	object.spike.y += object.ypos

	// Backup the object position and replace it with the spike ball's position for collision checks
	temp0 = object.xpos
	temp1 = object.ypos
	object.xpos = object.spike.x
	object.ypos = object.spike.y

	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_TOUCH, object.entityPos, -8, -8, 8, 8, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)

		if checkResult == true
			CallFunction(PlayerObject_Hit)
		end if
	next

	// Restore that backed-up position from earlier
	object.xpos = temp0
	object.ypos = temp1
end event


event ObjectDraw
	// Draw the statuc wood post first
	DrawSprite(0)

	// Then draw the chains
	temp0 = 0
	temp1 = 16
	temp4 = object.swingAngle
	temp4 >>= 8
	while temp0 < object.length
		Cos(temp2, temp4)
		temp2 *= temp1
		temp2 <<= 7
		temp2 += object.xpos
		Sin(temp3, temp4)
		temp3 *= temp1
		temp3 <<= 7
		temp3 += object.ypos
		DrawSpriteXY(1, temp2, temp3)
		temp0++
		temp1 += 16
	loop
	temp1 -= 8
	
	// And finally draw the spike ball itself
	DrawSpriteXY(2, object.spike.x, object.spike.y)
end event


event ObjectStartup
	LoadSpriteSheet("MCZ/Objects.gif")

	SpriteFrame(-8, -8, 16, 16, 135, 148) // 0 - Wood block post
	SpriteFrame(-8, -8, 16, 16, 135, 148) // 1 - Chain
	SpriteFrame(-16, -16, 32, 32, 103, 0) // 2 - Spike ball

	foreach (TypeName[Rotating Spike], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].swingAngle = object[arrayPos0].direction
		object[arrayPos0].swingAngle <<= 15 // To make it around 256 or -256

		object[arrayPos0].direction = FLIP_NONE

		object[arrayPos0].spinRate = object[arrayPos0].propertyValue
		object[arrayPos0].spinRate &= 240
		if object[arrayPos0].spinRate >= 128
			object[arrayPos0].spinRate -= 256
		end if
		object[arrayPos0].spinRate <<= 4

		object[arrayPos0].length = object[arrayPos0].propertyValue
		object[arrayPos0].length &= 15

		object[arrayPos0].spikePos = object[arrayPos0].length
		object[arrayPos0].spikePos <<= 4
	next

	// Add the object to the debug item list a few times for different lengths
	temp0 = 0
	RotatingSpike_startDebugID = DebugMode_ObjCount
	RotatingSpike_startDebugID--
	while temp0 < 4
		SetTableValue(TypeName[Rotating Spike], DebugMode_ObjCount, DebugMode_TypesTable)
		SetTableValue(RotatingSpike_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
		SetTableValue(RotatingSpike_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
		DebugMode_ObjCount++
		temp0++
	loop
end event

event RSDKDraw
	DrawSprite(0)
	temp0 = 0
	temp1 = 16
	temp4 = object.swingAngle
	temp4 >>= 8
	while temp0 < object.length
		Cos(temp2, temp4)
		temp2 *= temp1
		temp2 <<= 7
		temp2 += object.xpos
		Sin(temp3, temp4)
		temp3 *= temp1
		temp3 <<= 7
		temp3 += object.ypos
		DrawSpriteXY(1, temp2, temp3)
		temp0++
		temp1 += 16
	loop
	temp1 -= 8
	DrawSpriteXY(2, object.spike.x, object.spike.y)
end event

event RSDKLoad
	LoadSpriteSheet("MCZ/Objects.gif")
	SpriteFrame(-8, -8, 16, 16, 135, 148)
	SpriteFrame(-8, -8, 16, 16, 135, 148)
	SpriteFrame(-16, -16, 32, 32, 103, 0)

	// TODO: rsdkedit stuff idk just give me that re2 early beta already,,
end event
