// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Tornado Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias object.frame : object.propellerFrame // States 0-2
private alias object.frame : object.tornadoFrame // The rest of em

private alias object.value0  : object.timer
private alias object.value1  : object.pilotFrame
private alias object.value2  : object.pilotFrameDelay
private alias object.value3  : object.pilotAnimPos
private alias object.value4  : object.bgAnimTablePos
private alias object.value5  : object.logoShineTimer
private alias object.value6  : object.logoSonicFrame
private alias object.value7  : object.logo2Frame
private alias object.value8  : object.logoShineTablePos
private alias object.value9  : object.bgSuperSonicState
private alias object.value10 : object.bgSuperSonicFrame
private alias object.value11 : object.bgSSonicTablePos
private alias object.value12 : object.bgSuperSonicTimer
private alias object.value13 : object.bgSuperSonicPos.x
private alias object.value14 : object.bgSuperSonicPos.y
private alias object.value15 : object.tailsSuperBlendTimer
private alias object.value16 : object.tailsSuperBlendClr

// States
private alias 0  : TORNADO_SETUP
private alias 1  : TORNADO_PLAYERFALLING
private alias 2  : TORNADO_FLYING
private alias 3  : TORNADO_INBG
private alias 4  : TORNADO_SETUPENDPOSE
private alias 5  : TORNADO_ENDPOSE_FRAME1
private alias 6  : TORNADO_ENDPOSE_FRAME2
private alias 7  : TORNADO_ENDPOSE_FRAME3
private alias 8  : TORNADO_LOGOSONIC_SHINE
private alias 9  : TORNADO_LOGO2_SHINE
private alias 10 : TORNADO_ENDINGSCREEN
private alias 11 : TORNADO_FADETOCREDITS

// BG Super Sonic States
private alias 0 : TORNADO_BGSSONIC_INACTIVE
private alias 1 : TORNADO_BGSSONIC_FLYING
private alias 2 : TORNADO_BGSSONIC_WAITING
private alias 3 : TORNADO_BGSSONIC_INBG
private alias 4 : TORNADO_BGSSONIC_SETUPENDPOSE
private alias 5 : TORNADO_BGSSONIC_PLAYENDPOSE
private alias 6 : TORNADO_BGSSONIC_ENDPOSE


// Bird Aliases
private alias object.value0 : bird.timer

// Ending Setup Values
private alias EndingSetup_value33 : EndingSetup_scrollVelocity.x
private alias EndingSetup_value34 : EndingSetup_scrollVelocity.y

// Function declarations
reserve function Tornado_ProcessTailsSuperPal
reserve function Tornado_AnimatePilot

// Tables
private table Tornado_TailsPilotFrameTable
	5, 32
	6, 8
	7, 8
	8, 8
	7, 8
	6, 16
	9, 32
	6, 16
end table

private table Tornado_SonicPilotFrameTable
	10, 9
	11, 9
	10, 9
	11, 9
	10, 9
	11, 9
	10, 9
	11, 9
end table

public table Tornado_bgAnimFrames_Sonic
	19, 19, 19, 19, 20, 20, 20, 20, 20, 20, 20, 21, 21, 21, 22, 22
	22, 23, 23, 23, 23, 23, 23, 23, 23, 23, 23, 23, 23, 23
end table

public table Tornado_bgAnimFrames_SS_Plane
	12, 12, 12, 12, 13, 13, 13, 13, 13, 13, 13, 14, 14, 14, 15, 15
	15, 16, 16, 16, 16, 16, 16, 16, 16, 16, 16, 16, 16, 16
end table

public table Tornado_bgAnimFrames_Tails
	27, 27, 27, 27, 28, 28, 28, 28, 28, 28, 28, 29, 29, 29, 30, 30
	30, 31, 31, 31, 31, 31, 31, 31, 31, 31, 31, 31, 31, 31
end table

public table Tornado_bgAnimFrames_Knux
	35, 35, 35, 35, 36, 36, 36, 36, 36, 36, 36, 37, 37, 37, 38, 38
	38, 39, 39, 39, 39, 39, 39, 39, 39, 39, 39, 39, 39, 39
end table

public table Tornado_bgAnimPosTable
	0xA00000, 0x700000, 0xB00000, 0x700000, 0xB60000, 0x710000, 0xBC0000, 0x720000, 0xC40000, 0x740000, 0xC80000, 0x750000, 0xCA0000, 0x760000, 0xCC0000, 0x770000
	0xCE0000, 0x780000, 0xD00000, 0x790000, 0xD20000, 0x7A0000, 0xD40000, 0x7B0000, 0xD60000, 0x7C0000, 0xD90000, 0x7E0000, 0xDC0000, 0x810000, 0xDE0000, 0x840000
	0xE10000, 0x870000, 0xE40000, 0x8B0000, 0xE70000, 0x8F0000, 0xEC0000, 0x940000, 0xF00000, 0x990000, 0xF50000, 0x9D0000, 0xF90000, 0xA40000, 0x1000000, 0xAC0000
	0x1080000, 0xB80000, 0x1120000, 0xC40000, 0x11F0000, 0xD30000, 0x12C0000, 0xFA0000
end table

public table Tornado_bgAnimFrames_SS_Sonic
	43, 43, 43, 43, 43, 43, 43, 44, 44, 44, 44, 44, 44, 45, 45, 45
	45, 46, 46, 46, 47, 47, 47, 47, 47, 47, 47, 47, 47, 48, 48, 48
	48, 48, 48, 48, 48, 48
end table

public table Tornado_bgSSonicPosTable
	0xC00000, 0x900000, 0xB00000, 0x910000, 0xA80000, 0x920000, 0x9B0000, 0x960000, 0x990000, 0x980000, 0x980000, 0x990000, 0x990000, 0x9A0000, 0x9B0000, 0x9C0000
	0x9F0000, 0x9E0000, 0xA40000, 0xA00000, 0xAC0000, 0xA20000, 0xB70000, 0xA50000, 0xC40000, 0xA80000, 0xD30000, 0xAB0000, 0xDE0000, 0xAE0000, 0xE80000, 0xB00000
	0xEF0000, 0xB20000, 0xF40000, 0xB50000, 0xF90000, 0xB80000, 0xFC0000, 0xBB0000, 0xFE0000, 0xBE0000, 0xFF0000, 0xC00000, 0x1000000, 0xC20000, 0x1010000, 0xC50000
	0x1020000, 0xC80000, 0x1020000, 0xCC0000, 0x1010000, 0xD10000, 0xFD0000, 0xD70000, 0xF90000, 0xDE0000, 0xF90000, 0x1180000, 0x600000, 0x880000, 0x500000, 0x680000
	0x440000, 0x460000, 0x3C0000, 0x360000, 0x360000, 0x2A0000, 0x330000, 0x240000, 0x310000, 0x200000, 0x300000, 0x1E0000
end table

public table Tornado_endPosePosTable
	-0x3A0000, -0x880000, -0xC0000, -0x220000, -0x80000, -0x100000, -0x40000, -0x80000, -0x20000, -0x40000, -0x10000, -0x20000, -0x10000, -0x20000
end table

public table Tornado_logoSonicFrameTable
	51, 53, 54, 55, 5453, 51
end table

public table Tornado_logo2FrameTable
	51, 56, 57, 58, 59, 58, 57, 56, 51
end table


function Tornado_ProcessTailsSuperPal
	object.tailsSuperBlendTimer++
	if object.tailsSuperBlendTimer >= 12
		object.tailsSuperBlendTimer = 0
		object.tailsSuperBlendClr += 4
		if object.tailsSuperBlendClr >= 24
			object.tailsSuperBlendClr = 0
		end if
	end if

	temp1 = object.tailsSuperBlendClr
	GetTableValue(temp0, temp1, PlayerObject_TailsSuperPal)
	SetPaletteEntry(0, 202, temp0)

	temp1++
	GetTableValue(temp0, temp1, PlayerObject_TailsSuperPal)
	SetPaletteEntry(0, 205, temp0)

	temp1++
	GetTableValue(temp0, temp1, PlayerObject_TailsSuperPal)
	SetPaletteEntry(0, 206, temp0)

	temp1++
	GetTableValue(temp0, temp1, PlayerObject_TailsSuperPal)
	SetPaletteEntry(0, 203, temp0)
end function


function Tornado_AnimatePilot
	object.propellerFrame++
	object.propellerFrame &= 3

	if object.pilotFrameDelay < 2
		if stage.playerListPos == PlayerName[TAILS]
			GetTableValue(object.pilotFrame, object.pilotAnimPos, Tornado_SonicPilotFrameTable)
			object.pilotAnimPos++

			GetTableValue(object.pilotFrameDelay, object.pilotAnimPos, Tornado_SonicPilotFrameTable)
			object.pilotAnimPos++
		else
			GetTableValue(object.pilotFrame, object.pilotAnimPos, Tornado_TailsPilotFrameTable)
			object.pilotAnimPos++

			GetTableValue(object.pilotFrameDelay, object.pilotAnimPos, Tornado_TailsPilotFrameTable)
			object.pilotAnimPos++
		end if

		object.pilotAnimPos &= 15
	else
		object.pilotFrameDelay--
	end if
end function


event ObjectMain
	object.drawOrder = 4
	if stage.playerListPos == PlayerName[TAILS]
		if PlayerObject_SuperState != SUPERSTATE_NONE
			CallFunction(Tornado_ProcessTailsSuperPal)
		end if
	end if

	switch object.state
	case TORNADO_SETUP
		object.xpos += 0x10000
		object.ypos -= 0x8000

		if object.xpos == 0xB40000
			if PlayerObject_SuperState != SUPERSTATE_NONE
				if stage.playerListPos == PlayerName[KNUCKLES]
					object[0].animation = ANI_GLIDING_DROP
				end if
			end if
		end if

		if object.xpos > 0xD40000
			if PlayerObject_SuperState == SUPERSTATE_NONE
				object[0].animation = ANI_STOPPED
			else
				if stage.playerListPos == PlayerName[TAILS]
					object[0].animation = ANI_STOPPED
				end if

				if stage.playerListPos == PlayerName[KNUCKLES]
					object[0].animation = ANI_STOPPED
				end if
			end if

			EndingSetup_scrollVelocity.x = -0x10000
			arrayPos0 = object[34].entityPos
			temp0 = 0
			temp1 = 0
			temp3 = 0
			while temp0 < 20
				object[arrayPos0].type = TypeName[Bird]
				object[arrayPos0].priority = PRIORITY_ACTIVE
				bird[arrayPos0].timer = temp1
				if temp3 == 0
					object[arrayPos0].drawOrder = 3
				else
					object[arrayPos0].drawOrder = 5
				end if
				temp3 ^= 1
				Rand(temp2, 16)
				temp1 += temp2
				arrayPos0++
				temp0++
			loop

			switch stage.playerListPos
			case PlayerName[SONIC]
			case PlayerName[SONIC AND TAILS]
				if specialStage.emeralds >= 0b01111111
					object.bgSuperSonicTimer = 64
					object.bgSuperSonicState = TORNADO_BGSSONIC_FLYING
					stage.newXBoundary2 = screen.xsize
					stage.newYBoundary2 = 376
					stage.newYBoundary2 += screen.ysize
					stage.deathBoundary = stage.newYBoundary2
					stage.deathBoundary <<= 16
				end if
				break
			end switch

			object.state = TORNADO_PLAYERFALLING
		end if
		CallFunction(Tornado_AnimatePilot)
		break

	case TORNADO_PLAYERFALLING
		EndingSetup_scrollVelocity.y += 0x400
		if EndingSetup_scrollVelocity.y >= 0
			object.state = TORNADO_FLYING
		end if

		CallFunction(Tornado_AnimatePilot)
		break

	case TORNADO_FLYING
		object.timer++
		if object.timer >= 1102
			object.timer = 0
			switch stage.playerListPos
			case PlayerName[SONIC]
			case PlayerName[SONIC AND TAILS]
				if PlayerObject_SuperState == SUPERSTATE_NONE
					object[0].visible = false
				end if
				break
			case PlayerName[TAILS]
			case PlayerName[KNUCKLES]
				object[0].visible = false
				break
			end switch
			object.state = TORNADO_INBG
		end if

		CallFunction(Tornado_AnimatePilot)
		break

	case TORNADO_INBG
		switch stage.playerListPos
			case PlayerName[SONIC]
			case PlayerName[SONIC AND TAILS]
			if PlayerObject_SuperState != SUPERSTATE_NONE
				if object.bgSuperSonicState != TORNADO_BGSSONIC_INBG
					object.bgSuperSonicTimer++
					if object.bgSuperSonicTimer >= 90
						object.bgSuperSonicTimer = 0
						object[0].visible = false
						object.bgSuperSonicState = TORNADO_BGSSONIC_INBG
					end if
				end if
			end if
			break
		end switch

		object.timer--
		if object.timer < 0
			object.timer = 2
			switch stage.playerListPos
			case PlayerName[SONIC]
			case PlayerName[SONIC AND TAILS]
				if specialStage.emeralds < 0b01111111
					GetTableValue(object.tornadoFrame, object.bgAnimTablePos, Tornado_bgAnimFrames_Sonic)
				else
					GetTableValue(object.tornadoFrame, object.bgAnimTablePos, Tornado_bgAnimFrames_SS_Plane)
				end if
				break

			case PlayerName[TAILS]
				GetTableValue(object.tornadoFrame, object.bgAnimTablePos, Tornado_bgAnimFrames_Tails)
				break

			case PlayerName[KNUCKLES]
				GetTableValue(object.tornadoFrame, object.bgAnimTablePos, Tornado_bgAnimFrames_Knux)
				break
			end switch

			temp0 = object.bgAnimTablePos
			temp0 <<= 1
			GetTableValue(object.xpos, temp0, Tornado_bgAnimPosTable)
			object.xpos += 0x340000

			temp0++
			GetTableValue(object.ypos, temp0, Tornado_bgAnimPosTable)
			temp0 = screen.cameraY
			temp0 -= screen.ycenter
			temp0 += 32
			temp0 <<= 16
			object.ypos += temp0

			object.bgAnimTablePos++
			if object.bgAnimTablePos >= 28
				object.timer = 0
				object.bgAnimTablePos = 0
				if stage.playerListPos == PlayerName[TAILS]
					object.tornadoFrame = 49
				else
					object.tornadoFrame = 50
				end if
				object.xpos -= 0x220000
				object.ypos += 0x450000
				object.state = TORNADO_SETUPENDPOSE
			end if
		end if
		break

	case TORNADO_SETUPENDPOSE
		switch stage.playerListPos
			case PlayerName[SONIC]
			case PlayerName[SONIC AND TAILS]
			if PlayerObject_SuperState != SUPERSTATE_NONE
				if object.bgSuperSonicState != TORNADO_BGSSONIC_INBG
					object.bgSuperSonicTimer++
					if object.bgSuperSonicTimer >= 90
						object.bgSuperSonicTimer = 0
						object[0].visible = false
						object.bgSuperSonicState = TORNADO_BGSSONIC_INBG
					end if
				end if
			end if
			break
		end switch

		object.timer++
		if object.timer >= 120
			object.timer = 0
			object.state = TORNADO_ENDPOSE_FRAME1
		end if
		break

	case TORNADO_ENDPOSE_FRAME1
		object.timer--
		if object.timer < 0
			object.timer = 2
			temp0 = object.bgAnimTablePos
			temp0 <<= 1
			GetTableValue(temp1, temp0, Tornado_endPosePosTable)
			object.xpos += temp1

			temp0++
			GetTableValue(temp1, temp0, Tornado_endPosePosTable)
			object.ypos += temp1

			object.bgAnimTablePos++
			if object.bgAnimTablePos >= 7
				object.timer = 0
				object.state = TORNADO_ENDPOSE_FRAME2
			end if
		end if
		break

	case TORNADO_ENDPOSE_FRAME2
		object.timer++
		if object.timer == 4
			object.timer = 0
			GetTableValue(object.logoSonicFrame, 0, Tornado_logoSonicFrameTable)
			GetTableValue(object.logo2Frame, 0, Tornado_logo2FrameTable)
			object.state = TORNADO_ENDPOSE_FRAME3
		end if
		break

	case TORNADO_ENDPOSE_FRAME3
		if object.alpha < 0x100
			object.alpha += 4
		end if

		object.timer++
		if object.timer == 90
			object.timer = 0
			object.logoShineTablePos = 0
			object.state = TORNADO_LOGOSONIC_SHINE
		end if
		break

	case TORNADO_LOGOSONIC_SHINE
		object.logoShineTimer++
		if object.logoShineTimer == 2
			object.logoShineTimer = 0

			object.logoShineTablePos++
			GetTableValue(object.logoSonicFrame, object.logoShineTablePos, Tornado_logoSonicFrameTable)
			if object.logoShineTablePos == 6
				object.logoShineTablePos = 0
				object.state = TORNADO_LOGO2_SHINE
			end if
		end if
		break

	case TORNADO_LOGO2_SHINE
		object.logoShineTimer++
		if object.logoShineTimer == 2
			object.logoShineTimer = 0

			object.logoShineTablePos++
			GetTableValue(object.logo2Frame, object.logoShineTablePos, Tornado_logo2FrameTable)
			if object.logoShineTablePos == 9
				object.logoShineTablePos = 0
				object.state = TORNADO_ENDINGSCREEN
			end if
		end if
		break

	case TORNADO_ENDINGSCREEN
		object.timer++
		if object.timer >= 240
			object.timer = 0
			object.state = TORNADO_FADETOCREDITS
		end if
		break

	case TORNADO_FADETOCREDITS
		if object.timer < 320
			object.timer += 12
		else
			stage.activeList = PRESENTATION_STAGE
			stage.listPos = 2
			LoadStage()
		end if

		if object.timer > 0
			temp0 = object.timer
		else
			temp0 = 0
		end if
		SetScreenFade(0x00, 0x00, 0x00, temp0)
		break
	end switch

	switch object.bgSuperSonicState
	case TORNADO_BGSSONIC_INACTIVE
		break

	case TORNADO_BGSSONIC_FLYING
		object[0].xpos += 0x8000
		object[0].ypos += 0x10000

		object.bgSuperSonicTimer--
		if object.bgSuperSonicTimer < 0
			object.bgSuperSonicPos.x = object[0].xpos
			object.bgSuperSonicPos.y = object[0].ypos
			object.bgSuperSonicState = TORNADO_BGSSONIC_WAITING
		end if
		break

	case TORNADO_BGSSONIC_WAITING
		break

	case TORNADO_BGSSONIC_INBG
		object.bgSuperSonicTimer--
		if object.bgSuperSonicTimer < 0
			object.bgSuperSonicTimer = 2
			GetTableValue(object.bgSuperSonicFrame, object.bgSSonicTablePos, Tornado_bgAnimFrames_SS_Sonic)

			temp0 = object.bgSSonicTablePos
			temp0 <<= 1
			GetTableValue(object.bgSuperSonicPos.x, temp0, Tornado_bgSSonicPosTable)
			object.bgSuperSonicPos.x += 0x340000

			temp0++
			GetTableValue(object.bgSuperSonicPos.y, temp0, Tornado_bgSSonicPosTable)
			temp0 = screen.cameraY
			temp0 -= screen.ycenter
			temp0 += 32
			temp0 <<= 16
			object.bgSuperSonicPos.y += temp0

			object.bgSSonicTablePos++
			if object.bgSSonicTablePos >= 30
				object.bgSuperSonicTimer = 0
				object.bgSuperSonicState = TORNADO_BGSSONIC_SETUPENDPOSE
			end if
		end if
		break

	case TORNADO_BGSSONIC_SETUPENDPOSE
		object.bgSuperSonicTimer++
		if object.bgSuperSonicTimer >= 60
			object.bgSuperSonicTimer = 0
			object.bgSuperSonicState = TORNADO_BGSSONIC_PLAYENDPOSE
		end if
		break

	case TORNADO_BGSSONIC_PLAYENDPOSE
		object.bgSuperSonicTimer--
		if object.bgSuperSonicTimer < 0
			object.bgSuperSonicTimer = 2
			GetTableValue(object.bgSuperSonicFrame, object.bgSSonicTablePos, Tornado_bgAnimFrames_SS_Sonic)

			temp0 = object.bgSSonicTablePos
			temp0 <<= 1
			GetTableValue(object.bgSuperSonicPos.x, temp0, Tornado_bgSSonicPosTable)
			object.bgSuperSonicPos.x += 0x340000

			temp0++
			GetTableValue(object.bgSuperSonicPos.y, temp0, Tornado_bgSSonicPosTable)
			temp0 = screen.cameraY
			temp0 -= screen.ycenter
			temp0 += 32
			temp0 <<= 16
			object.bgSuperSonicPos.y += temp0

			object.bgSSonicTablePos++
			if object.bgSSonicTablePos >= 38
				object.bgSuperSonicState = TORNADO_BGSSONIC_ENDPOSE
			end if
		end if
		break

	case TORNADO_BGSSONIC_ENDPOSE
		break
	end switch
end event


event ObjectDraw
	switch object.state
	case TORNADO_SETUP
	case TORNADO_PLAYERFALLING
	case TORNADO_FLYING
		// Draw Tornado
		DrawSprite(object.propellerFrame)
		DrawSprite(4)
		DrawSprite(object.pilotFrame)
		break

	case TORNADO_INBG
		DrawSprite(object.tornadoFrame)
		break

	case TORNADO_SETUPENDPOSE
		break

	case TORNADO_ENDPOSE_FRAME1
		DrawSprite(object.tornadoFrame)
		
		// Draw End Pose (Frame 1)
		temp0 = object.xpos
		temp1 = object.ypos
		switch stage.playerListPos
		case PlayerName[SONIC]
		case PlayerName[SONIC AND TAILS]
			if specialStage.emeralds < 0b01111111
				temp0 -= 0x220000
				temp1 -= 0x2F0000
				DrawSpriteXY(24, temp0, temp1)
			end if
			break

		case PlayerName[TAILS]
			temp0 -= 0x240000
			temp1 -= 0x2D0000
			DrawSpriteXY(32, temp0, temp1)
			break

		case PlayerName[KNUCKLES]
			temp0 -= 0x220000
			temp1 -= 0x2F0000
			DrawSpriteXY(40, temp0, temp1)
			break
		end switch
		break

	case TORNADO_ENDPOSE_FRAME2
		DrawSprite(object.tornadoFrame)
		
		// Draw End Pose (Frame 2)
		temp0 = object.xpos
		temp1 = object.ypos
		switch stage.playerListPos
		case PlayerName[SONIC]
		case PlayerName[SONIC AND TAILS]
			if specialStage.emeralds < 0b01111111
				temp0 -= 0x2A0000
				temp1 -= 0x2F0000
				DrawSpriteXY(25, temp0, temp1)
			end if
			break

		case PlayerName[TAILS]
			temp0 -= 0x2A0000
			temp1 -= 0x250000
			DrawSpriteXY(33, temp0, temp1)
			break

		case PlayerName[KNUCKLES]
			temp0 -= 0x2A0000
			temp1 -= 0x270000
			DrawSpriteXY(41, temp0, temp1)
			break
		end switch
		break

	case TORNADO_ENDPOSE_FRAME3
	case TORNADO_LOGOSONIC_SHINE
	case TORNADO_LOGO2_SHINE
	case TORNADO_ENDINGSCREEN
	case TORNADO_FADETOCREDITS
		// Draw Sonic 2 Logo
		temp0 = screen.xsize
		temp0 -= 79
		temp1 = screen.ysize
		temp1 -= 38
		DrawSpriteScreenFX(52, FX_INK, temp0, temp1)
		DrawSpriteScreenFX(object.logoSonicFrame, FX_INK, temp0, temp1)
		DrawSpriteScreenFX(object.logo2Frame, FX_INK, temp0, temp1)

		DrawSprite(object.tornadoFrame)

		// Draw End Pose (Frame 3)
		temp0 = object.xpos
		temp1 = object.ypos
		switch stage.playerListPos
		case PlayerName[SONIC]
		case PlayerName[SONIC AND TAILS]
			if specialStage.emeralds < 0b01111111
				temp0 -= 0x6E0000
				temp1 -= 0x670000
				DrawSpriteXY(26, temp0, temp1)
			end if
			break

		case PlayerName[TAILS]
			temp0 -= 0x6D0000
			temp1 -= 0x670000
			DrawSpriteXY(34, temp0, temp1)
			break

		case PlayerName[KNUCKLES]
			temp0 -= 0x660000
			temp1 -= 0x670000
			DrawSpriteXY(42, temp0, temp1)
			break
		end switch
		break
	end switch

	switch object.bgSuperSonicState
	case TORNADO_BGSSONIC_INBG
	case TORNADO_BGSSONIC_SETUPENDPOSE
	case TORNADO_BGSSONIC_PLAYENDPOSE
	case TORNADO_BGSSONIC_ENDPOSE
		DrawSpriteXY(object.bgSuperSonicFrame, object.bgSuperSonicPos.x, object.bgSuperSonicPos.y)
		break
	end switch

	// Tornado End Pose Propeller
	if object.state >= TORNADO_ENDPOSE_FRAME1
		GetBit(temp0, oscillation, 1)
		temp0 += 17
		DrawSprite(temp0)
	end if
end event


event ObjectStartup
	LoadSpriteSheet("Ending/Objects.gif")
	SpriteFrame(21, 8, 4, 24, 373, 191) 		// Propeller - Frame 1 						- #0
	SpriteFrame(21, 11, 4, 18, 378, 194)		// Propeller - Frame 2 						- #1
	SpriteFrame(21, 16, 4, 8, 383, 199) 		// Propeller - Frame 3 						- #2
	SpriteFrame(21, 11, 4, 18, 388, 194)		// Propeller - Frame 4 						- #3
	SpriteFrame(-95, -16, 116, 64, 256, 191)	// Tornado 									- #4
	SpriteFrame(-45, -8, 21, 16, 256, 174)		// Tails Pilot - Frame 1 					- #5
	SpriteFrame(-45, -8, 21, 16, 278, 174)		// Tails Pilot - Frame 2 					- #6
	SpriteFrame(-45, -8, 21, 16, 300, 174)		// Tails Pilot - Frame 3 					- #7
	SpriteFrame(-45, -8, 21, 16, 322, 174)		// Tails Pilot - Frame 4 					- #8
	SpriteFrame(-47, -8, 23, 16, 344, 174)		// Tails Pilot - Frame 5 					- #9
	SpriteFrame(-50, -8, 24, 16, 256, 157)		// Sonic Pilot - Frame 6 					- #10
	SpriteFrame(-50, -8, 24, 16, 281, 157)		// Sonic Pilot - Frame 2 					- #11
	SpriteFrame(-32, -16, 60, 32, 1, 1)			// BG Tornado (Super Sonic) - Frame 1 		- #12
	SpriteFrame(-24, -12, 44, 24, 63, 1)		// BG Tornado (Super Sonic) - Frame 2 		- #13
	SpriteFrame(-20, -12, 39, 24, 108, 1)		// BG Tornado (Super Sonic) - Frame 3 		- #14
	SpriteFrame(-20, -12, 40, 24, 149, 1)		// BG Tornado (Super Sonic) - Frame 4 		- #15
	SpriteFrame(-19, -12, 38, 24, 190, 1)		// BG Tornado (Super Sonic) - Frame 5 		- #16
	SpriteFrame(-10, 7, 31, 31, 1, 34)			// Tornado End Pose (Propeller) - Frame 1 	- #17
	SpriteFrame(-11, 6, 31, 31, 33, 34)			// Tornado End Pose (Propeller) - Frame 2 	- #18
	SpriteFrame(-32, -28, 60, 44, 65, 26)		// BG Tornado (Sonic) 						- #19
	SpriteFrame(-24, -24, 44, 36, 126, 26)		// BG Tornado (Sonic) 						- #20
	SpriteFrame(-20, -19, 39, 31, 171, 26)		// BG Tornado (Sonic) 						- #21
	SpriteFrame(-20, -19, 40, 31, 211, 26)		// BG Tornado (Sonic) 						- #22
	SpriteFrame(-19, -15, 38, 27, 1, 146)		// BG Tornado (Sonic) 						- #23
	SpriteFrame(0, 0, 31, 38, 4, 179)			// Sonic End Pose - Frame 1 				- #24
	SpriteFrame(2, 5, 29, 42, 36, 175)			// Sonic End Pose - Frame 2 				- #25
	SpriteFrame(3, 3, 77, 96, 178, 159)			// Sonic End Pose - Frame 3 				- #26
	SpriteFrame(-32, -26, 60, 42, 1, 68)		// BG Tornado (Tails) 						- #27
	SpriteFrame(-24, -23, 44, 34, 62, 75)		// BG Tornado (Tails) 						- #28
	SpriteFrame(-20, -18, 39, 30, 1, 115)		// BG Tornado (Tails) 						- #29
	SpriteFrame(-20, -19, 40, 31, 41, 115)		// BG Tornado (Tails) 						- #30
	SpriteFrame(-19, -15, 38, 27, 82, 115)		// BG Tornado (Tails) 						- #31
	SpriteFrame(0, 2, 31, 36, 1, 219)			// Tails End Pose - Frame 1 				- #32
	SpriteFrame(0, 10, 32, 37, 33, 218)			// Tails End Pose - Frame 2 				- #33
	SpriteFrame(1, 0, 111, 96, 66, 159)			// Tails End Pose - Frame 3 				- #34
	SpriteFrame(-32, -32, 60, 48, 181, 256)		// BG Tornado (Knux) 						- #35
	SpriteFrame(-24, -24, 44, 36, 181, 305)		// BG Tornado (Knux) 						- #36
	SpriteFrame(-20, -19, 39, 31, 181, 342)		// BG Tornado (Knux) 						- #37
	SpriteFrame(-20, -19, 40, 31, 181, 374)		// BG Tornado (Knux) 						- #38
	SpriteFrame(-19, -15, 38, 27, 181, 406)		// BG Tornado (Knux) 						- #39
	SpriteFrame(0, 0, 31, 38, 382, 217)			// Knux End Pose - Frame 1 					- #40
	SpriteFrame(0, 0, 32, 39, 414, 216)			// Knux End Pose - Frame 2 					- #41
	SpriteFrame(0, 0, 64, 104, 447, 151)		// Knux End Pose - Frame 3 					- #42
	SpriteFrame(-11, -8, 19, 16, 124, 84)		// BG Super Sonic - Frame 1 				- #43
	SpriteFrame(-8, -8, 15, 12, 128, 101)		// BG Super Sonic - Frame 2 				- #44
	SpriteFrame(-7, -8, 11, 14, 132, 144)		// BG Super Sonic - Frame 3 				- #45
	SpriteFrame(-7, -7, 13, 13, 130, 129)		// BG Super Sonic - Frame 4 				- #46
	SpriteFrame(-6, -8, 13, 15, 130, 143)		// BG Super Sonic - Frame 5 				- #47
	SpriteFrame(1, 4, 111, 92, 144, 66)			// Super Sonic End Pose 					- #48
	SpriteFrame(-90, -53, 179, 105, 1, 256)		// Tornado End Pose (Sonic Pilot) 			- #49
	SpriteFrame(-90, -53, 179, 105, 1, 362)		// Tornado End Pose (Tails Pilot) 			- #50
	SpriteFrame(0, 0, 0, 0, 391, 256)			// Logo Blank								- #51
	SpriteFrame(-60, -20, 120, 40, 391, 256)	// Sonic 2 Logo 							- #52
	SpriteFrame(-63, -22, 81, 38, 397, 297)		// "Sonic" Flash - Frame 1 					- #53
	SpriteFrame(-63, -22, 81, 38, 397, 336)		// "Sonic" Flash - Frame 2 					- #54
	SpriteFrame(-63, -22, 81, 38, 397, 375)		// "Sonic" Flash - Frame 3 					- #55
	SpriteFrame(28, -20, 32, 39, 479, 297)		// "2" Flash - Frame 1 						- #56
	SpriteFrame(28, -20, 32, 39, 479, 337)		// "2" Flash - Frame 2 						- #57
	SpriteFrame(28, -20, 32, 39, 479, 377)		// "2" Flash - Frame 3 						- #58
	SpriteFrame(28, -20, 32, 39, 479, 417)		// "2" Flash - Frame 4 						- #59
end event

event RSDKDraw
	DrawSprite(4)
end event

event RSDKLoad
	LoadSpriteSheet("Ending/Objects.gif")
	SpriteFrame(21, 8, 4, 24, 373, 191) 		// Propeller - Frame 1 						- #0
	SpriteFrame(21, 11, 4, 18, 378, 194)		// Propeller - Frame 2 						- #1
	SpriteFrame(21, 16, 4, 8, 383, 199) 		// Propeller - Frame 3 						- #2
	SpriteFrame(21, 11, 4, 18, 388, 194)		// Propeller - Frame 4 						- #3
	SpriteFrame(-95, -16, 116, 64, 256, 191)	// Tornado 									- #4
end event
