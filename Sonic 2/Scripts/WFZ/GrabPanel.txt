// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Grab Panel Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================
private alias object.value0 : object.delay

// ========================
// Function Declarations
// ========================
reserve function GrabPanel_DebugDraw
reserve function GrabPanel_DebugSpawn

function GrabPanel_DebugDraw
	DrawSprite(0)
end function


function GrabPanel_DebugSpawn
	CreateTempObject(TypeName[Grab Panel], 0, object.xpos, object.ypos)
	object[tempObjectPos].delay = 60
end function


event ObjectMain
	temp7 = false
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		temp2 = 0
		if object[currentPlayer].type == TypeName[Tails Object]
			temp3 = -0x100000
		else
			temp3 = -0x160000
		end if

		temp0 = object.xpos
		temp0 += temp3
		temp0 -= object[currentPlayer].xpos
		Abs(temp0)
		if temp0 <= 0x40000
			temp0 = object.ypos
			temp0 -= object[currentPlayer].ypos
			temp1 = temp0
			Abs(temp0)
			if temp0 <= 0x160000
				object[currentPlayer].animation 		= ANI_CLINGING
				object[currentPlayer].animationSpeed 	= 128
				object[currentPlayer].direction 		= FLIP_X
				object[currentPlayer].xpos 				= object.xpos
				object[currentPlayer].xpos += temp3
				if object.propertyValue == 0x7F
					object[currentPlayer].state = PlayerObject_Blank
					object[currentPlayer].xvel 	= 0
					object[currentPlayer].yvel 	= 0
					object[currentPlayer].speed = 0
				end if

				temp2 = 1
			else
				if object[currentPlayer].animation == ANI_CLINGING
					object[currentPlayer].ypos = object.ypos
					if temp1 < 0
						object[currentPlayer].ypos += 0x160000
					else
						object[currentPlayer].ypos -= 0x160000
					end if

					temp2 = 1
				end if
			end if

			if temp2 != 0
				if currentPlayer == 0
					if object[currentPlayer].jumpPress != false
						if object[currentPlayer].ypos < object.ypos
							temp7 = 1
						else
							temp7 = 2
						end if
					end if

					object.delay--
					if object.delay == 0
						if object[currentPlayer].ypos < object.ypos
							temp7 = 1
						else
							temp7 = 2
						end if
					end if
				end if
			end if
		end if
	next

	if temp7 != 0
		object[currentPlayer].animation = ANI_FANROTATE
		PlaySfx(SfxName[Ledge Break], false)
		temp0 = object.xpos
		temp0 -= 0x100000
		temp1 = object.ypos
		temp1 -= 0x100000
		if temp7 == 1
			CreateTempObject(TypeName[Grab Panel Part], 0, temp0, temp1)
			object[tempObjectPos].xvel = -0x40000
			object[tempObjectPos].state = 1

			temp1 += 0x200000
			CreateTempObject(TypeName[Grab Panel Part], 4, temp0, temp1)
			object[tempObjectPos].xvel = -0x40000
			
			temp0 -= 0x200000
			temp1 -= 0x200000
			CreateTempObject(TypeName[Grab Panel Part], 16, temp0, temp1)
			object[tempObjectPos].xvel = -0x40000

			temp1 += 0x200000
			CreateTempObject(TypeName[Grab Panel Part], 24, temp0, temp1)
			object[tempObjectPos].xvel = -0x40000
		else
			CreateTempObject(TypeName[Grab Panel Part], 4, temp0, temp1)
			object[tempObjectPos].xvel = -0x40000

			temp1 += 0x200000
			CreateTempObject(TypeName[Grab Panel Part], 0, temp0, temp1)
			object[tempObjectPos].xvel = -0x40000
			object[tempObjectPos].state = 1

			temp0 -= 0x200000
			temp1 -= 0x200000
			CreateTempObject(TypeName[Grab Panel Part], 24, temp0, temp1)
			object[tempObjectPos].xvel = -0x40000

			temp1 += 0x200000
			CreateTempObject(TypeName[Grab Panel Part], 16, temp0, temp1)
			object[tempObjectPos].xvel = -0x40000
		end if
		object.type = TypeName[Blank Object]
	end if
end event


event ObjectDraw
	if object.propertyValue != 0x7F
		DrawSprite(0)
	// ??? no end if?
	// I mean code-wise, yeah the end event will reset it but, this is prolly a "bug"
end event


event ObjectStartup
	LoadSpriteSheet("SCZ/Objects.gif")
	SpriteFrame(-64, -32, 64, 64, 1, 69)

	foreach (TypeName[Grab Panel], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].delay = object[arrayPos0].propertyValue
		object[arrayPos0].delay *= 60
	next

	SetTableValue(TypeName[Grab Panel], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(GrabPanel_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(GrabPanel_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event

event RSDKDraw
	if object.propertyValue != 0x7F
		DrawSprite(0)
	else
		object.inkEffect = INK_BLEND
		DrawSpriteFX(0, FX_INK, object.xpos, object.ypos)
		object.inkEffect = INK_NONE
	end if
end event

event RSDKLoad
	LoadSpriteSheet("SCZ/Objects.gif")
	SpriteFrame(-64, -32, 64, 64, 1, 69)
	
	// 127 == Eggman Shuttle Grab Panel
	SetVariableAlias(ALIAS_VAR_PROPVAL, "delay")
end event
