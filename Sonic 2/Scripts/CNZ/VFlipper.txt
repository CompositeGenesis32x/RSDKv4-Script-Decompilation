// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: V Flipper Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================
private alias object.value0 : object.timer

// States
private alias 0 : VFLIPPER_IDLE
private alias 1 : VFLIPPER_ACTIVATED_L
private alias 2 : VFLIPPER_ACTIVATED_R

// Player Aliases
private alias object.value30 : player.jumpOffset			// Added to the player's position when jumping/rolling. Normally -5 for S&K, and -1 for Tails

// ========================
// Function Declarations
// ========================
reserve function VFlipper_DebugDraw
reserve function VFlipper_DebugSpawn

function VFlipper_DebugDraw
	DrawSprite(0)
end function


function VFlipper_DebugSpawn
	CreateTempObject(TypeName[V Flipper], 0, object.xpos, object.ypos)
end function


// ========================
// Events
// ========================

event ObjectMain
	switch object.state
	default
	case VFLIPPER_IDLE
		break

	case VFLIPPER_ACTIVATED_L
		object.frame = object.timer
		object.frame >>= 1
		object.frame++
		if object.timer < 6
			object.timer++
		else
			object.timer = 0
			object.frame = 0
			object.state = VFLIPPER_IDLE
		end if
		break

	case VFLIPPER_ACTIVATED_R
		object.frame = object.timer
		object.frame >>= 1
		object.frame += 3
		if object.timer < 6
			object.timer++
		else
			object.timer = 0
			object.frame = 0
			object.state = VFLIPPER_IDLE
		end if
		break
	end switch

	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_BOX, object.entityPos, -8, -24, 8, 24, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
		if object[currentPlayer].gravity == GRAVITY_GROUND
			switch checkResult
			default
			case COL_NONE
			case COL_TOP
			case COL_BOTTOM
				break
				
			case COL_LEFT
				object[currentPlayer].speed = -0x100000
				object[currentPlayer].xvel = -0x100000

				if object[currentPlayer].state != PlayerObject_TubeSwitch
					object[currentPlayer].state = PlayerObject_HandleRolling
					object[currentPlayer].animation = ANI_JUMPING
				end if

				if object[currentPlayer].prevAnimation != ANI_JUMPING
					object[currentPlayer].iypos -= player[currentPlayer].jumpOffset
				end if

				object.state = VFLIPPER_ACTIVATED_L
				PlaySfx(SfxName[Flipper], false)
				break

			case COL_RIGHT
				object[currentPlayer].speed = 0x100000
				object[currentPlayer].xvel = 0x100000

				CheckEqual(object[currentPlayer].state, PlayerObject_TubeSwitch)
				temp0 = checkResult
				CheckEqual(object[currentPlayer].state, PlayerObject_AirCtrlLock)
				temp0 |= checkResult
				if temp0 == false
					object[currentPlayer].state = PlayerObject_HandleRolling
					object[currentPlayer].animation = ANI_JUMPING
				end if

				if object[currentPlayer].prevAnimation != ANI_JUMPING
					object[currentPlayer].iypos -= player[currentPlayer].jumpOffset
				end if

				object.state = VFLIPPER_ACTIVATED_R
				PlaySfx(SfxName[Flipper], false)
				break
			end switch
		end if
	next
end event


event ObjectDraw
	DrawSprite(object.frame)
end event


event ObjectStartup
	CheckCurrentStageFolder("Zone04")
	if checkResult == true
		LoadSpriteSheet("CNZ/Objects.gif")
		SpriteFrame(-8, -24, 16, 48, 101, 157)
		SpriteFrame(-17, -25, 26, 47, 118, 157)
		SpriteFrame(-8, -24, 16, 48, 101, 157)
		SpriteFrame(-9, -25, 26, 47, 74, 157)
		SpriteFrame(-8, -24, 16, 48, 101, 157)
		SpriteFrame(-17, -25, 26, 47, 118, 157)
	else
		LoadSpriteSheet("MBZ/Objects.gif")
		SpriteFrame(-8, -24, 16, 48, 321, 338)
		SpriteFrame(-17, -25, 26, 47, 338, 338)
		SpriteFrame(-8, -24, 16, 48, 321, 338)
		SpriteFrame(-9, -25, 26, 47, 394, 338)
		SpriteFrame(-8, -24, 16, 48, 321, 338)
		SpriteFrame(-17, -25, 26, 47, 338, 338)
	end if

	SetTableValue(TypeName[V Flipper], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(VFlipper_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(VFlipper_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	CheckCurrentStageFolder("Zone04")
	if checkResult == true
		LoadSpriteSheet("CNZ/Objects.gif")
		SpriteFrame(-8, -24, 16, 48, 101, 157)
	else
		LoadSpriteSheet("MBZ/Objects.gif")
		SpriteFrame(-8, -24, 16, 48, 321, 338)
	end if
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
