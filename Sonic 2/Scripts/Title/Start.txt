// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Start Message Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================
private alias object.value0 : object.timer
private alias object.value4 : object.attractTimer

// States
private alias 0 : START_AWAITACTION
private alias 1 : START_NEWGAME
private alias 2 : START_LEVELSELECT
private alias 3 : START_CONTINUESCREEN
private alias 4 : START_ATTRACTMODE

event ObjectUpdate
	switch object.state
	case START_AWAITACTION
		CheckTouchRect(0, 0, screen.xsize, screen.ysize)
		if inputPress[0].start == true
			checkResult = true
		end if

		if inputPress[0].buttonA == true
			checkResult = true
		end if

		if inputPress[0].buttonB == true
			checkResult = true
		end if

		if inputPress[0].buttonC == true
			checkResult = true
		end if

		if checkResult > -1
			if options.stageSelectFlag == true
				if checkResult > 0
					object.state = START_LEVELSELECT
				else
					object.state = START_NEWGAME
				end if
			else
				object.state = START_NEWGAME
			end if

			PlaySfx(SfxName[Menu Select], false)
			StopMusic()
		else
			object.attractTimer++
			if object.attractTimer == 420
				object.state = START_ATTRACTMODE
			end if
		end if
		break

	case START_NEWGAME
		object.timer += 8
		if object.timer == 320
			options.attractMode 	= false
			player.lives 			= 3
			player.continues 		= 0
			player.score 			= 0
			player.scoreBonus 		= 50000
			starPostID 				= 0
			specialStage.listPos 	= 0
			specialStage.emeralds 	= 0b00000000
			specialStage.nextZone 	= 0

			// New Game - to EHZ1 you go!
			stage.activeList 		= REGULAR_STAGE
			stage.listPos 			= 0
			LoadStage()
		end if
		SetScreenFade(0x00, 0x00, 0x00, object.timer)
		break
		
	case START_LEVELSELECT
		object.timer += 8
		if object.timer == 320
			options.attractMode 	= false
			player.lives 			= 3
			player.continues 		= 0
			player.score 			= 0
			player.scoreBonus 		= 50000
			starPostID 				= 0
			specialStage.listPos 	= 0
			specialStage.emeralds 	= 0b00000000
			specialStage.nextZone 	= 0

			// Level Select
			stage.activeList 	= PRESENTATION_STAGE
			stage.listPos 		= 5
			LoadStage()
		end if
		SetScreenFade(0x00, 0x00, 0x00, object.timer)
		break

	case START_CONTINUESCREEN
		object.timer += 8
		if object.timer == 320
			// Continue Screen...?
			options.attractMode = false
			stage.listPos 		= 4
			LoadStage()
		end if
		SetScreenFade(0x00, 0x00, 0x00, object.timer)
		break

	case START_ATTRACTMODE
		object.timer += 8
		if object.timer == 320
			player.lives 			= 3
			player.continues 		= 0
			player.score 			= 0
			player.scoreBonus 		= 50000
			starPostID 				= 0
			specialStage.listPos 	= 0
			specialStage.emeralds 	= 0b00000000
			specialStage.nextZone 	= 0

			// Attract Mode timee
			stage.activeList = REGULAR_STAGE
			stage.listPos = attract.stage
			stage.listPos <<= 1

			attract.stage++
			attract.stage &= 3
			options.attractMode = true
			credits.screen = 0

			LoadStage()
		end if
		SetScreenFade(0x00, 0x00, 0x00, object.timer)
		break
	end switch
end event


event ObjectDraw
	object.animationTimer++
	if object.animationTimer > 59
		object.animationTimer = 0
	end if

	if object.animationTimer > 29
		DrawSpriteScreenXY(0, screen.xcenter, object.ypos)
	end if
end event


event ObjectStartup
	LoadSpriteSheet("Title/Title.gif")
	switch engine.deviceType
	case STANDARD
		SpriteFrame(-49, -1, 98, 9, 1, 380)		// Press Button 	- #0
		break

	case MOBILE
		SpriteFrame(-57, -1, 114, 9, 1, 390)	// Touch To Start 	- #0
		break
	end switch
end event


event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Title/Title.gif")
	SpriteFrame(-49, -1, 98, 9, 1, 380)		// Press Button 	- #0
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
