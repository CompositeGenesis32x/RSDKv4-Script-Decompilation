// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Tube Switch Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================
private alias object.value0 : object.active // Gets set a few times, but seems to never be used otherwise?

private alias 0 : TUBESWITCH_L_ENTRY
private alias 1 : TUBESWITCH_R_ENTRY

// Player aliases
private alias object.value34 : player.collisionDisabled


event ObjectMain
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		if player[currentPlayer].collisionDisabled == true
			// Player should collide with stuff while in a tube
			player[currentPlayer].collisionDisabled = false
		end if

		CheckEqual(object[currentPlayer].state, PlayerObject_Death)
		temp0 = checkResult
		CheckEqual(object[currentPlayer].state, PlayerObject_KnuxLedgePullUp)
		temp0 |= checkResult
		CheckEqual(object[currentPlayer].state, PlayerObject_KnuxWallClimb)
		temp0 |= checkResult
		if temp0 == false
			BoxCollisionTest(C_TOUCH, object.entityPos, -16, -16, 16, 16, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
			if checkResult == true
				if object[currentPlayer].yvel < 0
					object[currentPlayer].yvel = 0
				end if

				switch object.propertyValue
				case TUBESWITCH_L_ENTRY
					if object[currentPlayer].xvel > 0
						if object[currentPlayer].animation != ANI_JUMPING
							if object[currentPlayer].state != PlayerObject_TubeSwitch
								PlaySfx(SfxName[Rolling], false)
							end if
						end if

						if object[currentPlayer].state == PlayerObject_KnuxGlideLeft
							FlipSign(object[currentPlayer].speed)
						end if

						object[currentPlayer].direction = FLIP_NONE
						object[currentPlayer].state 	= PlayerObject_TubeSwitch
						object[currentPlayer].animation = ANI_JUMPING
					else
						if object[currentPlayer].gravity == GRAVITY_GROUND
							if object[currentPlayer].xvel > -0x20000
								object[currentPlayer].speed = -0x20000
							end if
						end if

						object[currentPlayer].state 	= PlayerObject_HandleRolling
						object[currentPlayer].animation = ANI_JUMPING
					end if
					break

				case TUBESWITCH_R_ENTRY
					if object[currentPlayer].xvel < 0
						if object[currentPlayer].animation != ANI_JUMPING
							if object[currentPlayer].state != PlayerObject_TubeSwitch
								PlaySfx(SfxName[Rolling], false)
							end if
						end if

						if object[currentPlayer].state == PlayerObject_KnuxGlideLeft
							FlipSign(object[currentPlayer].speed)
						end if

						object[currentPlayer].direction = FLIP_X
						object[currentPlayer].state 	= PlayerObject_TubeSwitch
						object[currentPlayer].animation = ANI_JUMPING
					else
						if object[currentPlayer].gravity == GRAVITY_GROUND
							if object[currentPlayer].xvel < 0x20000
								object[currentPlayer].speed = 0x20000
							end if
						end if
						object[currentPlayer].state 	= PlayerObject_HandleRolling
						object[currentPlayer].animation = ANI_JUMPING
					end if
					break

				end switch

				object.active = true
			else
				object.active = false
			end if
		end if
	next
end event


event RSDKDraw
	temp0 = object.xpos
	temp0 -= 0x80000
	temp1 = object.ypos
	temp1 -= 0x80000

	DrawSpriteXY(0, temp0, temp1)

	temp0 += 0x100000
	DrawSpriteXY(0, temp0, temp1)

	temp1 += 0x100000
	DrawSpriteXY(0, temp0, temp1)

	temp0 -= 0x100000
	DrawSpriteXY(0, temp0, temp1)
end event


event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-8, -8, 16, 16, 168, 18) // "T" (ubeSwitch) - #0
	
	AddEditorVariable("type")
	SetActiveVariable("type")
	AddEnumVariable("Enter From Left, Exit From Right", 0)
	AddEnumVariable("Enter From Right, Exit From Left", 1)
end event
