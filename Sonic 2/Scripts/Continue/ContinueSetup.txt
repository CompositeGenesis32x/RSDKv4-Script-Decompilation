// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Continue Setup Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias object.value0 : object.timer

event ObjectMain
	switch object.state
	case 0
		object.timer = 320
		SetScreenFade(0, 0, 0, object.timer)
		object.state++
		break

	case 1
		if object.timer > 0
			object.timer -= 8
		else
			PlayMusic(0)
			object.state++
			object[-1].state++ // Tell the Continue Screen object to start counting
		end if
		SetScreenFade(0, 0, 0, object.timer)
		break

	case 2
		CheckTouchRect(0, 0, screen.xsize, screen.ysize)
		if checkResult > -1
			inputPress.start = true
		end if

		if inputPress.start == true
			object[0].animation = ANI_CONTINUE_UP
			object[0].speed = 0
			PlaySfx(SfxName[Charge], false)
			object.state++
			object[-1].state++ // Tell the Continue Screen object to stop counting
		end if
		break

	case 3
		if music.volume > 0
			music.volume--
		end if

		object[0].speed += 0x2000
		if object[0].speed == 0x90000
			object[0].xvel = 0x100000
			if object[1].type == TypeName[Blank Object]
				object.state++
			end if
		end if

		if object[1].type != TypeName[Blank Object]
			if object[0].speed == 0x20000
				object[1].animation = ANI_CONTINUE_UP
				object[1].speed = 0
				PlaySfx(SfxName[Charge], false)
			end if

			if object[0].speed > 0x20000
				object[1].speed += 0x2000
				if object[1].speed == 0x90000
					object[1].xvel = 0x100000
					object.state++
				end if
			end if
		end if
		break

	case 4
		if music.volume > 0
			music.volume--
		end if

		temp0 = screen.xsize
		temp0 += 32
		if object[1].type == TypeName[Blank Object]
			temp1 = object[0].xpos
		else
			temp1 = object[1].xpos
		end if

		temp1 >>= 16
		if temp1 >= temp0
			object.state++
		end if
		break

	case 5
		if music.volume > 0
			music.volume--
		end if
		object.timer += 4
		SetScreenFade(0, 0, 0, object.timer)

		if object.timer == 384
			player.continues--
			player.lives = 3
			fadeColor = 0x000000
			object.timer = 0
			starPostID = 0
			object.direction = FLIP_NONE
			stage.activeList = recStageList
			stage.listPos = recStage
			LoadStage()
		end if
		break

	case 6
		object.timer += 4
		SetScreenFade(0, 0, 0, object.timer)
		if object.timer == 384
			fadeColor = 0x000000
			object.timer = 0
			starPostID = 0
			object.direction = FLIP_NONE
			engine.state = ENGINE_RESETGAME
		end if
		break

	end switch

	options.touchControls = false
end event


event ObjectDraw
	ClearScreen(1)
end event


event ObjectStartup

	foreach (TypeName[Continue Setup], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].drawOrder = 0
	next

	object[0].xpos = screen.xcenter
	object[0].xpos <<= 16
	object[0].animation = ANI_FANROTATE
	object[0].state = PlayerObject_HandleContRun

	// Different starting offsets based on P1
	switch stage.playerListPos
	case PlayerName[SONIC]
	case PlayerName[SONIC AND TAILS]
		object[0].ypos = 0xAC0000
		break

	case PlayerName[TAILS]
		object[0].ypos = 0xAC0000
		break

	case PlayerName[KNUCKLES]
		object[0].ypos = 0xB00000
		break
	end switch

	object[0].animation = ANI_CONTINUE

	if object[1].type != TypeName[Blank Object]
		object[1].xpos = screen.xcenter
		object[1].xpos <<= 16
		object[1].animation = ANI_FANROTATE
		object[1].state = PlayerObject_HandleContRun
		object[0].xpos -= 0x100000
		object[1].xpos += 0x100000
		object[1].ypos = object[0].ypos
		object[1].ypos += 0x40000
		object[1].animation = ANI_BORED
	end if

	stage.newXBoundary2 = screen.xsize
	stage.newYBoundary2 = screen.ysize

	SetMusicTrack("Continue.ogg", 0, false)

	stage.pauseEnabled = false
	if player.continues == 0
		// Player was most likely brought here from the dev menu stage select, give them three continues
		player.continues = 3
	end if

	options.touchControls = false
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
