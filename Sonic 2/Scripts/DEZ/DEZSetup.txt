// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: DEZ Setup Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

//-------Aliases-------//
private alias 40 : TYPE_DEZSETUP

// Tables

public table DEZSetup_palTable1
	0xE00000, 0xC00000, 0xA00000, 0x800000, 0x600000, 0x400000, 0x200000, 0x400000, 0x600000, 0x800000, 0xA00000, 0xC00000, 0xE00000, 0xE02000, 0xE04000, 0xE02000
end table

public table DEZSetup_palTable2
	0x00E000, 0x20C000, 0x40A000, 0x608000, 0x806000, 0xA04000, 0xC02000, 0xE00000, 0xC00020, 0xA00040, 0x800060, 0x600080, 0x4000A0, 0x2000C0, 0x0000E0, 0x0020C0
	0x0040A0, 0x006080, 0x008060, 0x00A040, 0x00C020
end table

public table DEZSetup_palTable3
	0x0040E0, 0x0000C0, 0x0000C0, 0x0060E0, 0x0020C0, 0x0000C0, 0x0040E0, 0x0040E0, 0x0000C0, 0x0020C0, 0x0060E0, 0x0020C0, 0x0000C0, 0x0040E0, 0x0040C0, 0x0000C0
	0x0020C0, 0x0040E0, 0x0000C0, 0x0000C0, 0x0060E0, 0x0020C0, 0x0000C0, 0x0040E0, 0x0020E0, 0x0000C0, 0x0020C0
end table

// Unused..?
// It's a long list of colors likely meant for a palette cycle but it doesn't match any other palette cycles
// Strangely, this table can also be found in CPZ Setup
private table DEZSetup_Unused
	0x00E000, 0x00A000, 0x006000, 0x002000, 0x000000, 0x000020, 0x000060, 0x0020A0, 0x0060E0, 0x0020A0, 0x000060, 0x000020, 0x000000, 0x200000, 0x600000, 0xA00000
	0xE00000, 0xE04000, 0xE00000, 0xA00000, 0x600000, 0x200000, 0x000000, 0x202000, 0x606000, 0xA0A000, 0xE0E000, 0xA0A000, 0x606000, 0x202000, 0x000000, 0x002000
	0x006000, 0x00A000
end table

event ObjectMain
	// Animate the converyer belts in the background of the Mecha Sonic fight
	object.value0++
	if object.value0 == 5
		object.value0 = 0
		object.frame++
		object.frame &= 7
		temp0 = object.frame
		temp0 += 768
		Copy16x16Tile(767, temp0)
	end if

	// Handles the various cycling palettes throughout the stage
	object.value14++
	if object.value14 == 8
		object.value14 = 0
		object.value15++
		object.value15 &= 15
		GetTableValue(temp0, object.value15, DEZSetup_palTable1)
		SetPaletteEntry(0, 175, temp0)
		object.value16++
		object.value16 %= 21
		GetTableValue(temp0, object.value16, DEZSetup_palTable2)
		SetPaletteEntry(0, 191, temp0)
		object.value17 += 3
		object.value17 %= 27
		GetTableValue(temp0, object.value17, DEZSetup_palTable3)
		SetPaletteEntry(0, 188, temp0)
		object.value18 += 3
		object.value18 %= 27
		GetTableValue(temp0, object.value18, DEZSetup_palTable3)
		SetPaletteEntry(0, 189, temp0)
		object.value19 += 3
		object.value19 %= 27
		GetTableValue(temp0, object.value19, DEZSetup_palTable3)
		SetPaletteEntry(0, 190, temp0)
	end if
end event


event ObjectStartup
	SetMusicTrack("DeathEgg.ogg", 0, 1)
	SetPaletteEntry(0, 192, 0)
	Copy16x16Tile(767, 768)
	animalType1 = TypeName[Blank Object]
	animalType2 = TypeName[Blank Object]
	object[1].type = TypeName[Blank Object]
	playerCount = 1
	object[10].type = TypeName[DEZ Setup]
	object[10].priority = PRIORITY_ACTIVE
	object[10].drawOrder = 0
	object[10].value18 = 1
	object[10].value19 = 2
	temp0 = stage.curYBoundary2
	temp0 -= screen.ysize
	stage.newYBoundary1 = temp0
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
