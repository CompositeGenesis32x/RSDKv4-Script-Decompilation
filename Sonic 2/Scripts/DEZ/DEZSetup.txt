// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: DEZ Setup Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias object.value0  : object.aniTileTImer
private alias object.value14 : object.palTimer
private alias object.value15 : object.palSlot175
private alias object.value16 : object.palSlot191
private alias object.value17 : object.palSlot188
private alias object.value18 : object.palSlot189
private alias object.value19 : object.palSlot190

// Reserved object slots
private alias 10 : SLOT_LEVELSETUP

// Tables

public table DEZSetup_palTable1
	0xE00000, 0xC00000, 0xA00000, 0x800000, 0x600000, 0x400000, 0x200000, 0x400000
	0x600000, 0x800000, 0xA00000, 0xC00000, 0xE00000, 0xE02000, 0xE04000, 0xE02000
end table

public table DEZSetup_palTable2
	0x00E000, 0x20C000, 0x40A000, 0x608000, 0x806000, 0xA04000, 0xC02000
	0xE00000, 0xC00020, 0xA00040, 0x800060, 0x600080, 0x4000A0, 0x2000C0
	0x0000E0, 0x0020C0, 0x0040A0, 0x006080, 0x008060, 0x00A040, 0x00C020
end table

public table DEZSetup_palTable3
	0x0040E0, 0x0000C0, 0x0000C0
	0x0060E0, 0x0020C0, 0x0000C0
	0x0040E0, 0x0040E0, 0x0000C0
	0x0020C0, 0x0060E0, 0x0020C0
	0x0000C0, 0x0040E0, 0x0040C0
	0x0000C0, 0x0020C0, 0x0040E0
	0x0000C0, 0x0000C0, 0x0060E0
	0x0020C0, 0x0000C0, 0x0040E0
	0x0020E0, 0x0000C0, 0x0020C0
end table

// Unused palette table, also found in CPZ and EndSetup
// These are actually colours for one of WFZ's palette cycles
// However, you won't find this in WFZSetup because in this 2013 remake, WFZ only uses 1 of the 3 palette cycles it had in the original game,
// this one being among the 2 that were cut
// Why they were cut, I have no idea, but at the very least, it's certainly a mystery how one of them ended up here of all places...
private table DEZSetup_unusedPalette
	0x00E000, 0x00A000, 0x006000, 0x002000, 0x000000, 0x000020, 0x000060, 0x0020A0
	0x0060E0, 0x0020A0, 0x000060, 0x000020, 0x000000, 0x200000, 0x600000, 0xA00000
	0xE00000, 0xE04000, 0xE00000, 0xA00000, 0x600000, 0x200000, 0x000000, 0x202000
	0x606000, 0xA0A000, 0xE0E000, 0xA0A000, 0x606000, 0x202000, 0x000000, 0x002000
	0x006000, 0x00A000
end table


event ObjectMain

	// Animate the converyer belts in the background of the Mecha Sonic fight
	object.aniTileTImer++
	if object.aniTileTImer == 5
		object.aniTileTImer = 0
		object.frame++
		object.frame &= 7
		temp0 = object.frame
		temp0 += 768
		Copy16x16Tile(767, temp0)
	end if

	// Manage the various cycling palettes throughout the stage
	object.palTimer++
	if object.palTimer == 8
		object.palTimer = 0

		object.palSlot175++
		object.palSlot175 &= 15
		GetTableValue(temp0, object.palSlot175, DEZSetup_palTable1)
		SetPaletteEntry(0, 175, temp0)

		object.palSlot191++
		object.palSlot191 %= 21
		GetTableValue(temp0, object.palSlot191, DEZSetup_palTable2)
		SetPaletteEntry(0, 191, temp0)

		object.palSlot188 += 3
		object.palSlot188 %= 27
		GetTableValue(temp0, object.palSlot188, DEZSetup_palTable3)
		SetPaletteEntry(0, 188, temp0)

		object.palSlot189 += 3
		object.palSlot189 %= 27
		GetTableValue(temp0, object.palSlot189, DEZSetup_palTable3)
		SetPaletteEntry(0, 189, temp0)

		object.palSlot190 += 3
		object.palSlot190 %= 27
		GetTableValue(temp0, object.palSlot190, DEZSetup_palTable3)
		SetPaletteEntry(0, 190, temp0)
	end if

end event


event ObjectStartup
	// Set the stage music
	SetMusicTrack("DeathEgg.ogg", 0, true)

	// No speed up/slow down variants need to be set since Speed Shoes shouldn't exist in DEZ at all

	SetPaletteEntry(0, 192, 0x000000) // Set color index 192 to be black

	Copy16x16Tile(767, 768)

	// There shouldn't be any animals in DEZ
	animalType1 = TypeName[Blank Object]
	animalType2 = TypeName[Blank Object]

	// Remove player 2
	object[1].type = TypeName[Blank Object]
	playerCount = 1

	// Make object 10 a DEZ Setup object
	object[SLOT_LEVELSETUP].type = TypeName[DEZ Setup]
	object[SLOT_LEVELSETUP].priority = PRIORITY_ACTIVE
	object[SLOT_LEVELSETUP].drawOrder = 0 // This is just a leftover from this script being copied from CPZSetup, it doesn't actually do anything since this object doesn't have an ObjectDraw event
	object[SLOT_LEVELSETUP].palSlot189 = 1
	object[SLOT_LEVELSETUP].palSlot190 = 2

	// Set stage bounds
	temp0 = stage.curYBoundary2
	temp0 -= screen.ysize
	stage.newYBoundary1 = temp0
end event


event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
