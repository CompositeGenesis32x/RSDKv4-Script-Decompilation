// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Special Finish Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias object.value0  : object.timer
private alias object.value1  : object.textTopPos
private alias object.value2  : object.textMidPos
private alias object.value3  : object.textBottomPos
private alias object.value4  : object.scorePos
private alias object.value5  : object.p1RingPos
private alias object.value6  : object.p2RingPos
private alias object.value7  : object.gemPos
private alias object.value8  : object.p1RingBonus
private alias object.value9  : object.p2RingBonus
private alias object.value10 : object.gemBonus
private alias object.value11 : object.resultsTextTop
private alias object.value12 : object.resultsTextMid
private alias object.value13 : object.resultsTextBottom
private alias object.value14 : object.emeraldAlpha

// Text Messages
private alias 0 : TEXT_NONE
private alias 1 : TEXT_FAILED
private alias 2 : TEXT_ONEEMERALD
private alias 3 : TEXT_ALLEMERALDS_CHAOS
private alias 4 : TEXT_CANCHANGEFORM
private alias 5 : TEXT_TOHIDDENPALACE

// States
private alias 0  : SPECIALSETUP_FADEIN
private alias 1  : SPECIALSETUP_FADEIDLE
private alias 2  : SPECIALSETUP_ENTERTEXT
private alias 3  : SPECIALSETUP_ENTEREMERALDS
private alias 4  : SPECIALSETUP_TALLYSCORE
private alias 5  : SPECIALSETUP_FAILED
private alias 6  : SPECIALSETUP_FADEOUT
private alias 7  : SPECIALSETUP_EXITRESULTS
private alias 8  : SPECIALSETUP_WINSHOWREWARD
private alias 9  : SPECIALSETUP_WINENTERTEXT
private alias 10 : SPECIALSETUP_WINEVENT
private alias 11 : SPECIALSETUP_WINIDLE

// Player Aliases
private alias object.value0 : player.rings

// Tables
private table SpecialFinish_emeraldXPosTable
	0, 24, 24, 0, -24, -24, 0
end table

private table SpecialFinish_emeraldYPosTable
	84, 96, 120, 132, 120, 96, 108
end table


event ObjectMain
	options.touchControls = false
	switch object.state
	case SPECIALSETUP_FADEIN
		if object.timer < 256
			object.timer += 8
			SetScreenFade(224, 224, 224, object.timer)
		else
			if SpecialSetup_gotEmerald == true
				temp0 = stage.actNum
				temp0--
				SetBit(specialStage.emeralds, temp0, 1)
				if specialStage.emeralds == 0b01111111
					object.resultsTextTop = TEXT_ALLEMERALDS_CHAOS
					object.resultsTextMid = TEXT_ALLEMERALDS_CHAOS
				else
					object.resultsTextTop = TEXT_ONEEMERALD
					object.resultsTextMid = TEXT_ONEEMERALD
				end if
				object.gemBonus = 10000
				specialStage.listPos++
				specialStage.listPos %= 7
			else
				object.resultsTextTop = TEXT_NONE
				object.resultsTextMid = TEXT_FAILED
				object.gemBonus = 0
			end if
			object.resultsTextBottom = 0

			object.p1RingBonus = player[2].rings
			if stage.player2Enabled == true
				object.p2RingBonus = player[3].rings
			else
				object.p2RingBonus = 0
			end if

			stage.listPos = specialStage.nextZone
			stage.activeList = REGULAR_STAGE
			object.timer = 0
			temp0 = 0
			while temp0 < 20
				ResetObjectEntity(temp0, TypeName[Blank Object], 0, 0, 0)
				temp0++
			loop
			temp0++
			while temp0 < 0x4A0
				ResetObjectEntity(temp0, TypeName[Blank Object], 0, 0, 0)
				temp0++
			loop
			stage.activeLayer[0] = 9
			stage.activeLayer[1] = 9
			stage.activeLayer[2] = 9
			stage.activeLayer[3] = 9
			object.controlMode = 0
			inputDown.left 	= false
			inputDown.right = false
			object.direction = FLIP_NONE
			object.textTopPos = -384
			object.textMidPos = 384
			object.scorePos = 640
			object.p1RingPos = 656
			object.p2RingPos = 672
			object.gemPos = 688
			object.scorePos += 128
			object.p1RingPos += 128
			object.p2RingPos += 128
			object.gemPos += 128
			object.inkEffect = INK_ALPHA
			PlayMusic(1)
			object.state++
			SetScreenFade(224, 224, 224, 255)
		end if
		break
	case SPECIALSETUP_FADEIDLE
		object.timer++
		if object.timer > 15
			object.timer = 0
			object.state++
		end if
		SetScreenFade(224, 224, 224, 255)
		break
	case SPECIALSETUP_ENTERTEXT
		if object.textTopPos < 0
			object.textTopPos += 16
		end if
		if object.textMidPos > 0
			object.textMidPos -= 16
		end if
		if object.scorePos > 0
			object.scorePos -= 16
		end if
		if object.p1RingPos > 0
			object.p1RingPos -= 16
		end if
		if object.p2RingPos > 0
			object.p2RingPos -= 16
		end if
		if object.gemPos > 0
			object.gemPos -= 16
		else
			object.state++
		end if
		break
	case SPECIALSETUP_ENTEREMERALDS
		object.timer++
		if object.emeraldAlpha < 256
			object.emeraldAlpha += 8
		end if
		if object.timer > 299
			object.timer = 0
			object.state++
		end if
		break
	case SPECIALSETUP_TALLYSCORE
		if object.p1RingBonus > 0
			object.p1RingBonus--
			player.score += 100
		end if
		if object.p2RingBonus > 0
			object.p2RingBonus--
			player.score += 100
		end if
		if object.gemBonus > 0
			object.gemBonus -= 100
			player.score += 100
		end if
		if player.score >= player.scoreBonus
			player.lives++
			player.scoreBonus += 50000
			PlaySfx(SfxName[Life], false)
		end if

		CheckGreater(object.p1RingBonus, 0)
		temp0 = checkResult
		CheckGreater(object.p2RingBonus, 0)
		temp0 |= checkResult
		CheckGreater(object.gemBonus, 0)
		temp0 |= checkResult
		if temp0 == true
			object.timer++
			if object.timer == 2
				PlaySfx(SfxName[Score Add], false)
				object.timer = 0
			end if
		else
			if SpecialSetup_gotEmerald == true
				if specialStage.emeralds == 0x3FFF
					object.state = SPECIALSETUP_WINSHOWREWARD
				else
					if specialStage.emeralds == 0b01111111
						switch stage.playerListPos
						case 0
						case 2
							object.state = SPECIALSETUP_WINSHOWREWARD
							break
						case 1
							if options.superTails == true
								object.state = SPECIALSETUP_WINSHOWREWARD
							else
								object.state++
							end if
							break
						end switch
					else
						object.state++
					end if
				end if
			else
				object.state++
			end if
			object.timer = 0
			PlaySfx(SfxName[Score Total], false)
		end if
		break
	case SPECIALSETUP_FAILED
		object.timer++
		if object.timer == 160
			object.timer = 0
			object.state++
			PlaySfx(SfxName[Warp], false)
		end if
		break
	case SPECIALSETUP_FADEOUT
		if object.timer < 400
			object.timer += 8
			SetScreenFade(248, 248, 248, object.timer)
		else
			object.timer = 248
			object.state++
			SetScreenFade(248, 248, 248, 255)
		end if
		break
	case SPECIALSETUP_EXITRESULTS
		if object.timer > 0
			object.timer -= 8
		else
			fadeColor = 0
			if stage.listPos < stage.listSize
				LoadStage()
			else
				stage.activeList = PRESENTATION_STAGE
				stage.listPos = 0
				LoadStage()
			end if
		end if
		SetScreenFade(object.timer, object.timer, object.timer, 255)
		break
	case SPECIALSETUP_WINSHOWREWARD
		object.timer++
		if object.timer == 90
			object.timer = 0
			object.textBottomPos = 0
			object.state++
		end if
		break
	case SPECIALSETUP_WINENTERTEXT
		if object.textTopPos > -384
			object.textTopPos -= 32
			object.textBottomPos -= 32
		end if
		if object.textMidPos < 384
			object.textMidPos += 32
		else
			if specialStage.emeralds == 0x3FFF
				object.resultsTextTop = TEXT_CANCHANGEFORM
				object.resultsTextMid = TEXT_CANCHANGEFORM
				object.resultsTextBottom = TEXT_CANCHANGEFORM
			else
				if options.hiddenPalace == true
					object.resultsTextTop = TEXT_TOHIDDENPALACE
					object.resultsTextMid = TEXT_TOHIDDENPALACE
					object.resultsTextBottom = TEXT_TOHIDDENPALACE
				else
					object.resultsTextTop = TEXT_CANCHANGEFORM
					object.resultsTextMid = TEXT_CANCHANGEFORM
					object.resultsTextBottom = TEXT_CANCHANGEFORM
				end if
			end if
			object.state++
		end if
		break
	case SPECIALSETUP_WINEVENT
		if object.textTopPos < 0
			object.textTopPos += 16
			object.textBottomPos += 16
		end if
		if object.textMidPos > 0
			object.textMidPos -= 16
		else
			PlaySfx(SfxName[Event], false)
			object.state++
		end if
		break
	case SPECIALSETUP_WINIDLE
		object.timer++
		if object.timer == 200
			object.timer = 0
			object.state = SPECIALSETUP_FADEOUT
			PlaySfx(SfxName[Warp], false)
		end if
		break
	end switch
end event


event ObjectDraw
	switch object.state
	case SPECIALSETUP_ENTERTEXT
	case SPECIALSETUP_ENTEREMERALDS
	case SPECIALSETUP_TALLYSCORE
	case SPECIALSETUP_FAILED
	case SPECIALSETUP_FADEOUT
	case SPECIALSETUP_WINSHOWREWARD
	case SPECIALSETUP_WINENTERTEXT
	case SPECIALSETUP_WINEVENT
	case SPECIALSETUP_WINIDLE
		ClearScreen(152)

		object.animationTimer += 16
		object.animationTimer &= 0x1FF
		Sin(temp0, object.animationTimer)
		temp0 >>= 3
		temp0 += 190
		temp0 *= object.emeraldAlpha
		temp0 >>= 8
		object.alpha = temp0

		temp0 = 0
		while temp0 < 7
			GetBit(temp1, specialStage.emeralds, temp0)
			if temp1 == true
				GetTableValue(temp2, temp0, SpecialFinish_emeraldXPosTable)
				temp2 += screen.xcenter
				GetTableValue(temp3, temp0, SpecialFinish_emeraldYPosTable)
				DrawSpriteScreenFX(temp0, FX_INK, temp2, temp3)
			end if
			temp0++
			temp1++
		loop

		temp7 = object.textTopPos
		temp7 += screen.xcenter
		switch object.resultsTextTop
		default
			break

		case TEXT_ONEEMERALD
			DrawSpriteScreenXY(24, temp7, 0)
			DrawSpriteScreenXY(25, temp7, 0)
			break

		case TEXT_ALLEMERALDS_CHAOS
			DrawSpriteScreenXY(27, temp7, 0)
			DrawSpriteScreenXY(28, temp7, 0)
			break

		case TEXT_CANCHANGEFORM
			DrawSpriteScreenXY(30, temp7, 0)
			DrawSpriteScreenXY(31, temp7, 0)
			DrawSpriteScreenXY(32, temp7, 0)
			break

		case TEXT_TOHIDDENPALACE
			DrawSpriteScreenXY(36, temp7, 0)
			DrawSpriteScreenXY(37, temp7, 0)
			break
		end switch

		temp7 = object.textMidPos
		temp7 += screen.xcenter
		switch object.resultsTextMid
		default
			break

		case TEXT_FAILED
			DrawSpriteScreenXY(23, temp7, 0)
			break

		case TEXT_ONEEMERALD
			DrawSpriteScreenXY(26, temp7, 0)
			break

		case TEXT_ALLEMERALDS_CHAOS
			DrawSpriteScreenXY(29, temp7, 0)
			break

		case TEXT_CANCHANGEFORM
			DrawSpriteScreenXY(33, temp7, 0)
			break

		case TEXT_TOHIDDENPALACE
			DrawSpriteScreenXY(38, temp7, 0)
			DrawSpriteScreenXY(39, temp7, 0)
			DrawSpriteScreenXY(40, temp7, 0)
			break
		end switch

		temp7 = object.textBottomPos
		temp7 += screen.xcenter
		switch object.resultsTextBottom
		default
			break

		case TEXT_CANCHANGEFORM
			DrawSpriteScreenXY(34, temp7, 0)
			DrawSpriteScreenXY(35, temp7, 0)
			break

		case TEXT_TOHIDDENPALACE
			DrawSpriteScreenXY(41, temp7, 0)
			break
		end switch

		temp7 = object.scorePos
		temp7 += screen.xcenter
		DrawSpriteScreenXY(17, temp7, 145)

		temp7 += 88
		DrawNumbers(7, temp7, 145, player.score, 9, 8, 0)

		if stage.player2Enabled == true
			temp7 = object.p1RingPos
			temp7 += screen.xcenter
			DrawSpriteScreenXY(20, temp7, 161)

			temp7 += 88
			DrawNumbers(7, temp7, 161, object.p1RingBonus, 5, 8, 0)

			temp7 = object.p2RingPos
			temp7 += screen.xcenter
			DrawSpriteScreenXY(21, temp7, 177)

			temp7 += 88
			DrawNumbers(7, temp7, 177, object.p2RingBonus, 5, 8, 0)

			if SpecialSetup_gotEmerald == true
				temp7 = object.gemPos
				temp7 += screen.xcenter
				DrawSpriteScreenXY(22, temp7, 193)

				temp7 += 88
				DrawNumbers(7, temp7, 193, object.gemBonus, 5, 8, 0)
			end if
		else
			temp7 = object.p1RingPos
			temp7 += screen.xcenter
			DrawSpriteScreenXY(18, temp7, 169)

			temp7 += 88
			DrawNumbers(7, temp7, 169, object.p1RingBonus, 5, 8, 0)

			if SpecialSetup_gotEmerald == true
				temp7 = object.p2RingPos
				temp7 += screen.xcenter
				DrawSpriteScreenXY(19, temp7, 193)

				temp7 += 88
				DrawNumbers(7, temp7, 193, object.gemBonus, 5, 8, 0)
			end if
		end if
		break
	end switch
end event


event ObjectStartup
	LoadSpriteSheet("Special/Objects.gif")
	
	//Chaos Emerald Frames
	SpriteFrame(-8, -8, 16, 16, 1, 260)		// Chaos Emerald 1 #0
	SpriteFrame(-8, -8, 16, 16, 18, 260)	// Chaos Emerald 2 #1
	SpriteFrame(-8, -8, 16, 16, 35, 260)	// Chaos Emerald 3 #2
	SpriteFrame(-8, -8, 16, 16, 52, 260)	// Chaos Emerald 4 #3
	SpriteFrame(-8, -8, 16, 16, 69, 260)	// Chaos Emerald 5 #4
	SpriteFrame(-8, -8, 16, 16, 86, 260)	// Chaos Emerald 6 #5
	SpriteFrame(-8, -8, 16, 16, 103, 260)	// Chaos Emerald 7 #6

	//Number Frames
	SpriteFrame(0, 0, 8, 11, 1, 361) 		// 0 - #7
	SpriteFrame(0, 0, 8, 11, 10, 361)		// 1 - #8
	SpriteFrame(0, 0, 8, 11, 19, 361)		// 2 - #9
	SpriteFrame(0, 0, 8, 11, 28, 361)		// 3 - #10
	SpriteFrame(0, 0, 8, 11, 37, 361)		// 4 - #11
	SpriteFrame(0, 0, 8, 11, 46, 361)		// 5 - #12
	SpriteFrame(0, 0, 8, 11, 55, 361)		// 6 - #13
	SpriteFrame(0, 0, 8, 11, 64, 361)		// 7 - #14
	SpriteFrame(0, 0, 8, 11, 73, 361)		// 8 - #15
	SpriteFrame(0, 0, 8, 11, 82, 361)		// 9 - #16

	SpriteFrame(-96, 0, 44, 15, 1, 345) 	// score text - #17
	SpriteFrame(-96, 0, 44, 15, 46, 345) 	// rings text - #18
	SpriteFrame(-96, 0, 84, 15, 91, 345) 	// gems bonus text - #19
	SpriteFrame(-96, 0, 92, 15, 176, 345) 	// sonic rings text - #20
	if options.region == 0
		SpriteFrame(-96, 0, 92, 15, 269, 345) // tails rings text - #21
	else
		SpriteFrame(-96, 0, 92, 15, 269, 329) // miles rings text - #21
	end if
	SpriteFrame(-96, 0, 92, 15, 362, 345) // gems bonus (again) - #22
	SpriteFrame(-96, 50, 187, 16, 1, 277) // special stage text - #23

	//"[Player] Got A Chaos Emerald" Frames
	switch stage.playerListPos
	case PlayerName[SONIC]
		SpriteFrame(-72, 32, 72, 16, 189, 277) 	// sonic text - #24
		SpriteFrame(8, 32, 72, 16, 412, 277) 	// "got a" text - #25
		break

	case PlayerName[TAILS]
		if options.region == 0
			SpriteFrame(-69, 32, 69, 16, 262, 277) 	// tails text - #24
		else
			SpriteFrame(-79, 32, 79, 16, 332, 277) 	// miles text - #24
		end if
		SpriteFrame(8, 32, 72, 16, 412, 277) 		// "got a" text - #25
		break

	case PlayerName[KNUCKLES]
		SpriteFrame(-104, 32, 128, 16, 1, 294) 	// knuckles text - #24
		SpriteFrame(32, 32, 72, 16, 412, 277) 	// "got a" text - #25
		break

	end switch
	SpriteFrame(-104, 50, 208, 16, 130, 294) // "chaos emerald" text - #26

	//"[Player] Has All The Chaos Emeralds" Frames
	switch stage.playerListPos
	case PlayerName[SONIC]
		SpriteFrame(-120, 32, 72, 16, 189, 277)	// sonic text - #37
		SpriteFrame(-40, 32, 159, 16, 162, 311) // "has all the" text - #28
		break

	case PlayerName[TAILS]
		if options.region == 0
			SpriteFrame(-117, 32, 69, 16, 262, 277) 	// tails text - #27
		else
			SpriteFrame(-127, 32, 79, 16, 332, 277) 	// miles text - #27
		end if
		SpriteFrame(-40, 32, 159, 16, 162, 311)			// "has all the" text - #28
		break

	case PlayerName[KNUCKLES]
		SpriteFrame(-148, 32, 128, 16, 1, 294) 	// knuckles text - #27
		SpriteFrame(-12, 32, 159, 16, 162, 311)	// "has all the" text - #28
		break

	end switch
	SpriteFrame(-112, 50, 224, 16, 130, 294) // "chaos emeralds" text - #29

	//"Now [Player] Can be Super/Hyper [Player]"
	switch stage.playerListPos
	case PlayerName[SONIC]
		SpriteFrame(-96, 20, 55, 16, 355, 294) 	// "now" text - #30
		SpriteFrame(-32, 20, 72, 16, 189, 277)	// sonic text - #31
		SpriteFrame(48, 20, 48, 16, 411, 294) 	// "can" text - #32
		break

	case PlayerName[TAILS]
		SpriteFrame(-96, 20, 55, 16, 355, 294)		// "now" text - #30
		if options.region == 0
			SpriteFrame(-32, 20, 69, 16, 262, 277)	// tails text - #31
		else
			SpriteFrame(-32, 20, 79, 16, 332, 277)	// miles text - #31
		end if
		SpriteFrame(48, 20, 48, 16, 411, 294)		// "can" text - #32
		break

	case PlayerName[KNUCKLES]
		SpriteFrame(-124, 20, 55, 16, 355, 294)	// "now" text - #30
		SpriteFrame(-60, 20, 128, 16, 1, 294)	// knuckles text - #31
		SpriteFrame(76, 20, 48, 16, 411, 294)	// "can" text - #32
		break

	end switch
	SpriteFrame(-80, 38, 160, 16, 1, 311) 	// "go" text - #33

	if specialStage.emeralds <= 0b01111111
		temp0 = 322 //super
	else
		temp0 = 403 //hyper
	end if

	switch stage.playerListPos
	case PlayerName[SONIC]
		SpriteFrame(-80, 56, 80, 16, temp0, 311) 	// super/hyper text - #34
		SpriteFrame(8, 56, 72, 16, 189, 277)		// sonic text - #35
		break

	case PlayerName[TAILS]
		SpriteFrame(-80, 56, 80, 16, temp0, 311)	// super/hyper text - #34
		if options.region == 0
			SpriteFrame(8, 56, 69, 16, 262, 277)	// tails text - #35
		else
			SpriteFrame(8, 56, 79, 16, 332, 277)	// miles text - #35
		end if
		break

	case PlayerName[KNUCKLES]
		SpriteFrame(-108, 56, 80, 16, temp0, 311)	// super/hyper text - #34
		SpriteFrame(-20, 56, 128, 16, 1, 294)		// knuckles text - #35
		break

	end switch

	//"Now [Player] Can Go To Hidden Palace" frames
	switch stage.playerListPos
	case PlayerName[SONIC]
		SpriteFrame(-72, 20, 55, 16, 355, 294) 	// "now" text - #36
		SpriteFrame(0, 20, 72, 16, 189, 277) 	// sonic text - #37
		break

	case PlayerName[TAILS]
		SpriteFrame(-72, 20, 55, 16, 355, 294)		// "now" text - #36
		if options.region == 0
			SpriteFrame(0, 20, 69, 16, 262, 277)	// tails text - #37
		else
			SpriteFrame(0, 20, 79, 16, 332, 277)	// miles text - #37
		end if
		break
	case PlayerName[KNUCKLES]

		SpriteFrame(-100, 20, 55, 16, 355, 294)	// "now" text - #36
		SpriteFrame(-28, 20, 128, 16, 1, 294)	// knuckles text - #37
		break
	end switch
	SpriteFrame(-72, 38, 48, 16, 411, 294)	// "can" - #38
	SpriteFrame(-8, 38, 32, 16, 1, 328) 	// "go" - #39
	SpriteFrame(40, 38, 32, 16, 34, 328) 	// "to" - #40
	SpriteFrame(-100, 56, 200, 16, 67, 328) // "hidden palace" - #41
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
