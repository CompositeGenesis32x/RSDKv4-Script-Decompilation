// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Rexon Shot Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------


event ObjectUpdate
	object.xpos += object.xvel
	object.ypos += object.yvel

	// Check if the object's out of screen range and unload it if so
	// (Don't know why a normal object.outOfBounds check couldn't have just been used...)
	temp0 = object.xpos
	temp0 >>= 16
	temp0 -= camera[0].xpos
	Abs(temp0)
	temp0 -= 32

	temp1 = object.ypos
	temp1 >>= 16
	temp1 -= camera[0].ypos
	Abs(temp1)
	temp1 -= 32

	if temp0 >= screen.xcenter
		object.type = TypeName[Blank Object]
	end if
	if temp1 >= screen.ycenter
		object.type = TypeName[Blank Object]
	end if

	// Check collision with players
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_TOUCH, object.entityPos, -4, -4, 4, 4, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)

		if checkResult == true
			CallFunction(PlayerObject_ProjectileHit)
		end if
	next
end event


event ObjectDraw
	DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
end event


event ObjectStartup
	CheckCurrentStageFolder("Zone05")
	if checkResult == true
		LoadSpriteSheet("HTZ/Objects.gif")
		SpriteFrame(-4, -4, 8, 8, 36, 54)
	else
		LoadSpriteSheet("MBZ/Objects.gif")
		SpriteFrame(-4, -4, 8, 8, 36, 54)
	end if
end event


event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	CheckCurrentStageFolder("Zone05")
	if checkResult == true
		LoadSpriteSheet("HTZ/Objects.gif")
		SpriteFrame(-4, -4, 8, 8, 36, 54)
	else
		LoadSpriteSheet("MBZ/Objects.gif")
		SpriteFrame(-4, -4, 8, 8, 36, 54)
	end if

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
