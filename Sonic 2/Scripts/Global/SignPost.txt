// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Sign Post Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

// Variables
//value0 is unused...
private alias object.value1 : object.spinCount
private alias object.value2 : object.spinTimer
private alias object.value3 : object.activePlayers
private alias object.value4 : object.allPlayersFinished
private alias object.value5 : object.lastPlayerID

// States
private alias 0 : SIGNPOST_IDLE
private alias 1 : SIGNPOST_SPUN
private alias 2 : SIGNPOST_EXITPLAYER
private alias 3 : SIGNPOST_IDLE_VS
private alias 4 : SIGNPOST_SPUN_VS
private alias 5 : SIGNPOST_UNUSED
private alias 6 : SIGNPOST_FINISHED

// Player Aliases
private alias object.type 			: player.type
private alias object.propertyValue 	: player.character
private alias object.xpos 			: player.xpos
private alias object.ypos 			: player.ypos
private alias object.speed 			: player.speed
private alias object.xvel 			: player.xvel
private alias object.controlMode 	: player.controlMode
private alias object.interaction 	: player.interaction
private alias object.jumpPress 		: player.jumpPress
private alias object.jumpHold 		: player.jumpHold
private alias object.up 			: player.up
private alias object.down 			: player.down
private alias object.left 			: player.left
private alias object.right 			: player.right
private alias object.value21 		: player.acceleration

#platform: USE_DECOMP
// Alias only used in decomp-exclusive code
private alias object.ixpos : player.ixpos
#endplatform

private alias 0 : CHARACTER_SONIC
private alias 1 : CHARACTER_TAILS
private alias 2 : CHARACTER_KNUCKLES

private alias 1 : SUPERSTATE_SUPER
private alias 2 : SUPERSTATE_FADEOUT

// VSGame aliases
private alias object.value0 : vsGame.timer
private alias object.value5 : vsGame.countdownPlayer

// VSGame States
private alias 0 : VSGAME_DISPLAYTAGS
private alias 3 : VSGAME_FINISH_COUNTDOWN
private alias 4 : VSGAME_GOTOVSRESULTS

// Game Modes
private alias 2 : MODE_TIMEATTACK

// Tracks
private alias 1 : TRACK_ACTFINISH

// ========================
// Function Declarations
// ========================
reserve function SignPost_DebugDraw
reserve function SignPost_DebugSpawn

private function SignPost_DebugDraw
	DrawSprite(0)
	DrawSprite(1)
end function


private function SignPost_DebugSpawn
	CreateTempObject(TypeName[Sign Post], 0, object.xpos, object.ypos)
	object[tempObjectPos].frame = 1
	object[tempObjectPos].spinCount = 16
	if options.vsMode == true
		object[tempObjectPos].state = SIGNPOST_IDLE_VS
	end if
end function


// ========================
// Events
// ========================

event ObjectUpdate
	switch object.state
	case SIGNPOST_IDLE
		temp0 = object.ypos
		temp0 += 0x400000
		if player[0].ypos < temp0
			stage.newXBoundary1 = object.xpos
			stage.newXBoundary1 >>= 16
			stage.newXBoundary1 -= screen.xcenter
			stage.newXBoundary1 -= screen.xcenter

			stage.newXBoundary2 = object.xpos
			stage.newXBoundary2 >>= 16
			stage.newXBoundary2 += screen.xcenter

			if player[0].xpos > object.xpos
				stage.newXBoundary1 = object.xpos
				stage.newXBoundary1 >>= 16
				stage.newXBoundary1 -= screen.xcenter
				
				switch player[0].character
				case CHARACTER_SONIC
					EditFrame(5, -24, -16, 48, 32, 34, 1)
					break

				case CHARACTER_TAILS
					EditFrame(5, -24, -16, 48, 32, 34, 34)
					break

				case CHARACTER_KNUCKLES
					EditFrame(5, -24, -16, 48, 32, 34, 67)
					break
				end switch

				// No achievements in debug mode!
				if stage.debugMode == false
					if stage.activeList == BONUS_STAGE
						// Boss Attack is on the Bonus Stage list, check for the Boss Attack achievement
						if stage.minutes < 7
							CallNativeFunction2(SetAchievement, AchievementName[Scrambled Egg], 100)
						end if
					else
						if stage.listPos == StageName[R - EMERALD HILL ZONE 1]
							if stage.minutes == 0
								if stage.seconds < 35
									CallNativeFunction2(SetAchievement, AchievementName[Quick Run], 100)
								end if
							end if
						end if
					end if
				end if

				stage.timeEnabled = false
				object.state = SIGNPOST_SPUN
				PlaySfx(SfxName[Sign Post], false)
				if PlayerObject_SuperState == SUPERSTATE_SUPER
					PlayerObject_SuperState = SUPERSTATE_FADEOUT
					if stage.debugMode == false
						CallNativeFunction2(SetAchievement, AchievementName[Superstar], 100)
					end if
				end if
			end if
		end if
		break
		
	case SIGNPOST_SPUN
		music.volume--

		object.spinTimer++
		if object.spinTimer == 2
			object.spinTimer = 0
			object.frame++
			if object.frame == 5
				object.frame = 1
				Rand(temp0, 48)
				temp0 <<= 16
				temp0 += object.xpos
				temp0 -= 0x180000
				Rand(temp1, 32)
				temp1 <<= 16
				temp1 += object.ypos
				temp1 -= 0x140000
				CreateTempObject(TypeName[Ring Sparkle], 0, temp0, temp1)
				object.spinCount--
				if object.spinCount == 0
					SetBit(object.activePlayers, 0, true)
					if stage.player2Enabled == true
						SetBit(object.activePlayers, 1, true)
					end if
					object.state = SIGNPOST_EXITPLAYER
				end if
			end if
		end if
		break

	case SIGNPOST_EXITPLAYER
		temp0 = screen.xcenter
		temp0 <<= 16
		temp0 += object.xpos
		if player[0].xpos > temp0
			PlayMusic(TRACK_ACTFINISH)
			object.state = SIGNPOST_FINISHED
			object[30].type = TypeName[Act Finish]
			object[30].drawOrder = 6
			object[30].priority = PRIORITY_ACTIVE
		end if

#platform: USE_DECOMP
		// Another finish condition, new to the decomp
		//  -> On wide displays, sometimes Sonic is unable to go offscreen
		//     This means that, after he passes the signpost, the Act Results will never spawn and he'll be softlocked forever
		//     With the decomp, let's go ahead and implement a fallback so that this doesn't happen

		// Find player's truncated XPos and add 23 (the collision offset size) to it
		temp0 = player[0].xpos
		temp0 >>= 16
		temp0 += 23
		
		// First check if the player's touching the right bounds
		CheckEqual(temp0, stage.curXBoundary2)
		temp2 = checkResult
		
		// Then check that the player has their base speed (as it gets reset upon hitting the right barrier)
		CheckEqual(player[0].speed, player[0].acceleration)
		temp2 &= checkResult
		
		// And finally, a small check to make sure the results haven't already been spawned earlier this frame from the normal code above
		CheckNotEqual(object.state, SIGNPOST_FINISHED)
		temp2 &= checkResult

		// Another alt condition, for screens > 520
		//  -> There's another invisible barrier 252 pixels away from the signpost, check to see if the player's against that
		temp0 = object.xpos
		temp0 >>= 16
		temp0 += 252
		CheckEqual(temp0, player[0].ixpos)
		temp2 |= checkResult

		if temp2 == true
			PlayMusic(TRACK_ACTFINISH)
			object.state = SIGNPOST_FINISHED
			object[30].type = TypeName[Act Finish]
			object[30].drawOrder = 6
			object[30].priority = PRIORITY_ACTIVE
		end if
#endplatform
		break

	case SIGNPOST_IDLE_VS
		stage.newXBoundary2 = object.xpos
		stage.newXBoundary2 >>= 16
		stage.newXBoundary2 += screen.xcenter
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			GetBit(temp1, object.activePlayers, currentPlayer)
			if temp1 == false
				if player[currentPlayer].xpos > object.xpos
					object.priority = PRIORITY_ACTIVE
					
					SetBit(object.activePlayers, currentPlayer, true)
					
					if currentPlayer == 0
						if PlayerObject_SuperState == SUPERSTATE_SUPER
							PlayerObject_SuperState = SUPERSTATE_FADEOUT
						end if

						if vs.playerID == 0
							temp2 = 0
						else
							temp2 = 1
						end if
					else
						if vs.playerID == 0
							temp2 = 1
						else
							temp2 = 0
						end if
					end if

					if temp2 == 0
						vs.time1P = stage.milliSeconds
						vs.time1P = stage.seconds
						vs.time1P *= 100
						temp2 = stage.minutes
						temp2 *= 6000
						vs.time1P += temp2
						vs.time1P += stage.milliSeconds
					else
						vs.time2P = stage.milliSeconds
						vs.time2P = stage.seconds
						vs.time2P *= 100
						temp2 = stage.minutes
						temp2 *= 6000
						vs.time2P += temp2
						vs.time2P += stage.milliSeconds
					end if

					object.lastPlayerID = currentPlayer
					object.spinCount = 16
					object.state = SIGNPOST_SPUN_VS
					PlaySfx(SfxName[Sign Post], false)

					if object.allPlayersFinished == false
						PlaySfx(SfxName[Sign Post 2P], false)
						object[30].state = VSGAME_FINISH_COUNTDOWN
						vsGame[30].countdownPlayer = currentPlayer
						vsGame[30].countdownPlayer ^= 1
						switch player[currentPlayer].character
						case 0
							EditFrame(5, -24, -16, 48, 32, 34, 1)
							break
						case 1
							EditFrame(5, -24, -16, 48, 32, 34, 34)
							break
						case 2
							EditFrame(5, -24, -16, 48, 32, 34, 67)
							break
						end switch
					else
						object[30].state = VSGAME_DISPLAYTAGS
					end if

					if object.activePlayers == 3
						object.allPlayersFinished	= true
						object[30].state 			= VSGAME_DISPLAYTAGS
					end if
				end if
			end if
		next
		break

	case SIGNPOST_SPUN_VS
		if object.allPlayersFinished == true
			music.volume--
		else
			foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
				GetBit(temp1, object.activePlayers, currentPlayer)
				if temp1 == false
					if player[currentPlayer].xpos > object.xpos
						object.spinTimer	= 1
						object.frame 	 	= 4
						object.spinCount 	= 1
					end if
				end if
			next
		end if

		object.spinTimer++
		if object.spinTimer == 2
			object.spinTimer = 0
			object.frame++
			if object.frame == 5
				object.frame = 1
				Rand(temp0, 48)
				temp0 <<= 16
				temp0 += object.xpos
				temp0 -= 0x180000
				Rand(temp1, 32)
				temp1 <<= 16
				temp1 += object.ypos
				temp1 -= 0x140000
				CreateTempObject(TypeName[Ring Sparkle], 0, temp0, temp1)
				
				object.spinCount--
				if object.spinCount == 0
					if object.allPlayersFinished == false
						object.allPlayersFinished = true
						object.state = SIGNPOST_IDLE_VS
						if object.lastPlayerID == 0
							screen.cameraTarget 		= 1
							object[1].scrollTracking 	= true
							object[9].type = TypeName[Blank Object]
							CreateTempObject(TypeName[VS Game], 0, object.xpos, object.ypos)
							vsGame[tempObjectPos].timer = 384
							object[tempObjectPos].state = 2
							screen.cameraX = object[1].xpos
							screen.cameraX >>= 16
							screen.cameraY = object[1].ypos
							screen.cameraY >>= 16
						end if
					else
						object.state = SIGNPOST_FINISHED
						object[30].state = VSGAME_GOTOVSRESULTS
						vsGame[30].timer = -768
					end if
				end if
			end if
		end if
		break

	case SIGNPOST_FINISHED
		break

	default
	case SIGNPOST_UNUSED
		break
	end switch

	temp0 = 40
	temp0 += 212
	temp0 <<= 16
	temp0 += object.xpos
	currentPlayer = 0
	while currentPlayer < playerCount
		GetBit(temp1, object.activePlayers, currentPlayer)
		if temp1 == true
			player[currentPlayer].interaction 	= false
			player[currentPlayer].controlMode 	= -1
			player[currentPlayer].up 			= false
			player[currentPlayer].down 			= false
			player[currentPlayer].left 			= false
			player[currentPlayer].right 		= true
			player[currentPlayer].jumpPress 	= false
			player[currentPlayer].jumpHold 		= false
		end if

		if player[currentPlayer].xpos > temp0
			player[currentPlayer].xpos 	= temp0
			player[currentPlayer].speed = 0
			player[currentPlayer].xvel 	= 0
		end if

		if player[currentPlayer].type == TypeName[Debug Mode]
			SetBit(object.activePlayers, currentPlayer, false)
		end if
		currentPlayer++
	loop
end event


event ObjectDraw
	DrawSprite(0)
	if object.spinCount > 8
		DrawSprite(object.frame)
	else
		temp0 = object.frame
		temp0 += 4
		DrawSprite(temp0)
	end if
end event


event ObjectStartup
	LoadSpriteSheet("Global/Items2.gif")
	SpriteFrame(-24, 16, 48, 16, 34, 132)
	SpriteFrame(-24, -16, 48, 32, 34, 100)
	SpriteFrame(-16, -16, 32, 32, 149, 99)
	SpriteFrame(-4, -16, 8, 32, 247, 2)
	SpriteFrame(-16, -16, 32, 32, 116, 99)
	SpriteFrame(-24, -16, 48, 32, 34, 1)
	SpriteFrame(-16, -16, 32, 32, 149, 99)
	SpriteFrame(-4, -16, 8, 32, 247, 2)
	SpriteFrame(-16, -16, 32, 32, 116, 99)

	foreach (TypeName[Sign Post], arrayPos0, ALL_ENTITIES)
		CheckGreater(object[arrayPos0].propertyValue, 0)
		temp0 = checkResult
		CheckEqual(options.vsMode, false)
		temp0 &= checkResult
		if temp0 == true
			object[arrayPos0].type = TypeName[Blank Object]
		else
			object[arrayPos0].frame = 1
			object[arrayPos0].spinCount = 16
			if object[arrayPos0].propertyValue != 2
				object[arrayPos0].priority = PRIORITY_XBOUNDS
			end if
		end if

		if options.vsMode == true
			object[arrayPos0].state = SIGNPOST_IDLE_VS
		end if
	next

	SetTableValue(TypeName[Sign Post], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(SignPost_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(SignPost_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event


// ========================
// Editor Events
// ========================

event RSDKEdit
	if editor.returnVariable == true
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			checkResult = object.propertyValue
			break
		case 0 // type
			checkResult = object.propertyValue
			break
		end switch
	else
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			object.propertyValue = editor.variableValue
			break
		case 0 // type
			object.propertyValue = editor.variableValue
			break
		end switch
	end if
end event


event RSDKDraw
	DrawSprite(0)
	DrawSprite(1)
end event


event RSDKLoad
	LoadSpriteSheet("Global/Items2.gif")
	SpriteFrame(-24, 16, 48, 16, 34, 132)
	SpriteFrame(-24, -16, 48, 32, 34, 100)

	AddEditorVariable("type")
	SetActiveVariable("type")
	AddEnumVariable("Normal", 0)
	AddEnumVariable("VS Only (No Y Bounds)", 1)
	AddEnumVariable("VS Only (Use Y Bounds)", 2)
end event
