// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Death Event Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias object.value1 : object.leftTextPos
private alias object.value2 : object.rightTextPos
private alias object.value3 : object.timer

private alias 0 : DEATHEVENT_GAMEOVER
private alias 1 : DEATHEVENT_TIMEOVER
private alias 2 : DEATHEVENT_DEATH
private alias 3 : DEATHEVENT_DEATH_TA

// Player aliases
private alias object.value0 : player.rings

event ObjectMain
	switch object.state
	case DEATHEVENT_GAMEOVER
	case DEATHEVENT_TIMEOVER
		temp0 = screen.xcenter
		temp0 -= 72
		if object.leftTextPos < temp0
			object.leftTextPos += 16
			if object.leftTextPos > temp0
				object.leftTextPos = temp0
			end if
		end if

		temp0 += 80
		if object.rightTextPos > temp0
			object.rightTextPos -= 16
			if object.rightTextPos < temp0
				object.rightTextPos = temp0
			end if
		end if

		if object.timer < 288
			object.timer += 4
		else
			if object.state == 0
				recStageList = stage.activeList
				recStage = stage.listPos
				stage.activeList = PRESENTATION_STAGE
				if options.vsMode == false
					if player.continues == 0
						stage.listPos = 0
						engine.state = ENGINE_RESETGAME
						CallNativeFunction2(ShowPromoPopup, 0, "GameOverPromo")
					else
						stage.listPos = 4
					end if
				else
					stage.listPos = 3
					if vs.playerID == 0
						if object.entityPos == 0
							player.score = 0
							player.lives = 3
							vs.time1P = 60000
							vs.rings1P = 0
							vs.totalRings1P = 0
							vs.items1P = 0
						else
							player.score2 = 0
							player.lives2 = 3
							vs.time2P = 60000
							vs.rings2P = 0
							vs.totalRings2P = 0
							vs.items2P = 0
						end if
					else
						if object.entityPos == 1
							player.score2 = 0
							player.lives2 = 3
							vs.time1P = 60000
							vs.rings1P = 0
							vs.totalRings1P = 0
							vs.items1P = 0
						else
							player.score = 0
							player.lives = 3
							vs.time2P = 60000
							vs.rings2P = 0
							vs.totalRings2P = 0
							vs.items2P = 0
						end if
					end if
				end if

				starPostID = 0
				if options.gameMode == MODE_NORMAL
					arrayPos1 = options.saveSlot
					arrayPos1 <<= 3
					arrayPos1++
					if saveRAM[arrayPos1] < 3
						saveRAM[arrayPos1] = 3
					end if
					arrayPos1++
					saveRAM[arrayPos1] = 0
					arrayPos1++
					saveRAM[arrayPos1] = 50000
					player.score = 0
					WriteSaveRAM()
				end if
			else
				if options.vsMode == true
					if vs.playerID == 0
						vs.rings1P = player[0].rings
						vs.rings2P = player[1].rings
					else
						vs.rings1P = player[1].rings
						vs.rings2P = player[0].rings
					end if

					starPostID = 0
					stage.activeList = PRESENTATION_STAGE
					stage.listPos = 3
				end if

				if options.gameMode == MODE_NORMAL
					arrayPos1 = options.saveSlot
					arrayPos1 <<= 3
					arrayPos1++
					saveRAM[arrayPos1] = player.lives
					WriteSaveRAM()
				end if
			end if
			
			if object.state == DEATHEVENT_TIMEOVER
				recMilliSeconds = 0
				recSeconds = 0
				recMinutes = 0
			end if

			LoadStage()
		end if

		if object.timer > 0
			music.volume -= 2
			SetScreenFade(0, 0, 0, object.timer)
		else
			if inputPress.buttonA == true
				object.timer = 0
			end if

			if inputPress.buttonB == true
				object.timer = 0
			end if

			if inputPress.buttonC == true
				object.timer = 0
			end if

			CheckTouchRect(0, 0, screen.xsize, screen.ysize)
			if checkResult > -1
				object.timer = 0
			end if
		end if
		break

	case DEATHEVENT_DEATH
		if object.timer < 288
			music.volume -= 2
			object.timer += 4
		else
			if options.gameMode == MODE_NORMAL
				arrayPos1 = options.saveSlot
				arrayPos1 <<= 3
				arrayPos1++
				saveRAM[arrayPos1] = player.lives
				WriteSaveRAM()
			end if
			LoadStage()
		end if
		SetScreenFade(0, 0, 0, object.timer)
		break

	case DEATHEVENT_DEATH_TA
		if object.timer < 288
			music.volume -= 2
			object.timer += 4
		else
			timeAttack.result = 0
			vs.bossAttack = false
			player.score = 0
			starPostID = 0
			recMilliSeconds = 0
			recSeconds = 0
			recMinutes = 0
			stage.milliSeconds = 0
			stage.seconds = 0
			stage.minutes = 0
			engine.state = ENGINE_RESETGAME
		end if
		SetScreenFade(0, 0, 0, object.timer)
		break
	end switch
end event


event ObjectDraw
	switch object.state
	case 0
		DrawSpriteScreenXY(0, object.leftTextPos, 96)
		DrawSpriteScreenXY(2, object.rightTextPos, 96)
		break

	case 1
		DrawSpriteScreenXY(1, object.leftTextPos, 96)
		DrawSpriteScreenXY(2, object.rightTextPos, 96)
		break
	end switch
end event


event ObjectStartup
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(0, 0, 64, 16, 1, 239)
	SpriteFrame(0, 0, 56, 16, 66, 239)
	SpriteFrame(0, 0, 64, 16, 123, 239)
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
