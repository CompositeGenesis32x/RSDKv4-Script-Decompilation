// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Tails Object Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias 1 : SLOT_TAILSOBJECT

// Player aliases
private alias 0 : SLOT_PLAYER1

private alias object.xpos : player.xpos
private alias object.ypos : player.ypos
private alias object.xvel : player.xvel
private alias object.xvel : player.yvel
private alias object.direction : player.direction
private alias object.drawOrder : player.drawOrder
private alias object.visible : player.visible
private alias object.value8 : player.moveVelocity.x
private alias object.value9 : player.moveVelocity.y


event ObjectUpdate
	arrayPos0 = player[SLOT_PLAYER1].drawOrder
	screen[arrayPos0].drawListSize = 2
	GetDrawListEntityRef(temp0, arrayPos0, 0)
	SetDrawListEntityRef(object.entityPos, arrayPos0, 0)
	SetDrawListEntityRef(temp0, arrayPos0, 1)
end event


// ========================
// Events
// ========================

event ObjectDraw
	object.direction = player[SLOT_PLAYER1].direction
	object.xvel = player[SLOT_PLAYER1].xvel
	object.xvel += player[SLOT_PLAYER1].moveVelocity.x
	object.yvel = player[SLOT_PLAYER1].yvel
	object.yvel += player[SLOT_PLAYER1].moveVelocity.y

	temp0 = object.frame
	temp0 >>= 2
	CheckEqual(object.xvel, 0)
	temp1 = checkResult
	CheckEqual(object.yvel, 0)
	temp1 &= checkResult
	if temp1 == false
		ATan2(object.rotation, object.xvel, object.yvel)
		object.rotation += 16
		temp2 = 512
		temp2 -= PlayerObject_stageRotation
		temp2 >>= 1
		object.rotation += temp2
		object.rotation &= 0xFF
		object.rotation >>= 5

		switch object.rotation
		case 0
		case 8
			temp0 += 5
			object.rotation = 0
			break

		case 1
			temp0 += 8
			if object.direction == FLIP_NONE
				object.rotation = 64
			else
				object.rotation = 0
			end if
			break

		case 2
			temp0 += 5
			object.rotation = 64
			break

		case 3
			temp0 += 8
			if object.direction == FLIP_NONE
				object.rotation = 128
			else
				object.rotation = 64
			end if
			break

		case 4
			temp0 += 5
			object.rotation = 128
			break

		case 5
			temp0 += 8
			if object.direction == FLIP_NONE
				object.rotation = 192
			else
				object.rotation = 128
			end if
			break

		case 6
			temp0 += 5
			object.rotation = 192
			break

		case 7
			temp0 += 8
			if object.direction == FLIP_NONE
				object.rotation = 0
			else
				object.rotation = 192
			end if
			break
		end switch

		if object.direction == FLIP_X
			object.rotation += 128
		end if
	else
		temp0 += 5
		object.rotation = 0
	end if

	object.rotation <<= 1
	if player[SLOT_PLAYER1].visible == true
		DrawSpriteFX(temp0, FX_ROTATE, player[SLOT_PLAYER1].xpos, player[SLOT_PLAYER1].ypos)
	end if

	object.frame++
	object.frame %= 12
end event


event ObjectStartup
	if stage.playerListPos == PlayerName[TAILS]
		LoadSpriteSheet("Players/Tails1.gif")
		
		// Place the Tails Object into the scene
		object[SLOT_TAILSOBJECT].type = TypeName[Tails Object]
		object[SLOT_TAILSOBJECT].drawOrder = 7
		object[SLOT_TAILSOBJECT].priority = PRIORITY_ACTIVE
		
		SpriteFrame(-22, -8, 16, 24, 82, 199)
		SpriteFrame(-26, -8, 20, 24, 99, 199)
		SpriteFrame(-26, -8, 20, 24, 120, 199)
		SpriteFrame(-26, -8, 20, 24, 141, 199)
		SpriteFrame(-26, -8, 20, 24, 162, 199)
		SpriteFrame(-35, -8, 24, 16, 231, 166)
		SpriteFrame(-35, -8, 24, 16, 231, 183)
		SpriteFrame(-35, -8, 24, 16, 231, 200)
		SpriteFrame(-25, 9, 20, 16, 235, 217)
		SpriteFrame(-25, 9, 18, 16, 237, 234)
		SpriteFrame(-25, 9, 20, 16, 216, 234)
		SpriteFrame(-30, -6, 24, 16, 231, 166)
		SpriteFrame(-30, -6, 24, 16, 231, 183)
		SpriteFrame(-30, -6, 24, 16, 231, 200)
	end if
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Players/Tails1.gif")
	SpriteFrame(-22, -8, 16, 24, 82, 199)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
