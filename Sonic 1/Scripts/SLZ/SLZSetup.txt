// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: SLZ Setup Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias object.value0 : object.paletteTimer
private alias object.value1 : object.paletteID
private alias object.value2 : object.scrollPos

// Function declarations
reserve function SLZSetup_SpeedUpMusic
reserve function SLZSetup_SlowDownMusic

// Static Values
public value SLZSetup_value29 = 0;
private alias SLZSetup_value29 : SLZSetup_rotateTimer

// Tracks
private alias 0 : TRACK_STAGE
private alias 2 : TRACK_INVINCIBLE

// Music Events
private alias 25 : SLOT_MUSICEVENT_CHANGE
private alias 26 : SLOT_MUSICEVENT_BOSS

private alias 0 : MUSICEVENT_FADETOBOSS
private alias 1 : MUSICEVENT_FADETOSTAGE
private alias 2 : MUSICEVENT_TRANSITION

private alias 0 : MUSICEVENT_FLAG_NOCHANGE
private alias 1 : MUSICEVENT_FLAG_SPEEDUP
private alias 2 : MUSICEVENT_FLAG_SLOWDOWN

// Tables
public table SLZSetup_palTable1
	0x00E0E0, 0x00A0A0, 0x006060, 0x002020, 0x006060, 0x00A0A0
end table

public table SLZSetup_palTable2
	0x600000, 0xA00000, 0xE00000, 0xA00000, 0x600000, 0x200000
end table

public table SLZSetup_palTable3
	0x606000, 0x202000, 0x606000, 0xA0A000, 0xE0E000, 0xA0A000
end table

public table SLZSetup_replay_credits_STK
	0xBB00000, 0x4C0000
	0b00000000, 37
	0b00001000, 36
	0b00000000, 6
	0b00001000, 204
	0b00000010, 39
	0b00000000, 159
	0b00000100, 37
	0b00110100, 1
	0b00100100, 7
	0b00000100, 47
	0b00110100, 1
	0b00100100, 33
end table

function SLZSetup_SpeedUpMusic
	CheckEqual(object[SLOT_MUSICEVENT_CHANGE].type, TypeName[Music Event])
	temp0 = checkResult
	CheckEqual(object[SLOT_MUSICEVENT_CHANGE].propertyValue, 2)
	temp0 &= checkResult
	CheckEqual(stage.musicFlag, MUSICEVENT_FLAG_NOCHANGE)
	temp0 &= checkResult
	if temp0 == 0
		switch music.currentTrack
		case 0
			SetMusicTrack("Invincibility_F.ogg", TRACK_INVINCIBLE, 30436)
			SwapMusicTrack("Starlight_F.ogg", TRACK_STAGE, 67640, 8000)
			break
		case 2
			SetMusicTrack("Starlight_F.ogg", TRACK_STAGE, 67640)
			SwapMusicTrack("Invincibility_F.ogg", TRACK_INVINCIBLE, 30436, 8000)
			break
		case 4
			SetMusicTrack("Starlight_F.ogg", TRACK_STAGE, 67640)
			SetMusicTrack("Invincibility_F.ogg", TRACK_INVINCIBLE, 30436)
			break
		end switch
	else
		stage.musicFlag = MUSICEVENT_FLAG_SPEEDUP
	end if
end function


function SLZSetup_SlowDownMusic
	CheckEqual(object[SLOT_MUSICEVENT_CHANGE].type, TypeName[Music Event])
	temp0 = checkResult
	CheckEqual(object[SLOT_MUSICEVENT_CHANGE].propertyValue, 2)
	temp0 &= checkResult
	CheckEqual(stage.musicFlag, MUSICEVENT_FLAG_NOCHANGE)
	temp0 &= checkResult
	if temp0 == 0
		switch music.currentTrack
		case 0
			SetMusicTrack("Invincibility.ogg", TRACK_INVINCIBLE, 39528)
			SwapMusicTrack("Starlight.ogg", TRACK_STAGE, 84364, 12500)
			break

		case 2
			SetMusicTrack("Starlight.ogg", TRACK_STAGE, 84364)
			SwapMusicTrack("Invincibility.ogg", TRACK_INVINCIBLE, 39528, 12500)
			break

		case 4
			SetMusicTrack("Starlight.ogg", TRACK_STAGE, 84364)
			SetMusicTrack("Invincibility.ogg", TRACK_INVINCIBLE, 39528)
			break
		end switch
	else
		stage.musicFlag = MUSICEVENT_FLAG_SLOWDOWN
	end if
end function


event ObjectMain

	GetTableValue(temp0, 21, StageSetup_oscillationTable)

	if temp0 == 0
		SLZSetup_rotateTimer++
		SLZSetup_rotateTimer &= 3
	end if

	object.paletteTimer++
	if object.paletteTimer == 8
		object.paletteTimer = 0
		object.paletteID++
		object.paletteID %= 6

		GetTableValue(temp0, object.paletteID, SLZSetup_palTable1)
		SetPaletteEntry(0, 171, temp0)
		GetTableValue(temp0, object.paletteID, SLZSetup_palTable2)
		SetPaletteEntry(0, 173, temp0)
		GetTableValue(temp0, object.paletteID, SLZSetup_palTable3)
		SetPaletteEntry(0, 174, temp0)
	end if

end event


event ObjectDraw
	// Draw FG Construction Sprites
	temp0 = screen.xoffset
	temp0 <<= 1
	temp0 &= 511
	FlipSign(temp0)

	temp0 += object.scrollPos
	temp1 = screen.yoffset
	temp1 <<= 1
	temp1 &= 255
	FlipSign(temp1)
	DrawSpriteScreenXY(0, temp0, temp1)
	DrawSpriteScreenXY(1, temp0, temp1)
end event


event ObjectStartup

	LoadSpriteSheet("SLZ/Objects.gif")
	
	SetMusicTrack("Starlight.ogg", TRACK_STAGE, 84364)
	SpeedUpMusic = SLZSetup_SpeedUpMusic
	SlowDownMusic = SLZSetup_SlowDownMusic
	
	// Sprite frames
	// (Foreground pole)
	SpriteFrame(-16, 0, 32, 256, 224, 0)
	SpriteFrame(-16, 256, 32, 256, 224, 0)

	// Flicky and Picky are the ones living in these stars
	animalType1 = TypeName[Flicky]
	animalType2 = TypeName[Picky]

	object[10].type = TypeName[SLZ Setup]
	object[10].priority = PRIORITY_ACTIVE
	object[10].drawOrder = 5
	object[10].scrollPos = 496
	temp0 = screen.xcenter
	temp0 >>= 2
	object[10].scrollPos -= temp0
	SLZSetup_rotateTimer = 0

	if options.attractMode == true
		PlayerObject_ReplayTable = SLZSetup_replay_credits_STK
		PlayerObject_ReplaySize = 26
		PlayerObject_ReplayLength = 540
		CallFunction(PlayerObject_InitReplay)
	end if
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
