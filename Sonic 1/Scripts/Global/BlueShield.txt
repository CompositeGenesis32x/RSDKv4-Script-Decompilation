// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: Blue Shield Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias 24 : TYPE_BLUESHIELD

private alias object.value0 : object.timer

event ObjectMain

	object.drawOrder = -1

	// Dead player shouldn't have any shield
	if object[-playerCount].state == PlayerObject_Death
		object.type = TypeName[Blank Object]
	end if

	// Drowned player shouldn't have any shield, either
	if object[-playerCount].state == PlayerObject_Drown
		object.type = TypeName[Blank Object]
	end if

	// Update the object's animation
	object.timer++
	if object.timer == 2
		object.timer = 0
		object.frame++
		if object.frame > 5
			object.frame = 0
		end if
	end if
end event


event ObjectDraw

	// Super characters shouldn't have shields
	// Object isn't just erased in case the player untransforms
	if PlayerObject_SuperState != 1
		DrawSpriteFX(object.frame, FX_INK, object[-playerCount].xpos, object[-playerCount].ypos)
	end if

end event


event ObjectStartup

	LoadSpriteSheet("Global/Items2.gif")

	// Shield frames
	SpriteFrame(-24, -24, 48, 48, 109, 196)
	SpriteFrame(0, 0, 0, 0, 109, 196)
	SpriteFrame(-24, -24, 48, 48, 158, 196)
	SpriteFrame(0, 0, 0, 0, 109, 196)
	SpriteFrame(-24, -24, 48, 48, 207, 196)
	SpriteFrame(0, 0, 0, 0, 109, 196)

end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
