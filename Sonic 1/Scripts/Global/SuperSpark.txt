// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: Super Spark Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

//-------Aliases-------//
private alias 33 : TYPE_SUPERSPARK

private alias object.value0 	: object.timer

private alias object.xpos 		: player.xpos
private alias object.ypos 		: player.ypos
private alias object.speed 		: player.speed
private alias object.value18 	: player.sortedDrawOrder
private alias object.value42	: player.prevGravity

private alias -1 : DRAWORDER_PLAYER

event ObjectMain

	if object.propertyValue == 0
		object.xpos = player[-playerCount].xpos
		object.ypos = player[-playerCount].ypos
	end if

	switch object.state
	case 0
		if player[-playerCount].prevGravity == 0
			temp0 = player[-playerCount].speed
			Abs(temp0)

			if temp0 >= 0x80000
				object.drawOrder = DRAWORDER_PLAYER
				object.timer     = 0
				object.frame     = 0
				object.state++
			end if
		end if
		break

	case 1
		object.timer++
		if object.timer >= 3
			object.timer = 1
			object.frame++
			if object.frame >= 2
				object.state++
			end if
		end if
		break

	case 2
		object.timer++
		if object.timer >= 3
			object.timer = 1
			object.frame--
			if object.frame <= 0
				object.state++
			end if
		end if
		break

	case 3
		object.timer++
		if object.timer >= 3
			object.timer = 9
			object.state++
		end if
		break

	case 4
		if object.propertyValue == 1
			object.type = TypeName[Blank Object]
		end if

		object.timer++
		if object.timer >= 13
			temp0 = player[-playerCount].speed
			Abs(temp0)
			CheckLower(temp0, 0x80000)
			temp0 = checkResult
			CheckEqual(player[-playerCount].prevGravity, 0)
			temp0 &= checkResult
			if temp0 == true
				object.state = 0
			else
				object.timer = 0
				CreateTempObject(TypeName[Super Spark], 1, player[-playerCount].xpos, player[-playerCount].ypos)
				object[tempObjectPos].frame     = 0
				object[tempObjectPos].state     = 1
				object[tempObjectPos].drawOrder = player[-playerCount].sortedDrawOrder
			end if
		end if
		break

	end switch

end event


event ObjectDraw

	// Only draw when in states 1-3
	switch object.state
	case 1
	case 2
	case 3
		DrawSprite(object.frame)
		break

	end switch

end event


event ObjectStartup
	LoadSpriteSheet("Global/Items3.gif")
	SpriteFrame(-8, -8, 16, 16, 168, 158)
	SpriteFrame(-16, -16, 32, 32, 50, 223)
	SpriteFrame(-24, -24, 48, 48, 1, 207)
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
