// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: Invisible Block Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias 54 : TYPE_INVISIBLEBLOCK

private alias object.value0 : object.XSize
private alias object.value1 : object.YSize

// Function declarations
reserve function InvisibleBlock_DebugDraw
reserve function InvisibleBlock_DebugSpawn

function InvisibleBlock_DebugDraw
	temp0 = object.xpos
	temp1 = object.ypos
	temp0 -= 0x80000
	temp1 -= 0x80000
	DrawSpriteXY(0, temp0, temp1)
	temp0 += 0x100000
	DrawSpriteXY(0, temp0, temp1)
	temp0 -= 0x100000
	temp1 += 0x100000
	DrawSpriteXY(0, temp0, temp1)
	temp0 += 0x100000
	DrawSpriteXY(0, temp0, temp1)
end function


function InvisibleBlock_DebugSpawn
	CreateTempObject(TypeName[Invisible Block], 17, object.xpos, object.ypos)
	object[tempObjectPos].drawOrder = 5
	object[tempObjectPos].XSize = 16
	object[tempObjectPos].YSize = 16
end function


event ObjectMain
	temp0 = object.XSize
	FlipSign(temp0)
	temp1 = object.YSize
	FlipSign(temp1)
	switch object.state
	case 0
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			if object[currentPlayer].state != PlayerObject_KnuxLedgePullUp
				BoxCollisionTest(C_BOX, object.entityPos, temp0, temp1, object.XSize, object.YSize, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
				switch checkResult
				case 0
					temp0 += 2
					temp1 += 2
					temp2 = object.XSize
					temp3 = object.YSize
					temp2 -= 2
					temp3 -= 2
					BoxCollisionTest(C_TOUCH, object.entityPos, temp0, temp1, temp2, temp3, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
					if checkResult == 1
						object[currentPlayer].gravity = GRAVITY_GROUND
					end if
					break
				case 4
					if object[currentPlayer].gravity == GRAVITY_GROUND
						CallFunction(PlayerObject_Kill)
					end if
					break
				end switch
			end if
		next
		break
	case 1
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			if object[currentPlayer].state != PlayerObject_KnuxLedgePullUp
				BoxCollisionTest(C_TOUCH, object.entityPos, temp0, temp1, object.XSize, object.YSize, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
				if checkResult == 1
					if object[currentPlayer].gravity == GRAVITY_GROUND
						object[currentPlayer].xpos = object[currentPlayer].collisionRight
						FlipSign(object[currentPlayer].xpos)
						object[currentPlayer].xpos -= object.XSize
						object[currentPlayer].xpos <<= 16
						object[currentPlayer].xpos += object.xpos
						if object[currentPlayer].speed > 0
							object[currentPlayer].speed = 0
						end if
					end if
				end if
			end if
		next
		break
	case 2
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			if object[currentPlayer].state != PlayerObject_KnuxLedgePullUp
				BoxCollisionTest(C_TOUCH, object.entityPos, temp0, temp1, object.XSize, object.YSize, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
				if checkResult == 1
					if object[currentPlayer].gravity == GRAVITY_GROUND
						object[currentPlayer].xpos = object[currentPlayer].collisionLeft
						FlipSign(object[currentPlayer].xpos)
						object[currentPlayer].xpos += object.XSize
						object[currentPlayer].xpos <<= 16
						object[currentPlayer].xpos += object.xpos
						if object[currentPlayer].speed < 0
							object[currentPlayer].speed = 0
						end if
					end if
				end if
			end if
		next
		break
	end switch
end event


event ObjectDraw
	if object[0].type == TypeName[Debug Mode]
		temp0 = object.xpos
		temp1 = object.ypos
		temp2 = object.propertyValue
		temp4 = temp2
		temp4 &= 240
		temp4 <<= 15
		temp2 >>= 4
		temp2++
		temp6 = temp2
		temp3 = object.propertyValue
		temp3 &= 15
		temp5 = temp3
		temp5 <<= 19
		temp3++
		temp0 -= temp4
		temp4 = temp0
		temp1 -= temp5
		while temp3 > 0
			while temp2 > 0
				DrawSpriteXY(object.state, temp0, temp1)
				temp0 += 0x100000
				temp2--
			loop
			temp2 = temp6
			temp0 = temp4
			temp1 += 0x100000
			temp3--
		loop
	end if
end event


event ObjectStartup

	LoadSpriteSheet("Global/Display.gif")

	// Sprite frames
	SpriteFrame(-8, -7, 16, 14, 1, 176)  // 0 - Eggman face
	SpriteFrame(-8, -7, 16, 14, 17, 176) // 1 - Left arrow
	SpriteFrame(-8, -7, 16, 14, 1, 190)  // 2 - Right arrow
	// (bottom right Eggman icon is unused)

	// Do some set up for every invis block object
	foreach (TypeName[Invisible Block], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].drawOrder = 5

		object[arrayPos0].XSize = object[arrayPos0].propertyValue
		object[arrayPos0].XSize &= 240
		object[arrayPos0].XSize += 16
		object[arrayPos0].XSize >>= 1

		object[arrayPos0].YSize = object[arrayPos0].propertyValue
		object[arrayPos0].YSize &= 15
		object[arrayPos0].YSize++
		object[arrayPos0].YSize <<= 3
	next

	// Add this object to the debug object list
	// Note that it's only added once, so there aren't extra variants for different sizes or anything
	SetTableValue(TypeName[Invisible Block], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(InvisibleBlock_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(InvisibleBlock_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++

end event

event RSDKDraw
	temp0 = object.xpos
	temp1 = object.ypos
	temp2 = object.propertyValue
	temp4 = temp2
	temp4 &= 240
	temp4 <<= 15
	temp2 >>= 4
	temp2++
	temp6 = temp2
	temp3 = object.propertyValue
	temp3 &= 15
	temp5 = temp3
	temp5 <<= 19
	temp3++
	temp0 -= temp4
	temp4 = temp0
	temp1 -= temp5
	while temp3 > 0
		while temp2 > 0
			DrawSpriteXY(object.state, temp0, temp1)
			temp0 += 0x100000
			temp2--
		loop
		temp2 = temp6
		temp0 = temp4
		temp1 += 0x100000
		temp3--
	loop
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-8, -7, 16, 14, 1, 176)
	SpriteFrame(-8, -7, 16, 14, 17, 176)
	SpriteFrame(-8, -7, 16, 14, 1, 190)
end event
