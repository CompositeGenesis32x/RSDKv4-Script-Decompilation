// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: Moving Block Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias object.value0 : object.collisionOffset.x

// the script is called "SecretPlatform" but the game refers to it as "MovingPlatform"
private alias 0 : MOVINGPLATFORM_HIDDEN
private alias 1 : MOVINGPLATFORM_AWAITPLAYER
private alias 2 : MOVINGPLATFORM_MOVING_RIGHT
private alias 3 : MOVINGPLATFORM_FALLING
private alias 4 : MOVINGPLATFORM_STOPPED

// pushButtonAliases
private alias object.value0 : pushButton.stood

// Function declarations
reserve function MovingBlock_DebugDraw
reserve function MovingBlock_DebugSpawn

function MovingBlock_DebugDraw
	DrawSprite(0)
end function


function MovingBlock_DebugSpawn
	CreateTempObject(TypeName[Moving Block], 0, object.xpos, object.ypos)
	object[tempObjectPos].priority = PRIORITY_XBOUNDS
	object[tempObjectPos].state = MOVINGPLATFORM_AWAITPLAYER
end function


event ObjectMain
	switch object.state
	case MOVINGPLATFORM_HIDDEN
		if pushButton[+2].stood == true
			object.state++
		end if
		break

	case MOVINGPLATFORM_AWAITPLAYER
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			BoxCollisionTest(C_BOX, object.entityPos, -16, -8, 16, 8, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
			if checkResult == COL_TOP
				object.state++
				if stage.debugMode == false
					if currentPlayer == 0
						CallNativeFunction2(SetAchievement, AchievementName[Secret of Labyrinth Zone], 100)
					end if
				end if
			end if
		next
		break

	case MOVINGPLATFORM_MOVING_RIGHT
		temp0 = object.xpos
		temp0 &= -0x10000
		object.xpos += 0x10000

		ObjectTileCollision(CSIDE_LWALL, 16, 0, 0)
		if checkResult == true
			object.state++
		end if

		object.collisionOffset.x = object.xpos
		object.collisionOffset.x &= -0x10000
		object.collisionOffset.x -= temp0
		temp1 = object.xpos
		object.xpos = temp0

		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			BoxCollisionTest(C_BOX, object.entityPos, -16, -8, 16, 8, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
			if checkResult == COL_TOP
				object[currentPlayer].xpos += object.collisionOffset.x
			end if
		next

		object.xpos = temp1
		break

	case MOVINGPLATFORM_FALLING
		temp0 = object.ypos
		temp0 &= -0x10000
		object.ypos += object.yvel
		object.yvel += 0x3800

		ObjectTileCollision(CSIDE_FLOOR, 0, 8, 0)
		if checkResult == true
			object.state++
			object.priority = PRIORITY_ACTIVE_BOUNDS
		end if

		object.collisionOffset.x = object.ypos
		object.collisionOffset.x &= -0x10000
		object.collisionOffset.x -= temp0
		temp1 = object.ypos
		object.ypos = temp0
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			BoxCollisionTest(C_BOX, object.entityPos, -16, -8, 16, 8, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
			if checkResult == COL_TOP
				object[currentPlayer].ypos += object.collisionOffset.x
			end if

			if object[currentPlayer].gravity == GRAVITY_GROUND
				if checkResult == COL_BOTTOM
					CallFunction(PlayerObject_Kill)
				end if
			end if
		next
		object.ypos = temp1
		break

	case MOVINGPLATFORM_STOPPED
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			BoxCollisionTest(C_BOX, object.entityPos, -16, -8, 16, 8, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
		next
		break
	end switch
end event


event ObjectDraw
	if object.state > MOVINGPLATFORM_HIDDEN
		DrawSprite(0)
	end if
end event


event ObjectStartup
	LoadSpriteSheet("LZ/Objects.gif")
	SpriteFrame(-16, -8, 32, 16, 1, 1)

	foreach (TypeName[Moving Block], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].priority = PRIORITY_XBOUNDS
	next

	SetTableValue(TypeName[Moving Block], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(MovingBlock_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(MovingBlock_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
