// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Sonic Team Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases

// States
private alias object.value0 : sonicTeam.timer

private alias 0 : SONICTEAM_SETUP
private alias 1 : SONICTEAM_FADEIN
private alias 2 : SONICTEAM_SHOWLOGO
private alias 3 : SONICTEAM_FADEOUT

event ObjectMain
	switch object.state
	case SONICTEAM_SETUP
		sonicTeam.timer = 320
		object.state++
		if options.region == 1
			LoadSpriteSheet("Title/SonicTeam.gif")
		end if
		SetScreenFade(0x00, 0x00, 0x00, 0xFF)
		break
		
	case SONICTEAM_FADEIN
		if sonicTeam.timer > 0
			sonicTeam.timer -= 8
		else
			object.state++
		end if
		SetScreenFade(0x00, 0x00, 0x00, sonicTeam.timer)
		break

	case SONICTEAM_SHOWLOGO
		sonicTeam.timer++
		if sonicTeam.timer == 120
			sonicTeam.timer = 0
			object.state++
		end if
		break

	case SONICTEAM_FADEOUT
		if sonicTeam.timer < 320
			sonicTeam.timer += 8
		else
			ResetObjectEntity(object.entityPos, TypeName[Logo], 0, 0, 0)
		end if
		SetScreenFade(0x00, 0x00, 0x00, sonicTeam.timer)
		break

	end switch
end event


event ObjectDraw

	if options.region == 0
		// Draw the black BG
		DrawRect(0, 0, screen.xsize, screen.ysize, 0x00, 0x00, 0x00, 0xFF)
	end if

	// Draw either Sonic Team Presents or the secret credits screen depending on region
	DrawSpriteScreenXY(options.region, screen.xcenter, screen.ycenter)

end event


event ObjectStartup
	// Loads both here so they're both loaded in memory
	// It can then load one later and it's just an internal ID change
	LoadSpriteSheet("Title/SonicTeam.gif")
	LoadSpriteSheet("Title/Title.gif")

	SpriteFrame(-76, -24, 148, 40, 174, 259)
	SpriteFrame(-256, -128, 512, 256, 0, 0)
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Title/Title.gif")
	SpriteFrame(-76, -24, 148, 40, 174, 259)
end event