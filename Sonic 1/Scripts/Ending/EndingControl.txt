// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Ending Control Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.timer

// Player Aliases
private alias object.state : player.state
private alias object.xpos : player.xpos
private alias object.ypos : player.ypos
private alias object.speed : player.speed
private alias object.frame : player.frame
private alias object.controlMode : player.controlMode
private alias object.drawOrder : player.drawOrder
private alias object.up : player.up
private alias object.down: player.down
private alias object.left : player.left
private alias object.right : player.right
private alias object.jumpPress : player.jumpPress
private alias object.jumpHold : player.jumpHold
private alias object.value1 : player.timer


// ========================
// Events
// ========================

event ObjectUpdate
	switch object.state
	case 0
		PlayMusic(0)
		object.state++
		// [Fallthrough]
	case 1
		object[0].controlMode = CONTROLMODE_NONE
		object[0].up = false
		object[0].down = false
		object[0].left = true
		object[0].right = false
		object[0].jumpHold = false
		object[0].jumpPress = false
		
		if options.superStates == false
			// Six emeralds
			temp0 = 63
		else
			// Seven emeralds
			temp0 = 127
		end if

		if specialStage.emeralds >= temp0
			// object[+1] is a node on the right hill of the scene
			object[0].xpos = object[+1].xpos
			object[0].ypos = object[+1].ypos
		end if

		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			if currentPlayer == 0
				player[currentPlayer].speed = -0x60000
			else
				player[currentPlayer].speed = -0x60000
				player[currentPlayer].xpos = object[0].xpos
				player[currentPlayer].xpos += 0x400000
				player[currentPlayer].ypos = object[0].ypos
				StopSfx(SfxName[Skidding])
			end if
		next

		if object.timer > 0
			object.timer -= 16
		else
			temp0 = 424
			temp0 += screen.xcenter
			stage.newXBoundary2 = temp0
			object.state++
		end if

		SetScreenFade(0, 0, 0, object.timer)
		break

	case 2
		temp0 = object.xpos
		temp0 -= 0x180000
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			if player[currentPlayer].speed < -0x60000
				player[currentPlayer].speed = -0x60000
			end if

			if player[currentPlayer].xpos < temp0
				player[currentPlayer].left = false
				player[currentPlayer].right = true
			end if

			if player[currentPlayer].xpos > object.xpos
				if player[currentPlayer].right == true
					player[currentPlayer].right = false
					player[currentPlayer].speed = 0
					if currentPlayer == playerCount
						object.state++
					else
						if currentPlayer == 0
							ResetObjectEntity(currentPlayer, TypeName[Ending Pose], 0, object.xpos, object.ypos)
							player[currentPlayer].controlMode = CONTROLMODE_NONE
							player[currentPlayer].drawOrder = 6

							if options.superStates == 0
								// Six emeralds
								temp0 = 63
							else
								// Seven emeralds
								temp0 = 127
							end if

							if specialStage.emeralds < temp0
								switch stage.playerListPos
								case PLAYER_SONIC_ALONE
								case PLAYER_SONIC_AND_TAILS
									player[currentPlayer].frame = 0
									break

								case PLAYER_TAILS_ALONE
									player[currentPlayer].frame = 13
									break

								case PLAYER_KNUX_ALONE
									player[currentPlayer].frame = 24
									break
									
								end switch
							else
								switch stage.playerListPos
								case PLAYER_SONIC_ALONE
								case PLAYER_SONIC_AND_TAILS
									player[currentPlayer].frame = 4
									break

								case PLAYER_TAILS_ALONE
									player[currentPlayer].frame = 16
									break

								case PLAYER_KNUX_ALONE
									player[currentPlayer].frame = 27
									break
									
								end switch
								player[currentPlayer].state = 5
							end if

							player[currentPlayer].timer = 224
						end if
					end if
				end if
			end if
		next
		break
		
	end switch
end event


event ObjectStartup
	foreach (TypeName[Ending Control], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].priority = PRIORITY_ACTIVE
		object[arrayPos0].timer = 272
	next
end event


event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
