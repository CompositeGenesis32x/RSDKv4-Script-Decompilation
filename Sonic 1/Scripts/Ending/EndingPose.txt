// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Ending Pose Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.timer
private alias object.value1 : object.xOffset
private alias object.value2 : object.playerFrame

// Player Aliases
private alias object.state : player.state
private alias object.xpos : player.xpos
private alias object.ypos : player.ypos
private alias object.speed : player.speed
private alias object.frame : player.frame
private alias object.direction : player.direction
private alias object.up : player.up
private alias object.drawOrder : player.drawOrder
private alias object.controlMode : player.controlMode


// ========================
// Function Declarations
// ========================

reserve function EndingPose_HandleXOffset


private function EndingPose_HandleXOffset
	if object.xOffset > 0
		object.xOffset -= 16
	end if
end function


// ========================
// Events
// ========================

event ObjectUpdate
	// TODO: numeric states used here
	// don't forget the `player[currentPlayer].state = 5` line in Ending Control too
	switch object.state
	case 0
		object.state++
		if stage.playerListPos == 1
			object.playerFrame = 36
		end if

	case 1
		object.timer++
		if stage.playerListPos == 1
			object.playerFrame = object.timer
			object.playerFrame >>= 3
			object.playerFrame %= 5
			object.playerFrame += 36
		end if

		if object.timer == 240
			currentPlayer = 1
			while currentPlayer < playerCount
				ResetObjectEntity(currentPlayer, TypeName[Ending Pose], 0, player[currentPlayer].xpos, player[currentPlayer].ypos)
				player[currentPlayer].drawOrder = 6
				player[currentPlayer].state = 14
				player[currentPlayer].frame = 43
				player[currentPlayer].direction = FACING_LEFT
				currentPlayer++
			loop
			
			object.timer = 0
			switch stage.playerListPos
			case 0
			case 3
				object.frame = 1
				break
				
			case 1
				object.frame = 14
				break

			case 2
				object.frame = 25
				break
			end switch
			object.state++
			object.playerFrame = 0
		else
			currentPlayer = 1
			while currentPlayer < playerCount
				if player[currentPlayer].speed == 0
					player[currentPlayer].direction = FACING_LEFT
				end if
				currentPlayer++
			loop
		end if
		break

	case 2
		CallFunction(EndingPose_HandleXOffset)
		object.timer++
		if object.timer == 12
			object.timer = 0
			switch stage.playerListPos
			case 0
			case 3
				object.frame = 2
				object.ixpos = camera[0].xpos
				object.iypos = camera[0].ypos
				break

			case 1
			case 2
				break
			end switch
			object.state++
		end if
		break

	case 3
		CallFunction(EndingPose_HandleXOffset)
		object.timer++
		if object.timer == 4
			object.timer = -1560
			switch stage.playerListPos
			case 0
			case 3
				object.frame = 3
				break

			case 1
				object.frame = 15
				object.ixpos = camera[0].xpos
				object.iypos = camera[0].ypos
				break

			case 2
				object.frame = 26
				object.ixpos = camera[0].xpos
				object.iypos = camera[0].ypos
				break
				
			end switch
			object.state++
		end if
		break

	case 4
		if object.timer < 320
			object.timer += 12
		else
			// Roll the credits!
			stage.activeList = PRESENTATION_STAGE
			stage.listPos = 2
			
			lampPostID = 0
			recMilliSeconds = 0
			recSeconds = 0
			recMinutes = 0
			LoadStage()
		end if
		
		if object.timer > 0
			temp0 = object.timer
		else
			temp0 = 0
		end if
		SetScreenFade(0, 0, 0, temp0)
		break

	case 5
		object.timer++
		if object.timer == 80
			object.timer = 0
			object.state++
		else
			switch stage.playerListPos
			case 0
			case 3
				object.frame = 4
				break

			case 1
				object.frame = 16
				break
				
			case 2
				object.frame = 27
				break
			end switch

			currentPlayer = 1
			while currentPlayer < playerCount
				if player[currentPlayer].speed == 0
					player[currentPlayer].direction = FACING_LEFT
				end if
				currentPlayer++
			loop
		end if
		break

	case 6
		object.frame = object.timer
		object.frame >>= 2
		object.frame &= 1
		
		switch stage.playerListPos
		case 0
		case 3
			object.frame += 5
			break

		case 1
			object.frame += 17
			break
			
		case 2
			object.frame += 27
			break
			
		end switch

		object.timer++
		if object.timer == 80
			object.timer = 0
			switch stage.playerListPos
			case 0
			case 3
				object.frame = 7
				break

			case 1
				object.frame = 19
				break
				
			case 2
				object.frame = 29
				break
			end switch

			currentPlayer = 1
			while currentPlayer < playerCount
				player[currentPlayer].up = 1
				player[currentPlayer].controlMode = CONTROLMODE_NONE
				currentPlayer++
			loop
			CreateTempObject(TypeName[Emeralds], 0, object.xpos, object.ypos)
			object[tempObjectPos].drawOrder = 6
			object.state++
		else
			currentPlayer = 1
			while currentPlayer < playerCount
				if player[currentPlayer].speed == 0
					player[currentPlayer].direction = FACING_LEFT
				end if
				currentPlayer++
			loop
		end if
		break

	case 7
		break

	case 8
		if object.timer < 384
			object.timer += 6
		else
			object.timer = 256
			SetTileLayerEntry(184, 0, 2, 2)
			SetTileLayerEntry(185, 0, 3, 2)
			SetTileLayerEntry(186, 0, 2, 3)
			SetTileLayerEntry(187, 0, 3, 3)
			SetTileLayerEntry(188, 0, 4, 2)
			SetTileLayerEntry(189, 0, 5, 2)
			SetTileLayerEntry(190, 0, 4, 3)
			SetTileLayerEntry(191, 0, 5, 3)
			object.state++
		end if

		SetScreenFade(208, 255, 224, object.timer)
		break

	case 9
		if object.timer > 0
			object.timer -= 16
			SetScreenFade(208, 255, 224, object.timer)
		else
			switch stage.playerListPos
			case 0
			case 3
				object.frame = 8
				break

			case 1
				object.frame = 20
				break

			case 2
				object.frame = 30
				break
			end switch

			object.state++
			currentPlayer = 1
			while currentPlayer < playerCount
				ResetObjectEntity(currentPlayer, TypeName[Ending Pose], 0, player[currentPlayer].xpos, player[currentPlayer].ypos)
				player[currentPlayer].drawOrder = 6
				player[currentPlayer].state = 12
				player[currentPlayer].frame = 43
				player[currentPlayer].direction = FACING_LEFT
				currentPlayer++
			loop
		end if
		break

	case 10
		switch stage.playerListPos
		case 0
		case 3
			object.frame = object.timer
			object.frame /= 6
			object.frame &= 1
			object.frame += 8
			break

		case 1
			object.frame = object.timer
			object.frame /= 10
			object.frame &= 3
			object.frame += 20
			if object.timer >= 40
				object.frame = 23
			end if
			break

		case 2
			object.frame = object.timer
			object.frame /= 10
			object.frame &= 3
			object.frame += 30
			if object.timer >= 40
				object.frame = 34
			end if
			break
			
		end switch

		object.timer++
		if object.timer == 42
			object.timer = 0
			object.state++
		end if
		break

	case 11
		object.timer++
		if object.timer == 60
			object.timer = 0
			switch stage.playerListPos
			case 0
			case 3
				object.frame = 1
				break
				
			case 1
				object.frame = 14
				break
				
			case 2
				object.frame = 25
				break
			end switch

			object.state = 2
		end if
		break

	case 12
		object.frame = object.timer
		object.frame /= 10
		object.frame &= 3
		object.frame += 20
		if object.timer >= 40
			object.frame = 23
		end if
		object.timer++
		if object.timer == 42
			object.timer = 0
			object.state++
		end if
		break

	case 13
		object.timer++
		if object.timer == 60
			object.timer = 0
			object.frame = 43
			object.state++
		end if
		break

	case 14
		object.timer++
		if object.timer == 8
			object.frame++
			object.timer = 0
			object.state++
		end if
		break
		
	end switch
end event


event ObjectDraw
	if object.playerFrame != 0
		DrawSprite(object.playerFrame)
	end if
	
	DrawSpriteFX(object.frame, FX_FLIP, object.xpos, object.ypos)

	if object.entityPos == 0
		if object.state > 0
			temp0 = screen.xcenter
			temp0 -= 96
			temp0 -= object.xOffset
			if stage.player2Enabled == true
				if options.region == 1
					temp1 = 42
				else
					temp1 = 41
				end if
			else
				temp1 = 10
				if stage.playerListPos < 3
					temp1 += stage.playerListPos
				end if
			end if

			DrawSpriteScreenXY(temp1, temp0, 80)
		end if
	end if
end event


event ObjectStartup
	LoadSpriteSheet("Ending/Objects.gif")
	
	SpriteFrame(-14, -20, 30, 39, 28, 16)
	SpriteFrame(-16, -16, 32, 35, 3, 220)
	SpriteFrame(-18, -27, 44, 71, 36, 184)
	SpriteFrame(-56, -68, 174, 134, 81, 121)
	SpriteFrame(-10, -21, 24, 40, 3, 16)
	SpriteFrame(-10, -21, 24, 40, 3, 16)
	SpriteFrame(-14, -21, 28, 40, 3, 57)
	SpriteFrame(-16, -20, 29, 39, 3, 98)
	SpriteFrame(-16, -21, 32, 40, 3, 138)
	SpriteFrame(-16, -21, 32, 40, 3, 179)
	SpriteFrame(-48, 0, 96, 31, 256, 141)
	SpriteFrame(-48, -17, 96, 48, 256, 182)
	SpriteFrame(-48, -17, 96, 48, 256, 124)
	SpriteFrame(-10, -13, 24, 31, 288, 16)
	SpriteFrame(-19, -12, 33, 31, 254, 16)
	SpriteFrame(-55, -48, 111, 96, 400, 1)
	SpriteFrame(-12, -16, 27, 34, 313, 16)
	SpriteFrame(-12, -16, 27, 34, 59, 16)
	SpriteFrame(-15, -16, 30, 34, 87, 16)
	SpriteFrame(-15, -14, 31, 32, 118, 16)
	SpriteFrame(-12, -16, 25, 34, 150, 16)
	SpriteFrame(-12, -16, 25, 34, 176, 16)
	SpriteFrame(-12, -16, 25, 34, 202, 16)
	SpriteFrame(-12, -16, 25, 34, 228, 16)
	SpriteFrame(-14, -23, 27, 41, 325, 57)
	SpriteFrame(-16, -21, 28, 39, 296, 57)
	SpriteFrame(-32, -52, 64, 104, 448, 98)
	SpriteFrame(-12, -23, 24, 41, 32, 57)
	SpriteFrame(-16, -23, 28, 41, 57, 57)
	SpriteFrame(-18, -22, 45, 40, 86, 57)
	SpriteFrame(-16, -23, 33, 41, 132, 57)
	SpriteFrame(-16, -23, 33, 41, 166, 57)
	SpriteFrame(-16, -23, 33, 41, 200, 57)
	SpriteFrame(-16, -23, 33, 41, 200, 57)
	SpriteFrame(-16, -23, 27, 41, 268, 57)
	SpriteFrame(-16, -23, 33, 41, 234, 57)
	SpriteFrame(-21, -6, 16, 24, 256, 99)
	SpriteFrame(-25, -6, 20, 24, 273, 99)
	SpriteFrame(-25, -6, 20, 24, 294, 99)
	SpriteFrame(-25, -6, 20, 24, 315, 99)
	SpriteFrame(-25, -6, 20, 24, 336, 99)
	SpriteFrame(-48, 0, 96, 40, 256, 141)
	SpriteFrame(-48, 0, 96, 40, 256, 199)
	SpriteFrame(-10, -13, 22, 31, 58, 99)
	SpriteFrame(-20, -17, 36, 35, 44, 131)
end event


event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
