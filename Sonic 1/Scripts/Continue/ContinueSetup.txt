// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: Continue Setup Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias 39 : TYPE_CONTINUESETUP

private alias object.value0 : continueSetup.fadeValue

// Player aliases
private alias object.state			:	player.state
private alias object.type			:	player.type
private alias object.speed			:	player.speed
private alias object.xpos			:	player.xpos
private alias object.ypos			:	player.ypos
private alias object.yvel			:	player.yvel
private alias object.xvel			:	player.xvel
private alias object.animation		:	player.animation
private alias object.animationSpeed :	player.animationSpeed

event ObjectMain

	switch object.state
	case 0
		continueSetup.fadeValue = 320
		SetScreenFade(0, 0, 0, continueSetup.fadeValue)
		object.state++
		break

	case 1
		if continueSetup.fadeValue > 0
			continueSetup.fadeValue -= 8
		else
			player[0].yvel = 0x30000
			if player[1].type != TypeName[Blank Object]
				player[1].yvel = 0x30000
			end if

			PlayMusic(0)
			object.state++
		end if
		SetScreenFade(0, 0, 0, continueSetup.fadeValue)
		break

	case 2
		temp0 = 0
		switch stage.playerListPos
		case PlayerName[SONIC]
		case PlayerName[SONIC AND TAILS]
			temp1 = 0xAC0000
			break

		case PlayerName[TAILS]
			temp1 = 0xB10000
			break

		case PlayerName[KNUCKLES]
			temp1 = 0xA80000
			break

		end switch

		temp2 = temp1
		temp2 += 0x80000
		player[0].animationSpeed = 60
		if player[0].ypos >= temp2
			player[0].ypos = temp1
			player[0].yvel = 0
			player[0].animation = ANI_CONTINUE

			temp0++
		end if

		if player[1].type == TypeName[Blank Object]
			temp0++
		else
			player[1].animationSpeed = 60

			if player[1].ypos >= 0xB90000
				player[1].ypos = 0xB10000
				player[1].yvel = 0
				player[1].animation = ANI_CONTINUE
				temp0++
			end if
		end if

		if temp0 == 2
			object.state++
			object[+2].state++
		end if
		break
	case 3
		CheckTouchRect(0, 0, screen.xsize, screen.ysize)
		if checkResult > -1
			inputPress.start = 1
		end if
		if inputPress.start == 1
			player[0].animation = ANI_CONTINUE_UP
			player[0].speed = 0
			if stage.playerListPos == 2
				player[0].ypos -= 0x20000
			else
				player[0].ypos -= 0x60000
			end if
			PlaySfx(SfxName[Charge], 0)
			object.state++
			object[+2].state++
		end if
		break
	case 4
		if music.volume > 0
			music.volume--
		end if
		player[0].speed += 0x2000
		if player[0].speed == 0x90000
			player[0].xvel = 0x100000
			if player[1].type == TypeName[Blank Object]
				object.state++
			end if
		end if
		if player[1].type != TypeName[Blank Object]
			if player[0].speed == 0x20000
				player[1].animation = ANI_CONTINUE_UP
				player[1].speed = 0
				player[1].ypos -= 0x60000
				PlaySfx(SfxName[Charge], 0)
			end if
			if player[0].speed > 0x20000
				player[1].speed += 0x2000
				if player[1].speed == 0x90000
					player[1].xvel = 0x100000
					object.state++
				end if
			end if
		end if
		break
	case 5
		if music.volume > 0
			music.volume--
		end if
		temp0 = screen.xsize
		temp0 += 32
		if player[1].type == TypeName[Blank Object]
			temp1 = player[0].xpos
		else
			temp1 = player[1].xpos
		end if
		temp1 >>= 16
		if temp1 >= temp0
			object.state++
		end if
		break
	case 6
		if music.volume > 0
			music.volume--
		end if
		continueSetup.fadeValue += 4
		SetScreenFade(0, 0, 0, continueSetup.fadeValue)
		if continueSetup.fadeValue == 384
			player.continues--
			player.lives = 3
			fadeColor = 0
			continueSetup.fadeValue = 0
			lampPostID = 0
			object.direction = FLIP_NONE
			stage.activeList = recStageList
			stage.listPos = recStage
			LoadStage()
		end if
		break
	case 7
		continueSetup.fadeValue += 4
		SetScreenFade(0, 0, 0, continueSetup.fadeValue)
		if continueSetup.fadeValue == 384
			fadeColor = 0
			continueSetup.fadeValue = 0
			lampPostID = 0
			object.direction = FLIP_NONE
			engine.state = 8
		end if
		break
	end switch
	options.touchControls = false
end event


event ObjectDraw
	ClearScreen(1)
end event


event ObjectStartup

	foreach (TypeName[Continue Setup], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].drawOrder = 0
	next

	player[0].xpos = screen.xcenter
	player[0].xpos <<= 16
	player[0].animation = ANI_FANROTATE
	player[0].state = PlayerObject_HandleRunSpd2
	player[0].yvel = 0x30000

	// Is P2 there?
	if player[1].type != TypeName[Blank Object]
		// Adjust the scene to make room for P2
		player[1].xpos = screen.xcenter
		player[1].xpos <<= 16
		player[1].animation = ANI_FANROTATE
		player[1].state = PlayerObject_HandleRunSpd2
		player[1].yvel = 0x30000
		player[0].xpos += 0x200000
		player[1].xpos -= 0x200000
		player[1].ypos += 0x50000
	end if

	switch stage.playerListPos
	case PlayerName[SONIC]
	case PlayerName[SONIC AND TAILS]
		break
	case PlayerName[TAILS]
		player[0].ypos += 0x50000
		break
	case PlayerName[KNUCKLES]
		player[0].ypos -= 0x40000
		break
	end switch

	stage.newXBoundary2 = screen.xsize
	stage.newYBoundary2 = screen.ysize

	SetMusicTrack("Continue.ogg", 0, 0)

	stage.pauseEnabled = 0

	if player.continues == 0
		player.continues = 3
	end if

	options.touchControls = 0

end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
