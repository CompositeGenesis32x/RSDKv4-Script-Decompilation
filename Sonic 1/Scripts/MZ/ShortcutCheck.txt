// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Shortcut Check Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias 0 : SLOT_PLAYER1

// ========================
// Events
// ========================

event ObjectUpdate
	// Check interaction with players
	BoxCollisionTest(C_TOUCH, object.entityPos, -16, -64, 16, 64, SLOT_PLAYER1, C_BOX, C_BOX, C_BOX, C_BOX)
	if checkResult == true
		if stage.debugMode == false
			CallNativeFunction2(SetAchievement, AchievementName[Secret of Marble Zone], 100)
		end if
		
		// This object serves no purpose anymore, so we may as well clear it now
		object.type = TypeName[Blank Object]
	end if
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
	
	if (editor.showGizmos == true)
		// Draw the sensor hitbox
		temp0 = object.ixpos; temp0 -= 16
		temp1 = object.iypos; temp1 -= 64
		DrawRectOutline(temp0, temp1, 32, 128, 255, 255, 255, 255)
	end if
end event


event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-8, -8, 16, 16, 239, 239) // "Trigger"
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
