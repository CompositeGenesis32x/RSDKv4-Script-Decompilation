// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Button Pillar Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================
private alias object.value0 : object.timer
private alias object.value1 : object.startPos.y
private alias object.value2 : object.shinePos.y

private alias 0 : BUTTONPILLAR_AWAITBUTTON
private alias 1 : BUTTONPILLAR_MOVING
private alias 2 : BUTTONPILLAR_MOVED

// PushButton aliases
private alias object.propertyValue : pushButton.stood

// ========================
// Function Declarations
// ========================
reserve function ButtonPillar_DebugDraw
reserve function ButtonPillar_DebugSpawn

function ButtonPillar_DebugDraw
	DrawSprite(0)
end function


function ButtonPillar_DebugSpawn
	CreateTempObject(TypeName[Button Pillar], 0, object.xpos, object.ypos)
	object[tempObjectPos].startPos.y = object.ypos
end function


event ObjectMain
	switch object.state
	case BUTTONPILLAR_AWAITBUTTON
		if pushButton[-1].stood == true
			object.state = BUTTONPILLAR_MOVING
			object.priority = PRIORITY_ACTIVE
		end if
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			BoxCollisionTest(C_BOX, object.entityPos, -32, -56, 32, 56, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
		next
		break

	case BUTTONPILLAR_MOVING
		if object.timer < 144
			object.timer += 2
			foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
				BoxCollisionTest(C_BOX, object.entityPos, -32, -56, 32, 56, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
				if checkResult == COL_TOP
					object[currentPlayer].ypos += 0x20000
				end if
			next
			object.ypos += 0x20000
		else
			foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
				BoxCollisionTest(C_BOX, object.entityPos, -32, -56, 32, 56, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
			next
			object.state++
		end if
		break

	case BUTTONPILLAR_MOVED
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			BoxCollisionTest(C_BOX, object.entityPos, -32, -56, 32, 56, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
		next
		break
	end switch

	object.angle = oscillation
	object.angle <<= 1
	Sin(object.shinePos.y, object.angle)
	object.shinePos.y <<= 12
	object.shinePos.y += object.ypos
	object.shinePos.y &= 0xFFFF0000

	if object.outOfBounds == true
		temp0 = object.ypos
		object.ypos = object.startPos.y
		if object.outOfBounds == true
			object.priority = PRIORITY_ACTIVE_BOUNDS
			object.state = BUTTONPILLAR_AWAITBUTTON
			object.timer = 0
		else
			object.ypos = temp0
		end if
	end if
end event


event ObjectDraw
	DrawSprite(0)
	DrawSpriteXY(1, object.xpos, object.shinePos.y)
end event


event ObjectStartup
	LoadSpriteSheet("MZ/Objects.gif")
	SpriteFrame(-32, -56, 64, 112, 126, 1)
	SpriteFrame(-16, -16, 31, 32, 159, 114)

	foreach (TypeName[Button Pillar], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].startPos.y = object[arrayPos0].ypos
	next

	SetTableValue(TypeName[Button Pillar], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(ButtonPillar_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(ButtonPillar_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event

event RSDKDraw
	DrawSprite(0)
	DrawSprite(1)

	// Preview
	object.inkEffect = INK_BLEND
	temp1 = 0x20000
	temp1 *= 144
	temp1 += object.ypos
	DrawSpriteFX(0, FX_INK, object.xpos, temp1)
	DrawSpriteFX(1, FX_INK, object.xpos, temp1)
end event

event RSDKLoad
	LoadSpriteSheet("MZ/Objects.gif")
	SpriteFrame(-32, -56, 64, 112, 126, 1)
	SpriteFrame(-16, -16, 31, 32, 159, 114)
end event
