// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: Pecky Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias object.propertyValue : object.delay

private alias object.value0 : object.timer // its this in ANIMAL_WAIT
private alias object.value0 : object.bounceDir // its this in every other state
private alias object.value1 : object.randomizeDir

private alias 0 : ANIMAL_WAIT
private alias 1 : ANIMAL_FALL
private alias 2 : ANIMAL_BOUNCING_NOBOUNDS
private alias 3 : ANIMAL_BOUNCING_SWAPDIR
private alias 4 : ANIMAL_BOUNCING_PLAYERDIR
private alias 5 : ANIMAL_BOUNCING

// Function declarations
reserve function Pecky_DebugDraw
reserve function Pecky_DebugSpawn

function Pecky_DebugDraw
	DrawSprite(2)
end function


function Pecky_DebugSpawn
	CreateTempObject(TypeName[Pecky], 0, object.xpos, object.ypos)

	GetBit(temp0, object.direction, 0)
	if temp0 == false
		object[tempObjectPos].state = ANIMAL_BOUNCING_SWAPDIR
		object[tempObjectPos].xvel = 0x18000
		object[tempObjectPos].yvel = -0x30000

		Rand(checkResult, 32)
		if checkResult < 16
			object[tempObjectPos].direction = FLIP_X
			FlipSign(object[tempObjectPos].xvel)
		end if
	else
		object[tempObjectPos].state = ANIMAL_BOUNCING_PLAYERDIR
	end if
end function


event ObjectMain
	switch object.state
	case ANIMAL_WAIT
		if object.timer < object.delay
			object.timer++
		else
			object.xvel = 0x18000

			if object.randomizeDir == true
				object.drawOrder = 4

				Rand(checkResult, 32)
				if checkResult < 16
					object.direction = FLIP_X
					FlipSign(object.xvel)
				end if
			else
				object.direction = FLIP_X
				FlipSign(object.xvel)
			end if

			object.yvel = -0x40000
			object.state++
		end if
		break

	case ANIMAL_FALL
		object.ypos += object.yvel
		object.yvel += 0x3800

		if object.yvel > 0
			ObjectTileCollision(CSIDE_FLOOR, 0, 12, 0)
			if checkResult == true
				object.yvel = -0x30000
				object.state++
			end if
		end if
		break

	case ANIMAL_BOUNCING_PLAYERDIR
		if object[0].xpos < object.xpos
			object.direction = FLIP_X
		else
			object.direction = FLIP_NONE
		end if
 	// Fall through
	case ANIMAL_BOUNCING_NOBOUNDS
	case ANIMAL_BOUNCING_SWAPDIR
	case ANIMAL_BOUNCING
		object.xpos += object.xvel
		object.ypos += object.yvel
		object.yvel += 0x3800

		if object.yvel > 0
			ObjectTileCollision(CSIDE_FLOOR, 0, 12, 0)
			if checkResult == true
				if object.state == ANIMAL_BOUNCING_SWAPDIR
					if object.bounceDir == 1
						FlipSign(object.xvel)
						object.direction ^= FLIP_X
					end if
					object.bounceDir ^= 1
				end if
				
				object.yvel = -0x30000
			end if
		end if
		break
	end switch

	if object.state < ANIMAL_BOUNCING_SWAPDIR
		if object.outOfBounds == true
			object.type = TypeName[Blank Object]
		end if
	end if
end event


event ObjectDraw
	switch object.state
	case ANIMAL_WAIT
	case ANIMAL_FALL
		DrawSprite(2)
		break

	case ANIMAL_BOUNCING_NOBOUNDS
	case ANIMAL_BOUNCING_SWAPDIR
	case ANIMAL_BOUNCING_PLAYERDIR
	case ANIMAL_BOUNCING
		if object.yvel < 0
			DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		else
			DrawSpriteFX(1, FX_FLIP, object.xpos, object.ypos)
		end if
		break
	end switch
end event


event ObjectStartup
	LoadSpriteSheet("Global/Items2.gif")
	SpriteFrame(-8, -12, 16, 24, 207, 91)
	SpriteFrame(-8, -12, 16, 24, 207, 66)
	SpriteFrame(-7, -12, 16, 24, 49, 231)

	foreach (TypeName[Pecky], arrayPos0, ALL_ENTITIES)
		if object[arrayPos0].propertyValue == 1
			object[arrayPos0].state = ANIMAL_BOUNCING_SWAPDIR
			object[arrayPos0].xvel = 0x18000
			object[arrayPos0].yvel = -0x30000

			Rand(checkResult, 32)
			if checkResult < 16
				object[arrayPos0].direction = FLIP_X
				FlipSign(object[arrayPos0].xvel)
			end if
		else
			if object[arrayPos0].propertyValue == 2
				object[arrayPos0].state = ANIMAL_BOUNCING_PLAYERDIR
			end if

			if object[arrayPos0].propertyValue == 3
				object[arrayPos0].state = ANIMAL_BOUNCING
				object[arrayPos0].xvel = 0x18000
				FlipSign(object[arrayPos0].xvel)
				object[arrayPos0].direction = FLIP_X
			end if
		end if

		object[arrayPos0].propertyValue = 0
	next

	SetTableValue(TypeName[Pecky], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(Pecky_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(Pecky_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event

event RSDKEdit
	if editor.returnVariable == true
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			checkResult = object.propertyValue
			break
		case 0 // behaviour
			checkResult = object.propertyValue
			break
		end switch
	else
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			object.propertyValue = editor.variableValue
			break
		case 0 // behaviour
			object.propertyValue = editor.variableValue
			break
		end switch
	end if
end event

event RSDKDraw
	DrawSprite(2)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Items2.gif")
	SpriteFrame(-8, -12, 16, 24, 207, 91)
	SpriteFrame(-8, -12, 16, 24, 207, 66)
	SpriteFrame(-7, -12, 16, 24, 49, 231)

	AddEditorVariable("behaviour")
	SetActiveVariable("behaviour")
	AddEnumVariable("Bounce Around (No Bounds)", 0)
	AddEnumVariable("Swap direction each bounce", 1)
	AddEnumVariable("Follow Player", 2)
	AddEnumVariable("Bounce Around (Bounds)", 3)
end event
